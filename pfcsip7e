?CONSULT $SYSTEM.SYSTEM.COBOLEX0
?SEARCH  $SYSTEM.SYSTEM.COBOLLIB
?SEARCH  =TALLIB
?SEARCH  =ASC2EBC
?SEARCH  =EBC2ASC
?SEARCH  =IUMSW07
?SEARCH  =SDBCDU5
?SEARCH  =SYSAWKZ
?SEARCH  =WCAPM92
?SEARCH  =WISO107
?SEARCH  =WISO207
?SEARCH  =WSYS020
?SEARCH  =WSYS022
?SEARCH  =WSY7065
?SEARCH  =WSY7066
?SEARCH  =WSYS930
?SEARCH  =WSYS980
?SEARCH  =WSYS990
*kl20160527 - G.00.01 - Fuer Genehmigungsnummer
?SEARCH  =WSYS04T
*kl20160527 - G.00.01 - Ende
*G.01.05 - Modul für Zusammenstellung ZP-VERKAUF
?SEARCH =ZPVERK
*G.01.05 - ende

?NOLMAP, SYMBOLS, INSPECT
?SAVE ALL
?SAVEABEND
?LINES 66
?CHECK 3
?SQL


 IDENTIFICATION DIVISION.

 PROGRAM-ID. PFCSIP7 .

 DATE-COMPILED.


****************************************************************
* Letzte Aenderung :: 2018-11-26
* Letzte Version   :: G.02.01
* Kurzbeschreibung :: Standin: Die Leitung zum AS ist während
* Kurzbeschreibung :: einer Transaktion nicht aktiv. Der Anfrage-
* Kurzbeschreibung :: server leitet die Anfrage deshalb über
* Kurzbeschreibung :: die Drehscheibe WXCRD7XE an diesen Server
* Kurzbeschreibung :: PFCSIP7E weiter, indem der NEXTSERVER
* Kurzbeschreibung :: auf PFCSIP7E gesetzt wird.
* Package          :: ICC
* Auftrag          :: R7-269
*
* Aenderungen
*
*----------------------------------------------------------------*
* Vers. | Datum    | von | Kommentar                             *
*-------|-------- -|-----|---------------------------------------*
*G.02.01|2018-11-26| kus | R7-408:
*       |          |     | - Umstellung von festem ROUTKZ auf AS-Verf
*       |          |     | DKVCHIP-26:
*       |          |     | - IFSF 22.1 konfigurierbar über FCPARAM
*-------|----------|-----|---------------------------------------*
*G.01.08|20181001  | kus | R7-407:
*       |          |     | - KZ-VERF jetzt "m"
*-------|----------|-----|--- -----------------------------------*
*G.01.07|20180525 | kl  | Neukompilierung wg. Korrektur ZPVERKM
*       |         |     | (ZP-VERKAUF Modul)
*-------|----------|-----|--- -----------------------------------*
*G.01.06|2018-04-30| kus | F1ICC-108:
*       |          |     | - AS BMP 12 fuellen mit ZP vom Terminal
*       |          |     |   dafuer ZP-VERKAUF verwenden (ist Kombi
*       |          |     |   aus Terminal BMP 12/13)
*-------|----------|-----|---------------------------------------*
*G.01.05|2018-04-11| sk  |Jira R7-298: Modulaufruf f. zp_verkauf *
*       |          |     |Berechnung                             *
*-------|----------|-----|--- -----------------------------------*
*G.01.04|2018-03-20| kl  | - G.01.01A und G.01.02A zusammenge-   *
*       |          |     |   fuehrt; G.01.03 geloescht           *
*       |2018-04-05|     | - Optimierung Zugriff FCPARAM         *
*       |          |     |   R7-272
*-------|----------|-----|--- -----------------------------------*
*G.01.03|2018-03-20| kl  | geloescht: falsche Impl. ZP-VERKAUF   *
*       |          |     |            R7-269                     *
*-------|----------|-----|--- -----------------------------------*
*G.01.02|2018-03-20| kl  | aufgeloest in G.01.04                 *
*-------|----------|-----|--- -----------------------------------*
*G.01.01|2018-01-09| kl  | Initialierung T-MAX in B000 optimiert *
*-------|----------|-----|---------------------------------------*
*G.01.00|2018-01-05| kl  | - Speichertabelle fuer FCPARAM        *
*       |          |     |   vergroessert                        *
*       |          |     | - Versionierung auf G.01.00, da in    *
*       |          |     |   mehreren Iterationen produktiv      *
*-------|----------|-----|---------------------------------------*
*-------|----------|-----|---------------------------------------*
*G.00.06|2017-08-25| hkn |OUTDOOR: Restriktions-Prüfung mit 20
*       |          |     |Artikeln angepasst
*-------|----------|-----|---------------------------------------*
*G.00.05|2017-02-02| hkn |FCPARM ersetzt durch FCPARAM
*-------|----------|-----|---------------------------------------*
*G.00.04|2016-11-16| hkn |TND: PAC - BMP 52 ohne Prüfziffer,
*       |          |     |somit 12 Stellen, anstatt 13 Stellen
*-------|----------|-----|---------------------------------------*
*G.00.03|2016-07-05| hkn |Prüfung leere Artikelmenge in BMP 63
*       |          |     |verbessert
*-------|----------|-----|---------------------------------------*
*G.00.02|2016-06-14| hkn |TXILOG70.BETREAG-AUTOR gleich
*       |          |     |angefragter Betrag
*-------|----------|-----|---------------------------------------*
*G.00.01|2016-05-27| kl  | Fuer Eigenantwort (falls ok)
*       |          |     | ----------------------------
*       |          |     | Genehmigungsnummer in BMP 59
*       |          |     | Text               in BMP 60
*-------|----------|-----|---------------------------------------*
*G.00.00|2016-03-22| hkn | Neuerstellung aus PFCOFF7E
*----------------------------------------------------------------*
*
* Programmbeschreibung
* --------------------
*
* Das Programm bearbeitet Flottenkarten-Stand-In-Buchungen. Die Terminal-
* Anfragen werden auf IFSF-Anfragen für ein spezielles AS (z.Z. DKV) umgestzt
* und dem IFSF-Nachbucher zur Verfügung gestellt. Die Terminalanfragen
* werden direkt beantwortet. Die AS-Anfragen müssen
* für jedes AS programmiert werden. Umsetzungsregeln können in der
* Tabelle =FCPARAM hinterlegt werden. Vorgesehen sind als
* Rahmenprogramm zunächst folgende Flottenkartenautorisierungssysteme:
*
*    -   Avia    (erste Ausbaustufe)
*    -   Shell
*    -   Total
*    -   DKV   - Zur Zeit nur für DKV umgesetzt
*    -   BP
*    -   ENI
*    -   Orlen
*    -   UTA
*    -   TND
*
* Prüfungen: Eingangsprüfungen der Nachricht,
*            Sperrlistenpruefungen,
*            Limitpruefungen,
*            und Retriktionspruefungen
*
* PFCSIP7E ruft als Unterserver WTBLL06S auf, um
* umfangreiche Sperrlistenpruefung durchzufuehren.
*
* Ferner wird eine Limitpruefung durchgefuehrt. Bei nicht
* gesperrter Karte wird bis zu einem Limit X der AC 00
* zurückgesendet. Darueber wird mit AC=02 - Telefonische
* autorisierung einholen - geantwortet.
*
* Ausserdem wird eine Restriktionspruefung fuer jede in
* BMP63 gelieferte Warengruppe durchgeführt. Dazu werden die voll-
* staendigen Spurdaten benoetigt. Eine Handeingabe ist nicht
* zulaessig (Ablehnung mit AC 40).
*
* Antwortet PFCSIP7E mit AC = 00, werden die erforderlichen Daten
* geloggt.
*
* Das Programm erwartet die folgenden Parameter:
*    -   AS-ROUTKZ   Routkennzeichen für das relevante AS
*    -   MACKEYT     MAC-Schlüssel für Terminaltransaktionen
*    -   PACKEYT     PAC-Schlüssel für Terminaltransaktionen
*    -   BOXMON      Festlegung des Boxenservers
*    -   ARTMAP      Festlegung des zuständigen Artikel-Mappers
*
* Liste der unterstuetzten Karten:
* --------------------------------
*
* DKV                CID=17           IFSF (DK Format)

******************************************************************
******************************************************************
*
* Uebersicht der SECTIONs:
*
* CONFIGURATION
* INPUT-OUTPUT
* FILE
* WORKING-STORAGE
* EXTENDED-STORAGE
*
* A000-WHENEVER
* A100-STEUERUNG
*
* B000-VORLAUF
* B090-ENDE
* B100-VERARBEITUNG
*
* C000-INIT
* C100-ANFRAGE-CHECK
* C110-SPERRLISTEN-PRUEFUNG
* C120-LIMIT-PRUEFUNG
* C130-RESTRIKTIONEN-PRUEFUNG
* C200-AS-GENERELL
* C300-AS-SPEZIELL
* C400-BUILD-AS-NACHRICHT
* C500-LOGGING
* C600-ZUM-NACHBUCHER
*
* D200-FIX-KEY
* D305-AVIA
* D307-SHELL
* D310-TOTAL
* D312-DKV
* D314-BP
* D315-ENI
* D316-ORLEN
* D317-UTA
* D318-TND
* D900-GET-CARDID
* D910-GET-ASTRACENR
*
* E100-FEP-ANTWORT
* E305-01-ARTIKELDATEN
* E310-BMP48-DEFAULT
* E900-PUT-ERRLOG
*
* F910-MAC-PRUEFEN
* F920-MAC-BILDEN
* F930-PAC-UMSCHLUESSELN
*
* G100-PUT-TXILOG70
* G110-PUT-TXNLOG70-TS
* G140-PUT-ASYNC70
* G150-PUT-CRDUSEDN
* G130-PUT-UMSWEAT
*
* L100-ADD-BMP
* L110-COB2ISO
*
* M100-CALL-SDBCDU5
* M120-CALL-WCAPM92
* M130-CALL-WISO207
* M140-CALL-WSY7066
* M150-CALL-WSYS930
* M170-CALL-SYSAWKZ
* M180-CALL-IUMSW07
*
* P100-PSD-ARTIKELMAPPER
* P110-PSD-SPERRLISTEN-PRUEFUNG
* P900-WTHEX
* P910-WTUNHEX
* P950-GETPARAMTEXT
* P960-GET-PROC-ANCNAME
*
* R000-LESEN-MESSAGE
*
* S000-SCHREIBEN-REPLY
* S100-SELECT-KEYNAMEN
* S110-SELECT-MDNR2AS
* S120-UPDATE-MDNR2AS
* S150-SELECT-TSKART40
* S160-SELECT-STATION
* S170-SELECT-STATIONA
* S180-INSERT-TXILOG70
* S190-INSERT-TXNLOG70-TS
* S210-INSERT-ASYNC70
* S600-GET-STIPLIMS
* S700-RESTRIKTIONEN-INDOOR
* S701-RESTRIKTIONEN-INDOOR
* S702-RESTRIKTIONEN-OUTDOOR
* S900-OPEN-FCPARAM-CURSOR
* S910-FETCH-FCPARAM-CURSOR
* S920-CLOSE-FCPARAM-CURSOR
* S930-OPEN-KEYNAMEN-CURSOR
* S940-FETCH-KEYNAMEN-CURSOR
* S950-CLOSE-KEYNAMEN-CURSOR
*
* U100-BEGIN
* U101-BEGINTRANSACTION
* U110-COMMIT
* U111-ENDTRANSACTION
* U120-ROLLBACK
* U121-ABORTTRANSACTION
* U200-TIMESTAMP
* U300-SEARCH-TAB
* U400-INTERPRET-ABWEICHUNG
*
* V400-WT-HEX-STRING
*
* Z001-SQLERROR
* Z002-PROGERR
* Z999-ERRLOG
*
******************************************************************


 ENVIRONMENT DIVISION.
 CONFIGURATION SECTION.
 SPECIAL-NAMES.
     SWITCH-1 IS TRACE-FLAG
         ON  STATUS IS TRACE-ON
         OFF STATUS IS TRACE-OFF
     SWITCH-15 IS ANZEIGE-VERSION
         ON STATUS IS SHOW-VERSION
     CLASS ALPHNUM IS "0123456789"
                      "abcdefghijklmnopqrstuvwxyz"
                      "ABCDEFGHIJKLMNOPQRSTUVWXYZ"
                      " .,;-_!§$%&/=*+"
     DECIMAL-POINT IS COMMA.

 INPUT-OUTPUT SECTION.
 FILE-CONTROL.
     SELECT MESSAGE-DATEI     ASSIGN TO $RECEIVE
                              FILE STATUS IS FILE-STATUS.
     SELECT REPLY-DATEI       ASSIGN TO $RECEIVE
                              FILE STATUS IS FILE-STATUS.

 RECEIVE-CONTROL.
     TABLE OCCURS 20 TIMES
     REPLY CONTAINS MESSAGE-DATEI RECORD.

 DATA DIVISION.
 FILE SECTION.

 FD  MESSAGE-DATEI IS EXTERNAL
     RECORD IS VARYING IN SIZE.
**          ---> FREGAT-Satz + interner Zusatz (Laenge = 4096)
 01          INTERN-MESSAGE.
             COPY    INT-SCHNITTSTELLE-C OF  "=MSGLIB"
                     REPLACING =="*"== BY ==IMSG==.

 FD  REPLY-DATEI
     RECORD IS VARYING IN SIZE
               FROM 12 TO 4096 CHARACTERS
               DEPENDING ON REPLY-LAENGE.

 01          REPLY-SATZ.
     05      REPLY-DUMMY         PIC S9(04) COMP.
     05      REPLY-FLAG          PIC X(10).
     05      FILLER              PIC X(4084).

 WORKING-STORAGE SECTION.
*--------------------------------------------------------------------*
* Comp-Felder: Präfix Cn mit n = Anzahl Digits
*--------------------------------------------------------------------*
 01          COMP-FELDER.
     05      C4-ANZ              PIC S9(04) COMP VALUE ZEROS.
     05      C4-COUNT            PIC S9(04) COMP VALUE ZEROS.
     05      C4-I1               PIC S9(04) COMP VALUE ZEROS.
     05      C4-I2               PIC S9(04) COMP VALUE ZEROS.
     05      C4-I3               PIC S9(04) COMP VALUE ZEROS.
     05      C4-I4               PIC S9(04) COMP VALUE ZEROS.
     05      C4-LEN              PIC S9(04) COMP VALUE ZEROS.
     05      C4-PTR              PIC S9(04) COMP VALUE ZEROS.

     05      C4-X.
      10                         PIC X value low-value.
      10     C4-X2               PIC X VALUE SPACES.
     05      C4-NUM redefines C4-X
                                 PIC S9(04) COMP.

     05      C9-ANZ              PIC S9(09) COMP VALUE ZEROS.
     05      C9-COUNT            PIC S9(09) COMP VALUE ZEROS.

     05      C18-VAL             PIC S9(18) COMP VALUE ZEROS.

     05      REPLY-LAENGE        PIC  9(04) COMP VALUE ZEROS.

*--------------------------------------------------------------------*
* Display-Felder: Präfix D
*--------------------------------------------------------------------*
 01          DISPLAY-FELDER.
     05      D-NUM1              PIC  9 VALUE ZEROS.
     05      D-NUM2              PIC  9(02) VALUE ZEROS.
     05      D-NUM20             PIC  9(02) VALUE ZEROS.
     05      D-NUM21             PIC  9(02) VALUE ZEROS.
     05      D-NUM3              PIC  9(03) VALUE ZEROS.
     05      D-NUM4              PIC -9(04) VALUE ZEROS.
     05      D-NUM4M             PIC  9(04) VALUE ZEROS.
     05      D-NUM4N             PIC  9(04) VALUE ZEROS.
     05      D-NUM4OV            PIC  9(04) VALUE ZEROS.
     05      D-NUM6              PIC  9(06) VALUE ZEROS.
     05      D-NUM61             PIC  9(06) VALUE ZEROS.
     05      D-NUM9              PIC  9(09) VALUE ZEROS.
     05      D-NUM12             PIC  9(12) VALUE ZEROS.
     05      D-NUM18             PIC  9(18) VALUE ZEROS.

*--------------------------------------------------------------------*
* Felder mit konstantem Inhalt: Präfix K
*--------------------------------------------------------------------*
 01          KONSTANTE-FELDER.
     05      K-MODUL             PIC X(08)          VALUE "PFCSIP7S".

**          ---> Pflichtfelder einer 1220-AS-Nachricht
     05      K-BYTEMAP-A1220     PIC X(64) VALUE
     "0011001000110000000001011100000100000000110000011000000000100010".
**             1         2         3         4         5         6
**    1234567890123456789012345678901234567890123456789012345678901234

**          ---> dummy-Nachricht für AS-Antwort über Nachbucher
     05      K-DUMMY-ANTWORT.
         10  K-DUMMY-NTYPE       PIC X(04) VALUE "9999".
         10  K-DUMMY-BITMAP      PIC X(08) VALUE X"0000000000400000".
         10  K-DUMMY-VUNR        PIC X(15) VALUE "ueb.Nachbucher".

*----------------------------------------------------------------*
* Conditional-Felder
*----------------------------------------------------------------*
 01          SCHALTER.
     05      FILE-STATUS         PIC X(02).
          88 FILE-OK                         VALUE "00".
          88 FILE-NOK                        VALUE "01" THRU "99".
          88 FILE-TIME-OUT                   VALUE "30".
     05      REC-STAT REDEFINES  FILE-STATUS.
        10   FILE-STATUS1        PIC X.
          88 FILE-EOF                        VALUE "1".
          88 FILE-INVALID                    VALUE "2".
          88 FILE-PERMERR                    VALUE "3".
          88 FILE-LOGICERR                   VALUE "4".
          88 FILE-NONAME                     VALUE "5" THRU "8".
          88 FILE-IMPLERR                    VALUE "9".
        10                       PIC X.

     05      MSG-STATUS          PIC 9       VALUE ZEROS.
          88 MSG-OK                          VALUE ZEROS.
          88 MSG-EOF                         VALUE 1.

     05      CSYS-FLAG           PIC 9       VALUE 1.
          88 C-POS-INDOOR                    VALUE 1.
          88 C-OPT-OUTDOOR                   VALUE 2.
          88 C-WASCH                         VALUE 3.

     05      PRG-STATUS          PIC 9       VALUE ZEROS.
          88 PRG-OK                          VALUE ZEROS.
          88 PRG-NOK                         VALUE 1 THRU 9.
          88 PRG-ENDE                        VALUE 1.
          88 PRG-ABBRUCH                     VALUE 2.

     05      ENDE-FLAG           PIC 9       VALUE ZEROS.
          88 ENDE-OFF                        VALUE ZEROS.
          88 ENDE                            VALUE 1.

     05      ASYNC70-FLAG        PIC 9       VALUE ZEROS.
          88 ASYNC70-OK                      VALUE ZEROS.
          88 ASYNC70-NOK                     VALUE 1.

     05      FCPARAM-FLAG         PIC 9       VALUE ZEROS.
          88 FCPARAM-OK                       VALUE ZEROS.
          88 FCPARAM-EOD                      VALUE 1.
          88 FCPARAM-NOK                      VALUE 9.

     05      KEYNAMEN-FLAG       PIC 9       VALUE ZEROS.
          88 KEYNAMEN-OK                     VALUE ZEROS.
          88 KEYNAMEN-EOD                    VALUE 1.
          88 KEYNAMEN-NOK                    VALUE 9.

     05      MDNR2AS-FLAG        PIC 9       VALUE ZEROS.
          88 MDNR2AS-OK                      VALUE ZEROS.
          88 MDNR2AS-NOK                     VALUE 1.

     05      TSKART40-FLAG       PIC 9       VALUE ZEROS.
          88 TSKART40-OK                     VALUE ZEROS.
          88 TSKART40-NOK                    VALUE 1.

     05      STATION-FLAG        PIC 9       VALUE ZEROS.
          88 STATION-OK                      VALUE ZEROS.
          88 STATION-NOK                     VALUE 1.

     05      STATIONA-FLAG       PIC 9       VALUE ZEROS.
          88 STATIONA-OK                     VALUE ZEROS.
          88 STATIONA-NOK                    VALUE 1.

     05      TXILOG70-FLAG       PIC 9       VALUE ZEROS.
          88 TXILOG70-OK                     VALUE ZEROS.
          88 TXILOG70-NOK                    VALUE 1.

     05      TXNLOG70-FLAG       PIC 9       VALUE ZEROS.
          88 TXNLOG70-OK                     VALUE ZEROS.
          88 TXNLOG70-NOK                    VALUE 1.

     05      PRM-FLAG            PIC X     VALUE SPACES.
          88 PRM-NOT-FOUND                 VALUE SPACES.
          88 PRM-FOUND                     VALUE HIGH-VALUE.

     05      ERF-FLAG            PIC X     VALUE SPACES.
          88 ERF-ERROR                     VALUE SPACES.
          88 ERF-SPUR2                     VALUE LOW-VALUE.
          88 ERF-MANUELL                   VALUE HIGH-VALUE.

     05      PAC-FLAG            PIC X     VALUE LOW-VALUE.
          88 PAC-YES                       VALUE LOW-VALUE.
          88 PAC-NO                        VALUE HIGH-VALUE.

     05      MAC-FLAG            PIC X     VALUE LOW-VALUE.
          88 MAC-YES                       VALUE LOW-VALUE.
          88 MAC-NO                        VALUE HIGH-VALUE.

     05      GEODATA-FLAG        PIC X     VALUE LOW-VALUE.
          88 GEODATA-NO                    VALUE LOW-VALUE.
          88 GEODATA-YES                   VALUE HIGH-VALUE.

     05      KFZ-FLAG            PIC X     VALUE LOW-VALUE.
          88 KFZ-NO                        VALUE LOW-VALUE.
          88 KFZ-YES                       VALUE HIGH-VALUE.

     05      FEP-ANTWORT-FLAG    PIC X     VALUE LOW-VALUE.
          88 NO-FEP-ANTWORT                VALUE LOW-VALUE.
          88 FEP-ANTWORT                   VALUE HIGH-VALUE.

        05       CARD-FLAG           PIC 9(02) VALUE ZERO.
          88 NO-CARD                       VALUE ZERO.
          88 DKV                           VALUE 17.
          88 NOVOFLEET                     VALUE 24.
          88 CARD-OK                       VALUE 17 24.


*--------------------------------------------------------------------*
* weitere Arbeitsfelder
*--------------------------------------------------------------------*
**          ---> unverändert
 01          WORK-FELDER.
     05      W-ROUTKZ            PIC S9(04) COMP VALUE ZEROS.
     05      W-KEYNAME           PIC  X(08) VALUE SPACES.
     05      W-ISOGEN-VERS       PIC  X(02) VALUE SPACES.

**          ---> werden bei jeder Tx initiert
 01          WORK-INIT.
     05      W-CARDID            PIC S9(04)    COMP VALUE ZEROS.
     05      W-KANR-LEN          PIC S9(04)    COMP VALUE ZEROS.
     05      W18-BETRAG          PIC S9(16)V99 COMP VALUE ZEROS.
     05      W-BMP07             PIC 9(10)          VALUE ZEROS.
     05      W-ZP-VERKAUF        PIC S9(18)    COMP VALUE ZEROS.
*G.02.01 - AIID 
     05      W-AIID              PIC X(11).
*G.02.01 - Ende

     05      W-ACX.
      10     W-AC                PIC 9(02) VALUE ZEROS.
     05      W-ACQUIRER-ID       PIC X(06) VALUE SPACES.
     05      W-ABL               PIC 9(04) VALUE ZEROS.
     05      W-MDNR              PIC 9(08) VALUE ZEROS.
     05      W-TSNR              PIC 9(08) VALUE ZEROS.

     05      W-TERMNR            PIC 9(08) VALUE ZEROS.
     05      W-TRACENR           PIC 9(06) VALUE ZEROS.
     05      W-AS-TRACENR        PIC 9(06) VALUE ZEROS.
     05      W-NTYPE             PIC 9(04) VALUE ZEROS.
             88 NTYPE-BUCHUNG              VALUE 200.
*kl20180313 - G.01.01A - 1. Es reichen die ersten beiden Stellen
*                        2. Zahlung = 67,77, Gutschrift = 68,78
     05      W-ABWKZ.
        10   W-ABWKZ-ZAHL-GUT     PIC 9(02) VALUE ZEROS.
             88 ABWKZ-ZAHLUNG              VALUE 67, 77.
             88 ABWKZ-GUTSCHRIFT           VALUE 68, 78.
        10   W-ABWKZ-BELEG       PIC 9(04) VALUE ZEROS.
*kl20180313 - G.01.01A - Ende
     05      W-BELEGNR           PIC 9(04) VALUE ZEROS.
     05      W-ABWKZ-37          PIC 9(06) VALUE ZEROS.

     05      W-LTGIND            PIC 9(04) VALUE ZEROS.
     05      W-BETRAG            PIC 9(18) VALUE ZEROS.
     05      W-ERFASSUNGS-ART    PIC 9(02) VALUE ZEROS.
          88 W-ERF-MANUELL                   value 01.
          88 W-ERF-MAGNET                    value 02.
          88 W-ERF-CHIP                      value 05.
          88 W-ERF-KONTAKTLOS                value 07 91.

     05      W-TRANS-ART         PIC X(02)  VALUE SPACES.
     05      W-KANR              PIC X(19)  VALUE SPACES.
     05      W-WKZ               PIC 9(03)  VALUE ZEROS.
     05      W-SPUR2             PIC X(40)  VALUE SPACES.
     05      W-BUFFER            PIC X(128) VALUE SPACES.
     05      W-BUFFER-LEN        PIC S9(04) COMP VALUE ZEROS.
     05      W-BUFFER-AKT        PIC S9(04) COMP VALUE ZEROS.

*Die Konvertierung der TSNR fuer VUNR TSNR wird aus DB mit fuehrenden Nullen
*bereitgestellt

 01          W-KONV-TSNR         PIC Z(7)9    VALUE ZEROS.
 01          W-KONV-TSNR-STR     PIC X(08)    REDEFINES W-KONV-TSNR.

**          ---> Schlüsselfelder
 01          W-MACKEYA           PIC X(04) VALUE SPACES.
 01          W-PACKEYA           PIC X(08) VALUE LOW-VALUE.
 01          W-MACKEYT           PIC X(04) VALUE SPACES.
 01          W-PACKEYT           PIC X(04) VALUE SPACES.
 01          W-HERSTID           PIC X(02) VALUE SPACES.
 01          W-VERSION           PIC X(02) VALUE SPACES.
 01          W-PADDING           PIC X(08) VALUE LOW-VALUE.

**  ---> Liste der Inlineservices
 01          INLINE-SERVICES.
     05      W-ARTMAP            PIC X(16) VALUE "WXAMP07S-TEMP".
     05      INLINE-SERVICE      PIC S9(04) COMP VALUE ZEROS.
          88 USE-WXAMP                           VALUE 20.

 01          GEO-BUFFER.
     05      GEO-KZ-BREITE       PIC X           VALUE SPACES.
     05      GEO-BREITE-8        PIC 9(08)       VALUE ZEROS.
     05                          PIC X           VALUE SPACES.
     05      GEO-KZ-LAENGE       PIC X           VALUE SPACES.
     05      GEO-LAENGE-9        PIC 9(09)       VALUE ZEROS.
 01          GEO-FAKTOR          PIC S9(18) COMP VALUE 1000000.

**          ---> Bereiche für IFSF-BMP48
 01          W-BYTEMAP-48        PIC X(64) VALUE
     "0000000000000000000000000000000000000000000000000000000000000000".
 01          W-BITMAP            PIC X(08)  VALUE LOW-VALUES.
 01          W-BMP48-VAL         PIC X(128) VALUE SPACES.
 01          W-48-LEN            PIC 9(03)  VALUE ZEROS.

**  ---> Zwischenfelder fuer Fregat
 01          W-FRE.
     05      W-FRE-HEADER        PIC X(88)       VALUE SPACES.
     05      W-FRE-TERMID        PIC X(16)       VALUE SPACES.
     05      W-FRE-MONNAME       PIC X(16)       VALUE SPACES.
     05      W-FRE-DATLEN        PIC S9(04) COMP VALUE ZEROS.

 01          W-RST-FLAG          PIC X VALUE "J".
          88 W-RST-PRUEFEN             VALUE "J".
          88 W-RST-NICHT-PRUEFEN       VALUE "N".

*--------------------------------------------------------------------*
* Datm-Uhrzeitfelder (für TAL-Routine)
*--------------------------------------------------------------------*
 01          TAL-TIME.
     05      TAL-JHJJMMTT.
      10     TAL-JHJJ            PIC S9(04) COMP VALUE ZEROS.
      10     TAL-MM              PIC S9(04) COMP VALUE ZEROS.
      10     TAL-TT              PIC S9(04) COMP VALUE ZEROS.
     05      TAL-HHMI.
      10     TAL-HH              PIC S9(04) COMP VALUE ZEROS.
      10     TAL-MI              PIC S9(04) COMP VALUE ZEROS.
     05      TAL-SS              PIC S9(04) COMP VALUE ZEROS.
     05      TAL-HS              PIC S9(04) COMP VALUE ZEROS.
     05      TAL-MS              PIC S9(04) COMP VALUE ZEROS.

 01          TAL-TIME-D.
     05      TAL-JHJJMMTT.
        10   TAL-JHJJ            PIC  9(04) VALUE ZEROS.
        10   TAL-MM              PIC  9(02) VALUE ZEROS.
        10   TAL-TT              PIC  9(02) VALUE ZEROS.
     05      TAL-HHMI.
        10   TAL-HH              PIC  9(02) VALUE ZEROS.
        10   TAL-MI              PIC  9(02) VALUE ZEROS.
     05      TAL-SS              PIC  9(02) VALUE ZEROS.
     05      TAL-HS              PIC  9(02) VALUE ZEROS.
     05      TAL-MS              PIC  9(02) VALUE ZEROS.
 01          TAL-TIME-N REDEFINES TAL-TIME-D.
     05      TAL-TIME-N16        PIC  9(16).
     05      TAL-TIME-REST       PIC  9(02).

 01          TAL-JUL-DAY         PIC S9(09) COMP VALUE ZEROS.

**          ---> Datum im DATETIME-Format
 01          TAGESDATUM.
     05      TD-JHJJ             PIC X(04).
     05                          PIC X   VALUE "-".
     05      TD-MM               PIC XX.
     05                          PIC X   VALUE "-".
     05      TD-TT               PIC XX.
     05                          PIC X   VALUE ":".
     05      TD-HH               PIC XX.
     05                          PIC X   VALUE ":".
     05      TD-MI               PIC XX.
     05                          PIC X   VALUE ":".
     05      TD-SS               PIC XX.
     05                          PIC X   VALUE ".".
     05      TD-HS               PIC XX.


*--------------------------------------------------------------------*
* Parameter für Untermodulaufrufe
*--------------------------------------------------------------------*
 01          PARAMETER-FELDER.
     05      P-DUMMY             PIC X(02)       VALUE SPACES.

**          ---> ChangeCode mit ASC2EBC und EBC2ASC
 01          P-CC-LEN            PIC S9(04) COMP VALUE ZEROS.
 01          P-CC-IN             PIC X(64)       VALUE SPACES.
 01          P-CC-OUT            PIC X(64)       VALUE SPACES.

**          ---> für WT^HEX und WT^UNHEX Routinen
 01          HEX8                PIC X(08)       VALUE SPACES.
 01          HEX16               PIC X(16)       VALUE SPACES.

**          ---> für COBOL-Utilities GET-/PUT-STARTUPTEXT
**          --->                     GET-/PUT-PARAMTEXT
 01          STUP-PARAMETER.
     05      STUP-RESULT         PIC S9(04) COMP VALUE ZEROS.
     05      STUP-CPLIST         PIC  9(09) COMP VALUE ZEROS.
     05      STUP-PORTION        PIC  X(30) VALUE "STRING".
     05      STUP-TEXT           PIC X(128) VALUE SPACES.

**          ---> Holen des eigenen Pathwaysystems
 01          PAIRINFO            PIC S9(04) COMP VALUE ZEROS.
 01          FEHL                PIC S9(04) COMP VALUE ZEROS.

**          ---> Holen des Prozessnamens
 01          PNAME               PIC  X(47)      VALUE SPACES.
 01          PNAMELEN            PIC S9(04) COMP VALUE ZEROS.
 01          PROC-INFO           PIC  X(18)      VALUE SPACES.

**     ---> Kartenprofile fuer Restriktionsprüfung
 01    W-TRACK2-DKV.
       05 W-DKV-KARTE.
          10 W-DKV-ISO               PIC 9(06)  VALUE ZEROS.
          10 W-DKV-KDNR              PIC 9(06)  VALUE ZEROS.
          10 W-DKV-KANR              PIC 9(04)  VALUE ZEROS.
          10 W-DKV-PRF               PIC 9(01)  VALUE ZEROS.
       05 W-DKV-TRENNER              PIC X      VALUE "=".
       05 W-DKV-VERFALL              PIC 9(04)  VALUE ZEROS.
       05 W-DKV-DISKRET.
          10 W-DKV-RESERVE1          PIC 9(02)  VALUE ZEROS.
          10 W-DKV-RSTCODE           PIC 9(02)  VALUE ZEROS.
          10 W-DKV-PINREQ            PIC 9      VALUE ZEROS.
          10 W-DKV-RESERVE2          PIC 9      VALUE ZEROS.
          10 W-DKV-PIN-OFFSET        PIC 9(04)  VALUE ZEROS.
          10 W-DKV-RESERVE3          PIC 9(05)  VALUE ZEROS.

 01    W-TRACK2-NOVOFLEET.
       05 W-NVF-KARTE.
          10 W-NVF-ISO               PIC 9(06)  VALUE ZEROS.
          10 W-NVF-KDNR              PIC 9(06)  VALUE ZEROS.
          10 W-NVF-KANR              PIC 9(04)  VALUE ZEROS.
          10 W-NVF-PRF               PIC 9(01)  VALUE ZEROS.
       05 W-NVF-TRENNER              PIC X      VALUE "=".
       05 W-NVF-VERFALL              PIC 9(04)  VALUE ZEROS.
       05 W-NVF-DISKRET.
          10 W-NVF-RESERVE1          PIC 9(02)  VALUE ZEROS.
          10 W-NVF-RSTCODE           PIC 9(02)  VALUE ZEROS.
          10 W-NVF-RESERVE2          PIC 9(02)  VALUE ZEROS.
          10 W-NVF-PIN-OFFSET        PIC 9(04)  VALUE ZEROS.
          10 W-NVF-RESERVE3          PIC 9(05)  VALUE ZEROS.

**--> BMP63 - Ausgangsstruktur (Terminalanfrage)
 01          WEAT-STRUCT.
     05      WEAT-INFO-DATEN.
         10  WEAT-KM              PIC X(03) VALUE SPACES.
         10  WEAT-KFZNR           PIC X(02) VALUE SPACES.
         10  WEAT-FAHRER          PIC X(02) VALUE SPACES.
         10  WEAT-ERFASSUNG       PIC X(03) VALUE SPACES.
         10  WEAT-INFO            PIC X(20) VALUE SPACES.
         10  WEAT-EINGABE         PIC X     VALUE SPACES.
         10  FILLER               PIC X     VALUE SPACES.
     05      WEAT-COUNTER         PIC X     VALUE SPACES.
     05      WEAT-ARTIKEL         OCCURS 20.
         10  WEAT-PROD-CODE       PIC X(03) VALUE SPACES.
         10  WEAT-ART-EINZELPREIS PIC X(04) VALUE SPACES.
         10  WEAT-QNT             PIC X(03) VALUE SPACES.
         10  WEAT-AMNT            PIC X(05) VALUE SPACES.
         10  WEAT-VZ              PIC X     VALUE SPACES.

**--> BMP63 - Ausgangsstruktur (Terminalanfrage - entpackt)
 01          UP-STRUCT.
     05      UP-INFO-DATEN.
         10  UP-KM                PIC 9(06) VALUE ZEROS.
         10  UP-KFZNR             PIC 9(04) VALUE ZEROS.
         10  UP-FAHRER            PIC 9(04) VALUE ZEROS.
         10  UP-ERFASSUNG         PIC 9(06) VALUE ZEROS.
         10  UP-INFO              PIC X(40) VALUE SPACES.
         10  UP-EINGABE           PIC 9(02) VALUE ZEROS.
         10  FILLER               PIC X(02) VALUE SPACES.
     05      UP-COUNTER           PIC 9(02) VALUE ZEROS.
     05      UP-ARTIKEL           OCCURS 20.
         10  UP-PROD-CODE         PIC 9(06) VALUE ZEROS.
         10  UP-ART-EINZELPREIS   PIC 9(08) VALUE ZEROS.
         10  UP-QNT               PIC 9(06) VALUE ZEROS.
         10  UP-AMNT              PIC 9(10) VALUE ZEROS.
         10  UP-VZ                PIC X(02) VALUE SPACES.

**--> BMP63 - Zielstruktur (Terminalantwort - IFSF - Shell)
 01          WEAT-STRUCT2.
     05      WEAT-COUNTER2        PIC X        VALUE SPACES.
     05      WEAT-PROD-CODE2      PIC X(03)    VALUE SPACES OCCURS 20.

**--> BMP63 - Zielstruktur (Terminalantwort - IFSF - Shell) entpackt

 01          UP-STRUCT2.
     05      UP-COUNTER2        PIC 9(02) VALUE ZEROS.
     05      UP-PROD-CODE2      PIC 9(06) VALUE ZEROS OCCURS 20.

** N.O. - Ende

*Speichert die CARDID aus =STIPLIMS des letzten Select
 01          W-CARDID-OLD        PIC 9(02) VALUE ZEROS.

*kl20160527 - G.00.01 - Zum Zusammenbauen der WEAT-Genehmigungsnummer
 01          D-GENNRX            PIC X(06).
*kl20160527 - G.00.01 - Ende

 EXTENDED-STORAGE SECTION.

*--------------------------------------------------------------------*
* weitere Buffer
*--------------------------------------------------------------------*
**          ---> Mapping ROUTKZ <-> APPL_KZ.IFSFAC
**          --->
**          ---> hier muss ggf. bei weiteren AS'sen erweitert werden
*G.02.01 - Refactoring fuer AS-Verfahren
 01          VERF-AS            PIC 9(02) VALUE ZEROS.
*G.02.01 - Ende
          88 VERF-AG                         VALUE 15.
          88 VERF-AV                         VALUE 05.
          88 VERF-BP                         VALUE 14.
          88 VERF-DK                         VALUE 12.
          88 VERF-NF                         VALUE 99.
          88 VERF-OR                         VALUE 16.
          88 VERF-SH                         VALUE 07.
          88 VERF-TN                         VALUE 18.
          88 VERF-TO                         VALUE 10.
          88 VERF-UT                         VALUE 17.

**          ---> Verfahrensfestlegung für Artikelmapper
**          ---> AG, AV und TN sind gleich (werden wie AG behandelt)
 01          AS-VERF             PIC X(02).
          88 AS-VERF-AG                      VALUE "AG".
          88 AS-VERF-AV                      VALUE "AV".
          88 AS-VERF-BP                      VALUE "BP".
          88 AS-VERF-DK                      VALUE "DK".
          88 AS-VERF-OR                      VALUE "OR".
          88 AS-VERF-SH                      VALUE "SH".
          88 AS-VERF-TN                      VALUE "TN".
          88 AS-VERF-TO                      VALUE "TO".
          88 AS-VERF-UT                      VALUE "UT".
          88 AS-VERF-DEFAULT                 VALUE "AG".



**          ---> Parametertabelle für Autorisierungssystem
**          ---> wird im Programmvorlauf geladen, d.h. bei Änderungen
**          ---> muss das Programm (Serverklasse) neu gestartet werden

*kl20180405 - G.01.04 - Sieht gut aus, ist aber hier nicht angebracht
* 01          T-FCPARAM.
*kl20180105 - G.03.08 - Tabelle auf 500 Einträge vergroessert
*     05      T-FCPARAM-TAB    occurs 200.
*     05      T-FCPARAM-TAB    occurs 1 to 200
*     05      T-FCPARAM-TAB    occurs 1 to 500
*kl20180105 - G.03.08 - Ende
*                             depending on     T-MAX
*                             ascending key is T-KEY
*                             indexed by       IND-TAB.
*      10     T-KEY.
*       15    T-ROUTKZ            PIC S9(04) COMP VALUE ZEROS.
*       15    T-CARDID            PIC S9(04) COMP VALUE ZEROS.
*       15    T-ISONTYP           PIC S9(04) COMP VALUE ZEROS.
*       15    T-KZ-MSG            PIC  X(02).
*       15    T-BMP               PIC S9(04) COMP VALUE ZEROS.
*       15    T-LFDNR             PIC S9(04) COMP VALUE ZEROS.
*      10     T-KZ-ABWEICHUNG     PIC  X(64).*
*
* 01          T-MAX               PIC S9(04) COMP VALUE ZEROS.
*
*kl20180105 - G.03.08 - Tabelle auf 500 Einträge vergroessert
* 01          T-TAB-MAX           PIC S9(04) COMP VALUE 200.
* 01          T-TAB-MAX           PIC S9(04) COMP VALUE 500.
*kl20180105 - G.03.08 - Ende
* 01          T-AKT-IND           PIC S9(04) COMP VALUE ZEROS.

*==> Zurück zur Standardtabellendefinition
 01          T-FCPARAM.
     05      T-FCPARAM-TAB    OCCURS 500.
       10    T-KEY.
         15  T-ROUTKZ            PIC S9(04) COMP VALUE ZEROS.
         15  T-CARDID            PIC S9(04) COMP VALUE ZEROS.
         15  T-ISONTYP           PIC S9(04) COMP VALUE ZEROS.
         15  T-KZ-MSG            PIC X(02).
         15  T-BMP               PIC S9(04) COMP VALUE ZEROS.
         15  T-LFDNR             PIC S9(04) COMP VALUE ZEROS.
        10   T-KZ-ABWEICHUNG     PIC  X(64).

 01          T-MAX               PIC S9(04) COMP VALUE ZEROS.
 01          T-TAB-MAX           PIC S9(04) COMP VALUE 500.
 01          T-AKT-IND           PIC S9(04) COMP VALUE ZEROS.
*kl20180405 - G.01.04 - Ende

**          ---> zu suchende Werte
 01          S-SEARCH-KEY.
     05      S-ROUTKZ            PIC S9(04) COMP VALUE ZEROS.
     05      S-CARDID            PIC S9(04) COMP VALUE ZEROS.
     05      S-ISONTYP           PIC S9(04) COMP VALUE ZEROS.
     05      S-KZ-MSG            PIC  X(02)      VALUE SPACES.
     05      S-BMP               PIC S9(04) COMP VALUE ZEROS.
     05      S-LFDNR             PIC S9(04) COMP VALUE ZEROS.

*kl20180405 - G.01.04 - Zusätzlicher Searchkey für CARDID = 0
*                       (wird aus S-SEARCH-KEY gefüllt; dann wird
*                        ledglich S2-CARDID mit ZERO überschrieben)
 01          S2-SEARCH-KEY.
     05      S2-ROUTKZ            PIC S9(04) COMP VALUE ZEROS.
     05      S2-CARDID            PIC S9(04) COMP VALUE ZEROS.
     05      S2-ISONTYP           PIC S9(04) COMP VALUE ZEROS.
     05      S2-KZ-MSG            PIC  X(02).
     05      S2-BMP               PIC S9(04) COMP VALUE ZEROS.
     05      S2-LFDNR             PIC S9(04) COMP VALUE ZEROS.
*kl20180405 - G.01.04 - Ende

**          ---> AS-Keytabelle
*G.02.01 - Tabelle auf 150 vergroessert
 01          TK-KEYNAMEN.
     05      TK-KEYNAMEN-TABELLE occurs 150.
      10     TK-ROUTKZ           PIC S9(04) VALUE ZEROS.
      10     TK-CARDID           PIC S9(04) VALUE ZEROS.
      10     TK-KEYNAME          PIC X(08)  VALUE SPACES.
      10     TK-ISOGEN           PIC X(02)  VALUE SPACES.
      10     TK-ISOVERS          PIC X(02)  VALUE SPACES.
      10     TK-HEXKEY           PIC X(04)  VALUE SPACES.
      10     TK-HEXISO           PIC X(02)  VALUE SPACES.
*G.02.01 - AIID hier mit speichern
      10     TK-AIID             PIC X(11).
      

 01          TK-MAX              PIC S9(04) COMP VALUE ZEROS.
 01          TK-TAB-MAX          PIC S9(04) COMP VALUE 150.
*G.02.01 - Ende


**          --->
 01          W-TEILSTRING-TABELLE.
     05      W-TEILSTRING-TAB    occurs 10.
      10     W-TEILSTRING        PIC X(64)       VALUE SPACES.

 01          W-DELIM-TABELLE.
     05      W-DELIM-TAB         occurs 10.
      10     W-DELIM             PIC X(01)       VALUE SPACES.

 01          W-COUNT-TABELLE.
     05      W-COUNT-TAB         occurs 10.
      10     W-COUNT             PIC S9(04) COMP VALUE ZEROS.

**          ---> zum Metadaten dieses Prozesses
 01          MY-META.
   05        MYPROG.
    10       SYSTEM              PIC X(08)       VALUE SPACES.
    10       VOL                 PIC X(08)       VALUE SPACES.
    10       SUBVOL              PIC X(08)       VALUE SPACES.
    10       MODUL               PIC X(08)       VALUE SPACES.
   05        PROC-INFO           PIC X(18)       VALUE SPACES.
   05        ANCNAME             PIC X(10)       VALUE SPACES.
   05        MY-SRV-ID           PIC S9(04) COMP VALUE ZEROS.
   05        MY-SRV-CLASS        PIC X(16)       VALUE SPACES.

**          ---> fuer Pathsend (serverclass_send_)
 01          PS-PATHNAME         PIC X(15)       VALUE SPACES.
 01          PS-PATHNAMELEN      PIC S9(04) COMP VALUE 15.
 01          PS-SRVCLASS         PIC X(15)       VALUE SPACES.
 01          PS-SRVCLASSLEN      PIC S9(04) COMP VALUE 15.
 01          PS-REQLEN           NATIVE-2.
 01          PS-MAXREPLEN        NATIVE-2.
 01          PS-AKTREPLEN        NATIVE-2.
 01          PS-TIMEOUT          PIC S9(04) COMP VALUE ZEROS.
 01          PS-ERROR            PIC S9(04) COMP VALUE ZEROS.

*           ---> fuer Pathsend (serverclass_send_info)
 01          SSI-ERROR           PIC S9(04) COMP VALUE ZEROS.
 01          SSI-PSERROR         PIC S9(04) COMP VALUE ZEROS.
 01          SSI-FSERROR         PIC S9(04) COMP VALUE ZEROS.

**          ---> für TAL-Modul WT^HEX^STRING
 01          WTHEXS.
     05      WTHEXS-SRC          PIC X(4096)     VALUE SPACES.
     05      WTHEXS-SRC-LEN      PIC S9(04) COMP VALUE ZEROS.
     05      WTHEXS-DST          PIC X(4096)     VALUE SPACES.
     05      WTHEXS-DST-LEN      PIC S9(04) COMP VALUE ZEROS.

*--------------------------------------------------------------------*
* Parameter für Untermodulaufrufe - COPY-Module
*--------------------------------------------------------------------*
 01          TS-INTERN-MESSAGE.
     COPY    INT-SCHNITTSTELLE-C OF  "=MSGLIB"
             REPLACING =="*"== BY ==TS==.

 01          AS-INTERN-MESSAGE.
     COPY    INT-SCHNITTSTELLE-C OF  "=MSGLIB"
             REPLACING =="*"== BY ==AS==.

**          ---> fuer Artikeldatenmapper WXAMP07
 01          AMP-SCHNITTSTELLE  IS EXTERNAL.
     COPY    WXAMP01C OF "=MSGLIB"
             REPLACING =="*"== BY ==AMP==.

**          ---> fuer Fehlerbeh.
     COPY    WSYS022C OF "=MSGLIB".

**          ---> Schnittstelle zu SDBCDU5
     COPY    SDBCDU0C OF "=MSGLIB"
             REPLACING =="*"== BY ==SDB==.

**          ---> Schnittstelle zu SYSAWKZ
     COPY    SYSWKZ0C OF "=MSGLIB"
             REPLACING =="*"== BY ==WKZ==.

**          ---> Schnittstelle zu WCAPM92
     COPY    PCAPM01C    OF "=MSGLIB"
             REPLACING =="*"== BY ==PCAP==.

**          ---> Schnittstelle zu WISO207
     COPY    WISO207C OF "=MSGLIB"
             REPLACING =="*"== BY ==W207==.

**          ---> Schnittstelle zu WSY7066
     COPY    WSY7066C OF "=MSGLIB"
             REPLACING =="*"== BY ==W66==.

**          ---> Schnittstelle zu WSYS930
     COPY    WSYS930C OF "=MSGLIB"
             REPLACING =="*"== BY ==ROUT==.

**          ---> Schnittstelle zum Identifikationsmodul WSYS980
     COPY    WSYS980C   OF "=MSGLIB"
             REPLACING =="*"== BY ==ID==.

**          ---> Fuer Umsatz
     COPY    WUMSO07C OF "=MSGLIB"
             REPLACING =="*"== BY ==WUMS==.

*kl20160527 - G.00.01 - Fuer Genehmigungsnummer (Pruefziffer)
**          ---> fuer Kartennummernpruefung
     COPY    WSYS04TC OF "=MSGLIB"
                 REPLACING =="*"== BY ==W04T==.
*kl20160527 - G.00.01 - Ende
*G.01.05
      COPY    ZPVERKAUF-IFC OF "=MSGLIB".
*G.01.05 - ende

******************************************************************
* Im Folgenden Vorkehrungen für SQL
******************************************************************

 EXEC SQL
     INCLUDE STRUCTURES ALL VERSION 315
 END-EXEC

 EXEC SQL
     INCLUDE SQLCA
 END-EXEC

 EXEC SQL
     BEGIN DECLARE SECTION
 END-EXEC

******************************************************************
* Im Folgenden zunächst Host-Variable, die Bestandteil von
* SQL - Tabellen sind
******************************************************************
 01          HOST-VARIABLEN.
     05      H-DUMMY             PIC X(02)       VALUE SPACES.
     05      H-SYSKEY            PIC S9(18) COMP VALUE ZEROS.
     05      H-ZP-IN             PIC X(22)       VALUE SPACES.
     05      H-ZP-OUT            PIC X(22)       VALUE SPACES.
     05      H-STIP-CALL         PIC 9(08)       VALUE ZEROS.
     05      H-STIP-REJECT       PIC 9(08)       VALUE ZEROS.
     05      H-RST-JN            PIC S9(04) COMP VALUE ZEROS.
     05      H-WG-OUTDOOR        PIC 9(02)       VALUE ZEROS.

******************************************************************
* Im Folgenden mit dem INVOKE-Befehl die Tabellenstruktur-
* definitonen der benötigten Tabellen einfügen
******************************************************************
**Struktur der Tabelle ASYNC70 für Nachricht an AS
 EXEC SQL
    INVOKE =ASYNC70 AS ASYNC70
 END-EXEC

**Struktur der Tabelle ARTMAP für Mapping TS-WG auf Host-WG
 EXEC SQL
    INVOKE =ARTMAP   AS ARTMAP
 END-EXEC

**Struktur der Tabelle FCPARAM
 EXEC SQL
    INVOKE =FCPARAM   AS FCPARAM
 END-EXEC

**Struktur der Tabelle KEYNAMEN
 EXEC SQL
    INVOKE =KEYNAMEN AS KEYNAMEN
 END-EXEC

**Struktur der Tabelle MDNR2AS
 EXEC SQL
    INVOKE =MDNR2AS  AS MDNR2AS
 END-EXEC

**Struktur der Tabelle TSKART40
 EXEC SQL
    INVOKE =TSKART40 AS TSKART40
 END-EXEC

**Struktur der Tabelle STATION
 EXEC SQL
    INVOKE =STATION  AS STATION
 END-EXEC

**Struktur der Tabelle STATIONA
 EXEC SQL
    INVOKE =STATIONA AS STATIONA
 END-EXEC

**Struktur der Tabelle - Limitprüfung
 EXEC SQL
    INVOKE =STIPLIMS AS STIPLIMS
 END-EXEC

**Struktur der Tabelle TXILOG70
 EXEC SQL
    INVOKE =TXILOG70 AS TXILOG70
 END-EXEC

**Transaktionslog Nachrichten der Station
 EXEC SQL
    INVOKE =TXNLOG70 AS TXNLOG70-TS
 END-EXEC

**Struktur der Tabelle UMSWEAT
 EXEC SQL
    INVOKE =UMSWEAT  AS UMSWEAT
 END-EXEC

**Struktur der Tabelle WGRSTPRF - Restriktionsprüfung
 EXEC SQL
    INVOKE =WGRSTPRF AS WGRSTPRF
 END-EXEC
 
*G.02.01 - neue Tabelle für AIID
 EXEC SQL
    INVOKE =FCAIID AS FCAIID
 END-EXEC
*G.02.01 - Ende

******************************************************************

 EXEC SQL
     END DECLARE SECTION
 END-EXEC

 EXEC SQL
     CONTROL TABLE * WAIT IF LOCKED
 END-EXEC.

 EXEC SQL
     CONTROL TABLE * OPEN ACCESSED PARTITIONS
 END-EXEC.


******************************************************************
* Im Folgenden werden die benöetigten CURSOR auf die
* verschiedenen SQL - Tabellen definiert
******************************************************************
**Cursor auf Tabelle FCPARAM
 EXEC SQL
     DECLARE FCPARAM_CURS CURSOR FOR
         SELECT   ROUTKZ, CARDID, ISONTYP, KZ_MSG, BMP, LFDNR
                 ,KZ_ABWEICHUNG
           FROM  =FCPARAM
          WHERE   ROUTKZ = :ROUTKZ  of FCPARAM
*G.00.05 - Anfang
            AND   APPKZ  = "R7"
*G.00.05 - Ende

*kl20180405 - G.01.04 - wg. Prioriseirung CARDID=X vor CARDID=0
*          ORDER  BY ROUTKZ, CARDID, ISONTYP, BMP, LFDNR
          ORDER  BY ROUTKZ          ASC,
                    CARDID          DESC,
                    ISONTYP         ASC,
                    BMP             ASC,
                    LFDNR           ASC
*kl20180405 - G.01.04 - Ende

         BROWSE  ACCESS
 END-EXEC

**Cursor auf Tabelle KEYNAMEN
 EXEC SQL
     DECLARE KEYNAMEN_CURS CURSOR FOR
         SELECT   ROUTKZ, CARDID, KEYNAME, ISOGEN, ISOVERS
           FROM  =KEYNAMEN
*G.02.01 - alle laden
*          WHERE   ROUTKZ = :ROUTKZ of KEYNAMEN
         ORDER  BY ROUTKZ, CARDID
*G.02.01 - Ende
         BROWSE  ACCESS
 END-EXEC

 EXEC SQL
      DECLARE OUTDOOR_CURS CURSOR FOR
          SELECT B.WG_TS
          FROM   =WGRSTPRF A, =ARTMAP B
          WHERE  A.WG = B.WG_HOST
          AND    B.MDNR,
                 B.TSNR,
                 B.CARDID,
                 A.OUTDOOR
             =  :MDNR      OF ARTMAP
               ,:TSNR      OF ARTMAP
               ,:CARDID    OF ARTMAP
               ,"J"
          AND    A.RST_CODE
            <= :RST-CODE OF WGRSTPRF
          FOR BROWSE ACCESS
 END-EXEC.

******************************************************************
* Ende der SQL - Definitionen
******************************************************************

 PROCEDURE DIVISION.

******************************************************************
* Die folgenden WHENEVER-Anweisungen legen Fehlerbehandlungen fest
******************************************************************
 A000-WHENEVER SECTION.
 A000-00.
     EXEC SQL
         WHENEVER SQLERROR       PERFORM :Z001-SQLERROR
     END-EXEC

     EXEC SQL
         WHENEVER SQLWARNING     PERFORM :Z001-SQLERROR
     END-EXEC
     .
 A000-99.
     EXIT.

******************************************************************
* Steuerungs-Section
******************************************************************
 A100-STEUERUNG SECTION.
 A100-00.
**  ---> wenn SWICH-15 gesetzt ist
**  ---> nur Umwandlungszeitpunkt zeigen und dann beenden
     IF  SHOW-VERSION
         DISPLAY K-MODUL " vom: " FUNCTION WHEN-COMPILED
         CALL "WSYS022" USING GEN-ERROR SQLCA
         STOP RUN
     END-IF

**  ---> Vorlauf: oeffnen Dateien etc.
     PERFORM B000-VORLAUF

**  ---> solange verarbeiten bis
**  ---> Datei $RECEIVE auf AT END kommt
     PERFORM UNTIL PRG-NOK
             OR    MSG-EOF

**      ---> holen momentanen Zeitpunkt
         PERFORM U200-TIMESTAMP
         MOVE TAGESDATUM TO H-ZP-IN

**      ---> Bearbeiten Nachricht
         EVALUATE IMSG-MONNAME (1:2)
             WHEN "TS"
**                      ---> Transaktion beginnen
                         PERFORM U100-BEGIN
**                      ---> Anfrage bearbeiten
                         PERFORM B100-VERARBEITUNG
**                      ---> Transaktion beenden
                         PERFORM U110-COMMIT
             WHEN OTHER
                         MOVE 1004 TO ERROR-NR of GEN-ERROR
                         STRING  IMSG-MONNAME (1:2)
                                 "@"
                         delimited by size
                         INTO    DATEN-BUFFER1
                         END-STRING
                         PERFORM Z002-PROGERR
                         SET ENDE TO TRUE
         END-EVALUATE

**      ---> Schreiben Reply pathsend
         PERFORM S000-SCHREIBEN-REPLY
         IF  NOT MSG-EOF
**          ---> Initialisierung Felder
             PERFORM C000-INIT
**          ---> lesen Message-Datei
             PERFORM R000-LESEN-MESSAGE
         END-IF

     END-PERFORM

**  ---> Nachlauf: Dateien schiessen
     PERFORM B090-ENDE
     STOP RUN
     .
 A100-99.
     EXIT.

******************************************************************
* Vorlauf
******************************************************************
 B000-VORLAUF SECTION.
 B000-00.
**  ---> Initialisierung Felder
     PERFORM C000-INIT

*G.02.01 - neuen Parameter AS-VERF laden und nicht mehr AS-ROUTKZ
***  ---> holen Parameter AS-ROUTKZ
*     MOVE "AS-ROUTKZ" TO STUP-PORTION
*     PERFORM P950-GETPARAMTEXT
*     IF  PRG-ABBRUCH
*         EXIT SECTION
*     END-IF
     
**  ---> holen Parameter AS-VERF
     MOVE "AS-VERF" TO STUP-PORTION
     PERFORM P950-GETPARAMTEXT
     IF  PRG-ABBRUCH
         EXIT SECTION
     END-IF


**  ---> holen Parameter für zuständiges AS
     MOVE STUP-TEXT (1:STUP-RESULT) TO VERF-AS
                                       ROUTKZ of FCPARAM
*                                       W-ROUTKZ
                                       S-ROUTKZ
***                                    ---> für Artikelmapper
*                                       VERF-ROUTKZ     
*G.02.01 - Ende     

**  ---> Anwendung setzen für Artikelmapper
**  ---> (die auf Kommentar gesetzten sind default (AG))

     EVALUATE TRUE

**       WHEN VERF-AG    SET AS-VERF-AG TO TRUE
**       WHEN VERF-AV    SET AS-VERF-AV TO TRUE

         WHEN VERF-BP    SET AS-VERF-BP TO TRUE
         WHEN VERF-DK    SET AS-VERF-DK TO TRUE
         WHEN VERF-OR    SET AS-VERF-OR TO TRUE
         WHEN VERF-SH    SET AS-VERF-SH TO TRUE

**       WHEN VERF-TN    SET AS-VERF-TN TO TRUE

         WHEN VERF-TO    SET AS-VERF-TO TO TRUE
         WHEN VERF-UT    SET AS-VERF-UT TO TRUE

         WHEN OTHER      SET AS-VERF-DEFAULT TO TRUE

     END-EVALUATE

**  ---> interne Tabelle initialisieren
*kl20180109 - G.01.01 - Initialisieren mit T-TAB-MAX 
*                       statt Fixwert 200/500
     MOVE   T-TAB-MAX       TO T-MAX
*kl20180109 - G.01.01 - Ende
     PERFORM VARYING C4-I1 FROM 1 BY 1
             UNTIL   C4-I1 > T-TAB-MAX
         INITIALIZE T-KEY (C4-I1)
     END-PERFORM

**  ---> ersten Eintrag holen
     PERFORM S900-OPEN-FCPARAM-CURSOR
     PERFORM S910-FETCH-FCPARAM-CURSOR

**  ---> Schleife über alle Einträge für das ROUTKZ 
     PERFORM VARYING C4-I1 FROM 1 BY 1
             UNTIL   FCPARAM-EOD
             OR      FCPARAM-NOK
             OR      C9-COUNT > T-TAB-MAX

         MOVE ROUTKZ        of FCPARAM TO T-ROUTKZ        (C4-I1)
         MOVE CARDID        of FCPARAM TO T-CARDID        (C4-I1)
         MOVE ISONTYP       of FCPARAM TO T-ISONTYP       (C4-I1)
         MOVE KZ-MSG        of FCPARAM TO T-KZ-MSG        (C4-I1)
         MOVE BMP           of FCPARAM TO T-BMP           (C4-I1)
         MOVE LFDNR         of FCPARAM TO T-LFDNR         (C4-I1)
         MOVE KZ-ABWEICHUNG of FCPARAM TO T-KZ-ABWEICHUNG (C4-I1)

**      ---> nächsten Eintrag holen
         PERFORM S910-FETCH-FCPARAM-CURSOR

     END-PERFORM

**  ---> schliessen Cursor
     PERFORM S920-CLOSE-FCPARAM-CURSOR
     MOVE C9-COUNT TO T-MAX

**  ---> Fehlerkonstellationen
     IF  FCPARAM-NOK
         MOVE "Fehler beim Laden der Tabelle =FCPARAM" TO
         DATEN-BUFFER1 MOVE "Programm-Abbruch" TO  DATEN-BUFFER2
         SET PRG-ABBRUCH TO TRUE
         EXIT SECTION
     END-IF
*kl20180105 - G.02.05 - T-TAB-MAX enthaelt maximal zulaessige Anzahl
*     IF  C9-COUNT > T-MAX
     IF  C9-COUNT > T-TAB-MAX
*kl20180105 - G.02.05 - Ende

         MOVE "Programm interne Tabelle zu klein" TO  DATEN-BUFFER1
         MOVE "mehr als 200 Einträge in =FCPARAM " TO  DATEN-BUFFER2
         MOVE ROUTKZ OF FCPARAM TO D-NUM4
         STRING  "für ROUTKZ = "     delimited by size
                 D-NUM4              delimited by size
           INTO  DATEN-BUFFER3
         END-STRING
         MOVE "Programm-Abbruch" TO  DATEN-BUFFER4
         SET PRG-ABBRUCH TO TRUE
         EXIT SECTION
     END-IF

*G.02.01 - zusätzlich AIID mit in diese Tabelle laden + alle Eintraege aus KEYNAMEN
**  ---> AS Schlüssel MACKEYA und PACKEYA aus Tabelle =KEYNAMEN einlesen
**  ---> !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
**  ---> !!!! zunächstmal wird nur der erste eingelesen !!!!
**  ---> !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
**  ---> ersten Eintrag holen
*     MOVE W-ROUTKZ TO ROUTKZ of KEYNAMEN
     PERFORM S930-OPEN-KEYNAMEN-CURSOR
     PERFORM S940-FETCH-KEYNAMEN-CURSOR

**  ---> Schleife über alle Einträge für das ROUTKZ
     PERFORM VARYING C4-I1 FROM 1 BY 1
             UNTIL   KEYNAMEN-EOD
             or      KEYNAMEN-NOK
             or      C9-COUNT > TK-TAB-MAX
**           or      c4-i1 > 1

         MOVE ROUTKZ  of KEYNAMEN TO TK-ROUTKZ  (C4-I1)
         MOVE CARDID  of KEYNAMEN TO TK-CARDID  (C4-I1)
         MOVE KEYNAME of KEYNAMEN TO TK-KEYNAME (C4-I1)
         MOVE ISOGEN  of KEYNAMEN TO TK-ISOGEN  (C4-I1)
         MOVE ISOVERS of KEYNAMEN TO TK-ISOVERS (C4-I1)
**      ---> Keynamen hexen
         MOVE TK-KEYNAME (C4-I1) TO HEX16
         PERFORM P900-WTHEX
         MOVE HEX8 TO TK-HEXKEY (C4-I1)
**      ---> Generation und Version hexe
         MOVE TK-ISOGEN  (C4-I1) TO HEX16
         MOVE TK-ISOVERS (C4-I1) TO HEX16 (3:2)
         PERFORM P900-WTHEX
         MOVE HEX8 (1:1) TO TK-HEXISO (C4-I1)
         MOVE HEX8 (2:1) TO TK-HEXISO (C4-I1) (2:1)
         
         PERFORM S960-SELECT-AIID
         MOVE AIID OF FCAIID TO TK-AIID(C4-I1)

**      ---> nächsten Eintrag holen
         PERFORM S940-FETCH-KEYNAMEN-CURSOR

     END-PERFORM

**  ---> schliessen Cursor
     PERFORM S950-CLOSE-KEYNAMEN-CURSOR
     MOVE C9-COUNT TO TK-MAX
     IF  TK-MAX = 1
         MOVE TK-HEXKEY (1) TO W-MACKEYA
         MOVE TK-HEXKEY (1) TO W-PACKEYA (1:4)
     END-IF
*G.02.01 - Ende

**  ---> Holen Terminal-Schlüsselnamen
**     > Ungepackten MAC(Terminal)-SchlüsselID holen
     MOVE "MACKEYT" TO STUP-PORTION
     PERFORM P950-GETPARAMTEXT
     IF  PRG-ABBRUCH
         SET PRG-ABBRUCH TO TRUE
         EXIT SECTION
     END-IF

     MOVE STUP-TEXT(1:STUP-RESULT) TO HEX16
     PERFORM P900-WTHEX
     MOVE HEX8 TO W-MACKEYT

**     > Ungepackten PAC(Terminal)-SchluesselID holen
     MOVE "PACKEYT" TO STUP-PORTION
     PERFORM P950-GETPARAMTEXT
     IF  PRG-ABBRUCH
         SET PRG-ABBRUCH TO TRUE
         EXIT SECTION
     END-IF

     MOVE STUP-TEXT(1:STUP-RESULT) TO HEX16
     PERFORM P900-WTHEX
     MOVE HEX8 TO W-PACKEYT

**Boxen-Monitor bestimmen
     MOVE "BOXMON" TO STUP-PORTION
     PERFORM P950-GETPARAMTEXT
     IF  PRG-ABBRUCH
         SET PRG-ABBRUCH TO TRUE
         EXIT SECTION
     END-IF

     MOVE STUP-TEXT(1:STUP-RESULT) TO W66-BOXMON-TEXT

**Artikelmapper bestimmen
     MOVE "ARTMAP" TO STUP-PORTION
     PERFORM P950-GETPARAMTEXT
     IF  PRG-ABBRUCH
         SET PRG-ABBRUCH TO TRUE
         EXIT SECTION
     END-IF

     MOVE STUP-TEXT(1:STUP-RESULT) TO W-ARTMAP

**Parameter WGRSTPRF bestimmen

     MOVE "WGRSTPRF" TO STUP-PORTION
     PERFORM P950-GETPARAMTEXT
     IF  PRG-ABBRUCH
         EXIT SECTION
     END-IF

     MOVE STUP-TEXT (1:STUP-RESULT) TO W-RST-FLAG

**Prozess- und ANCNAME bestimmen
     PERFORM P960-GET-PROC-ANCNAME

*Oeffnen $RECEIVE

     IF  PRG-OK
         OPEN INPUT  MESSAGE-DATEI
         OUTPUT REPLY-DATEI

**1. Lesen Message-Datei
         PERFORM R000-LESEN-MESSAGE
     END-IF
     .
 B000-99.
     EXIT.

******************************************************************
* Ende
******************************************************************
 B090-ENDE SECTION.
 B090-00.
     continue
*** =>
*** => weitere Verarbeitung hier einfügen
*** =>

**  ---> schliessen $RECEIVE
     IF  PRG-OK
         CLOSE MESSAGE-DATEI
     END-IF
     .
 B090-99.
     EXIT.

******************************************************************
* Verarbeitung
******************************************************************
 B100-VERARBEITUNG SECTION.
 B100-00.
**  --> Fregat-Angaben merken
     MOVE IMSG-HEADER  TO W-FRE-HEADER
     MOVE IMSG-TERMID  TO W-FRE-TERMID
     MOVE IMSG-MONNAME TO W-FRE-MONNAME
     MOVE IMSG-DATLEN  TO W-FRE-DATLEN
*G.02.01 - ROUTKZ von Drehscheibe + Laden Schluessel
     MOVE IMSG-ROUTKZ  TO W-ROUTKZ   
*G.02.01 - Ende

**  ---> Kontrolle der Anfrage <---
**  ---> ist die Anfrage evtl. OK?

     PERFORM C100-ANFRAGE-CHECK

     IF  ENDE
         EXIT SECTION
     END-IF
     IF  W-AC NOT = ZERO
**      ---> Ablehnung senden: Formatfehler
         PERFORM E100-FEP-ANTWORT
         EXIT SECTION
     END-IF

**  ---> holen Erweiterungsdate der Station (hier für: GEODATEN)
     PERFORM S170-SELECT-STATIONA
     IF  ENDE
         EXIT SECTION
     END-IF

**  ---> für alle AS'sen gültige Transaktions Regeln
     PERFORM C200-AS-GENERELL
     IF  ENDE
         EXIT SECTION
     END-IF

**  ---> spezielle Regeln für AS'sen
     PERFORM C300-AS-SPEZIELL
     IF  ENDE
         EXIT SECTION
     END-IF

     PERFORM C110-SPERRLISTEN-PRUEFUNG
     IF  ENDE
         EXIT SECTION
     END-IF
     IF  W-AC NOT = ZERO
*Ablehnung senden: Formatfehler
         PERFORM E100-FEP-ANTWORT
         EXIT SECTION
     END-IF

     PERFORM C120-LIMIT-PRUEFUNG
     IF  ENDE
         EXIT SECTION
     END-IF
     IF  W-AC NOT = ZERO
*Ablehnung senden: Formatfehler
         PERFORM E100-FEP-ANTWORT
         EXIT SECTION
     END-IF

     PERFORM C130-RESTRIKTIONEN-PRUEFUNG
     IF  ENDE
         EXIT SECTION
     END-IF
     IF  W-AC NOT = ZERO
*Ablehnung senden: Formatfehler
         PERFORM E100-FEP-ANTWORT
         EXIT SECTION
     END-IF

**  ---> und hier die AS-Nachricht zusammenbauen
     PERFORM C400-BUILD-AS-NACHRICHT
     IF  ENDE
         EXIT SECTION
     END-IF

**  ---> Antwort ans Terminal aufbereiten
     PERFORM E100-FEP-ANTWORT

**  ---> Schreiben der Log-Daten
     PERFORM C500-LOGGING
     IF  ENDE
         EXIT SECTION
     END-IF
     .
 B100-99.
     EXIT.

******************************************************************
* Initialisierung von Feldern und Strukturen
******************************************************************
 C000-INIT SECTION.
 C000-00.
     INITIALIZE SCHALTER
                GEN-ERROR
                TXILOG70
                TXNLOG70-TS
                ASYNC70
                WORK-INIT
                WGRSTPRF
                ARTMAP

     MOVE ZERO TO W-AC
     .
 C000-99.
     EXIT.

******************************************************************
* Kontrolle Eingangsnachricht
******************************************************************
 C100-ANFRAGE-CHECK SECTION.
 C100-00.
**  ---> interne Schnittstelle sichern
     MOVE INTERN-MESSAGE TO TS-INTERN-MESSAGE

**  ---> erstmal termid für ERRLOG aufbereiten
     IF  IMSG-TBMP(41) = 1 AND IMSG-TPTR(41) > 0 AND IMSG-TLEN(41) > 0
         MOVE IMSG-CF(IMSG-TPTR(41):IMSG-TLEN(41)) TO W-TERMNR
         MOVE W-TERMNR TO HEX16
         PERFORM P900-WTHEX
         MOVE HEX8(1:4) TO TERMID of APPL-SPEC-BUF
     END-IF

**  ---> Trace-Nr. schon malmerken
     IF  IMSG-TBMP(11) = 1 AND IMSG-TPTR(11) > 0 AND IMSG-TLEN(11) > 0
         MOVE IMSG-CF(IMSG-TPTR(11):IMSG-TLEN(11)) TO W-TRACENR
     END-IF

**  ---> weitere Felder merken
     MOVE IMSG-MDNR TO MDNR OF TXILOG70
                       MDNR OF TXNLOG70-TS
                       MDNR OF STATION
                       MDNR OF STATIONA
                       MDNR OF ASYNC70
                       MDNR OF ARTMAP
                       W-MDNR

     MOVE IMSG-TSNR TO TSNR OF TXILOG70
                       TSNR OF TXNLOG70-TS
                       TSNR OF STATION
                       TSNR OF STATIONA
                       TSNR OF ASYNC70
                       TSNR OF ARTMAP
                       W-TSNR

     MOVE W-TERMNR  TO TERMNR OF TXILOG70
                       TERMNR OF TXNLOG70-TS
                       TERMNR OF ASYNC70
**
*Identifizierung Indoor oder Outdoor
*C-OPT-OUTDOOR = true, dann Automatenverarbeitung
*

     IF IMSG-TBMP(25) = 1
        SET C-OPT-OUTDOOR OF CSYS-FLAG TO TRUE
     ELSE
        SET C-POS-INDOOR  OF CSYS-FLAG TO TRUE
     END-IF

**Zunächst den MAC prüfen, sofern vorhanden
     SET MAC-NO TO TRUE
     IF  IMSG-TBMP(64) = 1
         SET MAC-YES TO TRUE
         SET W66-EC  TO TRUE
         PERFORM F910-MAC-PRUEFEN
         IF  W-AC > ZERO
             SET ENDE TO TRUE
             EXIT SECTION
         END-IF
     END-IF

**  ---> Station zum Terminal lesen
     PERFORM S160-SELECT-STATION
     IF  ENDE
         EXIT SECTION
     END-IF
**  ---> Nachrichtentyp bestimmen (Buchung)
     MOVE IMSG-NTYPE    TO W-NTYPE

**  ---> Abwicklungs-KZ bestimmen (Zahlung/Gutschrift)
     MOVE IMSG-CF(IMSG-TPTR(03):IMSG-TLEN(03)) TO W-ABWKZ
     MOVE IMSG-CF(IMSG-TPTR(03) + 2:4)         TO W-BELEGNR

**  ---> Betrag aufbereiten und merken
     MOVE IMSG-CF(IMSG-TPTR(04):IMSG-TLEN(04)) TO W-BETRAG
     COMPUTE W18-BETRAG = W-BETRAG / 100

*G.01.05 - Anfang
*
*--> ZP-VERKAUF - Berechnung jetzt über Modul ZPVERK
*
**  ---> Verkaufszeitpunkt aufbereiten (20 + BMP13 + BMP12)
*     MOVE IMSG-CF(IMSG-TPTR(12):IMSG-TLEN(12)) TO D-NUM6
*     MOVE IMSG-CF(IMSG-TPTR(13):IMSG-TLEN(13)) TO D-NUM12
*     COMPUTE D-NUM12      = D-NUM12 * 1000000
*     COMPUTE W-ZP-VERKAUF = (10000000000 * TAL-JHJJ of TAL-TIME-D)
*                          + D-NUM12 + D-NUM6


*--> Werte fuer ZP_VERKAUF,  Format:JJJJMMTThhmmss
*--> Z-ZPVERKAUF-RC = 0 ==> alles ok, ZPVERKAUF-TXILOG70 berechnet
*                   > 0 ==> Fehler,   ZPVERKAUF-TXILOG70 = 0
*                   = 1 ==> BMP 12 oder 13 nicht numerisch
*                   = 2 ==> Fehler bei der Berechnung mit HCTIME

      MOVE 1                                    TO ZPVERKAUF-RC
      MOVE TAL-TIME-N                           TO ZPVERKAUF-FEPTALZEIT
      MOVE IMSG-CF(IMSG-TPTR(12):IMSG-TLEN(12)) TO ZPVERKAUF-BMP12
      MOVE IMSG-CF(IMSG-TPTR(13):IMSG-TLEN(13)) TO ZPVERKAUF-BMP13
      MOVE 0                                    TO ZPVERKAUF-TXILOG70
      CALL "ZPVERK" USING ZPVERKAUF-IFC
      MOVE ZPVERKAUF-TXILOG70                   TO W-ZP-VERKAUF

      EVALUATE ZPVERKAUF-RC
**      ---> OK
          WHEN ZERO   CONTINUE

**      ---> Numerik Fehler
          WHEN 1      MOVE "Info: Fehler bei der Berechnung ZP-VERKAUF"
                           TO DATEN-BUFFER1
                      MOVE "BMP 12 oder 13 nicht numerisch"
                           TO DATEN-BUFFER2
                      MOVE "In TXILOG70.ZP-VERKAUF wird Null eingetragen."
                           TO DATEN-BUFFER3
                      PERFORM Z002-PROGERR

**      ---> HCTIME Fehler
          WHEN 2      MOVE "Info: Fehler bei der Berechnung ZP-VERKAUF"
                           TO DATEN-BUFFER1
                      MOVE "Zeitberechnung über das Modul HCTIME fehlerhaft "
                           TO DATEN-BUFFER2
                      MOVE "In TXILOG70.ZP-VERKAUF wird Null eingetragen."
                           TO DATEN-BUFFER3
                      PERFORM Z002-PROGERR
      END-EVALUATE
*G.01.05 - Ende


**  ---> holen Cardid
     PERFORM D900-GET-CARDID
     IF  W-AC NOT = ZERO
         EXIT SECTION
     END-IF

**  ---> holen zuständigen Keynamen
     PERFORM D200-FIX-KEY
**  ---> setzen Keyname für diese Transaktion
     MOVE TK-KEYNAME (C4-I1) TO W-KEYNAME
     MOVE TK-HEXISO (C4-I1)  TO W-ISOGEN-VERS

**  ---> Handeingabe erlaubt? Dann ist BMP35 Pflicht
**  ---> S-ROUTKZ und S-CARDID sind schon gesetzt
     MOVE 200  TO S-ISONTYP
     MOVE "TS" TO S-KZ-MSG
     MOVE 35   TO S-BMP
     MOVE 1    TO S-LFDNR
     PERFORM U300-SEARCH-TAB
     IF  PRM-FOUND
         IF  T-KZ-ABWEICHUNG (T-AKT-IND) = "1"
             IF  not IMSG-TBMP(35) = 1
                 SET ERF-ERROR TO TRUE
                 MOVE 30  TO W-AC
                 MOVE 2201 TO ERROR-NR of GEN-ERROR
                 MOVE "BMP35 fehlt@" TO DATEN-BUFFER1
                 MOVE "Handeingabe nicht erlaubt" TO DATEN-BUFFER2
                 PERFORM Z002-PROGERR
                 EXIT SECTION
             END-IF
         END-IF
     END-IF

**  ---> Prüfen BMP35 , wenn nicht vorhanden, dann keine Verarbeitung
     MOVE ZERO TO C4-ANZ
     MOVE 1    TO C4-PTR
     MOVE SPACES     TO W-DELIM-TABELLE
     MOVE LOW-VALUES TO W-COUNT-TABELLE

     IF  IMSG-TBMP(35) NOT = 1
*Fehler:BMP35 nicht vorhanden, keine Prüfung möglich
         SET ERF-ERROR TO TRUE
         MOVE 30  TO W-AC
         MOVE 2201 TO ERROR-NR of GEN-ERROR
         MOVE "BMP35 (Spur2-Daten) ungültig" TO DATEN-BUFFER1
         PERFORM Z002-PROGERR
         EXIT SECTION
     ELSE
*Spur 2 ist vorhanden
         SET ERF-SPUR2 TO TRUE
         MOVE IMSG-CF(IMSG-TPTR(35):IMSG-TLEN(35)) TO W-SPUR2

         UNSTRING W-SPUR2
         delimited by "D" or "="
             INTO W-TEILSTRING (1)  delimiter in W-DELIM (1)
                                    count     in W-COUNT (1)
                  W-TEILSTRING (2)  delimiter in W-DELIM (2)
                                    count     in W-COUNT (2)
             with pointer C4-PTR
             tallying in  C4-ANZ
         END-UNSTRING

         MOVE W-TEILSTRING (1) TO W-KANR
         MOVE W-COUNT (1)      TO W-KANR-LEN
         IF  C4-ANZ > 1
             MOVE W-TEILSTRING (2) (1:4) TO W-ABL
         ELSE
             MOVE ZEROES                 TO W-ABL
         END-IF

*Uebernehmen Spur2 in Kartenprofil (je nach Kartenart)
         EVALUATE TRUE
             WHEN DKV
                  MOVE W-SPUR2                 TO W-TRACK2-DKV
                  MOVE W-DKV-RSTCODE           TO RST-CODE OF WGRSTPRF
             WHEN NOVOFLEET
                  MOVE W-SPUR2                 TO W-TRACK2-NOVOFLEET
                  MOVE W-NVF-RSTCODE           TO RST-CODE OF WGRSTPRF

             WHEN OTHER
                  SET ERF-ERROR TO TRUE
                  MOVE 30  TO W-AC
                  MOVE 2904 TO ERROR-NR of GEN-ERROR
                  MOVE "DKV oder NOVEFLEET" TO DATEN-BUFFER1
                  PERFORM Z002-PROGERR
                  EXIT SECTION
         END-EVALUATE
     END-IF

**  ---> Erfassungsart (BMP22)
     MOVE IMSG-CF(IMSG-TPTR(22) + 1:2) TO W-ERFASSUNGS-ART
     IF  (W-ERFASSUNGS-ART = "01" and not ERF-MANUELL)
     or  (W-ERFASSUNGS-ART = "02" and not ERF-SPUR2)
         SET ERF-ERROR TO TRUE
         MOVE 30  TO W-AC
         MOVE 2201 TO ERROR-NR of GEN-ERROR
         MOVE "Kombination aus:@" TO DATEN-BUFFER1
         MOVE "BMP22 und BMP2,14,35 nicht korrekt" TO DATEN-BUFFER2
         PERFORM Z002-PROGERR
         EXIT SECTION
     END-IF

* Transaktionen aus Rumänien nicht erlaubt im SIP-Verfahren
     IF IMSG-CF (IMSG-TPTR(49):IMSG-TLEN(49)) = "0946"
        MOVE 02 TO W-AC
        EXIT SECTION
     END-IF

**  ---> ggf. BMP25

     IF  IMSG-TBMP(37) = 1
         MOVE 30  TO W-AC
         MOVE 2201 TO ERROR-NR of GEN-ERROR
         MOVE "Feld zuviel (BMP37)@" TO DATEN-BUFFER1
         PERFORM Z002-PROGERR
         EXIT SECTION
     END-IF

**  ---> Währungs-KZ könnte hier gegen Tabelle WKZKURS geprüft werden
     MOVE IMSG-CF (IMSG-TPTR (49) + 1:3) TO W-WKZ
     MOVE W-WKZ TO WKZ-WKZ
     SET WKZ-CMD-WKZ-A TO TRUE
     PERFORM M170-CALL-SYSAWKZ
     IF  WKZ-ERR
         EXIT SECTION
     END-IF

**  ---> Artikeldaten müssen vorhanden sein
     IF  IMSG-TBMP(63) = 1
         MOVE IMSG-CF(IMSG-TPTR(63) + 32:1)  TO HEX8
         PERFORM P910-WTUNHEX
*G.06.03 - Anfang - BMP63 ohne Artikel
*        IF  HEX16(1:2) = "00"
**
         IF  IMSG-TLEN(63) = 33
*G.06.03 - Ende - BMP63 ohne Artikel

             MOVE 30   TO W-AC
             MOVE 2201 TO ERROR-NR of GEN-ERROR
             MOVE "Autorisierung OHNE Artikel@" TO DATEN-BUFFER1
             PERFORM Z002-PROGERR
             EXIT SECTION
         END-IF
     END-IF

** BMP63 auseindanderfieseln für Restriktionsprüfung
** Pruefen, ob in BMP63 etwas sinnvolles steht

     MOVE ALL SPACES                            TO WEAT-STRUCT
     MOVE IMSG-CF(IMSG-TPTR(63):IMSG-TLEN(63))  TO WEAT-STRUCT
     MOVE ZEROS                                 TO W-BUFFER-LEN

*---> Entpacken Infodaten

     MOVE WEAT-KM           TO HEX8
     ENTER TAL "WT^UNHEX" USING HEX8, HEX16
     IF HEX16(1:6) NUMERIC
        MOVE HEX16(1:6)     TO UP-KM
     ELSE
        MOVE ZEROES         TO UP-KM
     END-IF

     ADD  3                 TO W-BUFFER-LEN

     MOVE WEAT-KFZNR TO HEX8
     ENTER TAL "WT^UNHEX" USING HEX8, HEX16
     IF HEX16(1:4) NUMERIC
        MOVE HEX16(1:4)     TO UP-KFZNR
     ELSE
        MOVE ZEROES         TO UP-KFZNR
     END-IF

     ADD  2                 TO W-BUFFER-LEN

     MOVE WEAT-FAHRER TO HEX8
     ENTER TAL "WT^UNHEX" USING HEX8, HEX16
     IF HEX16(1:4) NUMERIC
        MOVE HEX16(1:4)     TO UP-FAHRER
     ELSE
        MOVE ZEROES         TO UP-FAHRER
     END-IF

     ADD  2                 TO W-BUFFER-LEN

     MOVE WEAT-ERFASSUNG TO HEX8
     ENTER TAL "WT^UNHEX" USING HEX8, HEX16
     IF HEX16(1:6) NUMERIC
        MOVE HEX16(1:6)     TO UP-ERFASSUNG
     ELSE
        MOVE ZEROES         TO UP-ERFASSUNG
     END-IF

     ADD  3                 TO W-BUFFER-LEN

     MOVE WEAT-INFO         TO UP-INFO
     ADD  20                TO W-BUFFER-LEN

     MOVE WEAT-EINGABE TO HEX8
     ENTER TAL "WT^UNHEX" USING HEX8, HEX16
     IF HEX16(1:2) NUMERIC
        MOVE HEX16(1:2)     TO UP-EINGABE
     ELSE
        MOVE ZERO           TO UP-EINGABE
     END-IF
     ADD  1                 TO W-BUFFER-LEN

*Filler PIC X(01) wird berücksichtigt
     ADD 1                  TO W-BUFFER-LEN

*Entpacken und pruefen Counter

     MOVE WEAT-COUNTER TO HEX8
     ENTER TAL "WT^UNHEX" USING HEX8, HEX16
     IF HEX16(1:2) NUMERIC
        MOVE HEX16(1:2)     TO UP-COUNTER
        ADD  1              TO W-BUFFER-LEN
     ELSE
        MOVE 30             TO W-AC
        STRING "Numericfehler BMP63 - Counter=", HEX16(1:2)
        DELIMITED BY SIZE INTO DATEN-BUFFER1
        PERFORM Z002-PROGERR
        EXIT SECTION
     END-IF

     IF UP-COUNTER > 20
        STRING "Zu viele Artikel: " UP-COUNTER
        DELIMITED BY SIZE INTO DATEN-BUFFER1
        PERFORM Z002-PROGERR
        MOVE 30              TO W-AC
        EXIT SECTION
     END-IF

*Entpacken Artikel

     PERFORM VARYING C4-I1 FROM 1 BY 1 UNTIL C4-I1 > UP-COUNTER

      MOVE WEAT-PROD-CODE(C4-I1) TO HEX8
      ENTER TAL "WT^UNHEX" USING HEX8, HEX16
      IF HEX16(1:6) NUMERIC
         MOVE HEX16(1:6)     TO UP-PROD-CODE(C4-I1)
         ADD  3              TO W-BUFFER-LEN
      ELSE
         MOVE C4-I1          TO D-NUM4
         MOVE 30             TO W-AC
         STRING "Numericfehler BMP63 - Artikelpos=", D-NUM4, " - Warengruppe"
         DELIMITED BY SIZE INTO DATEN-BUFFER1
         PERFORM Z002-PROGERR
         EXIT SECTION
      END-IF

      MOVE WEAT-ART-EINZELPREIS(C4-I1) TO HEX8
      ENTER TAL "WT^UNHEX" USING HEX8, HEX16
      IF HEX16(1:8) NUMERIC
         MOVE HEX16(1:8)     TO UP-ART-EINZELPREIS(C4-I1)
         ADD  8              TO W-BUFFER-LEN
      ELSE
         MOVE C4-I1          TO D-NUM4
         MOVE 30             TO W-AC
         STRING "Numericfehler BMP63 - Einzelpreis=", D-NUM4, " - Einzelpreis"
         DELIMITED BY SIZE INTO DATEN-BUFFER1
         PERFORM Z002-PROGERR
         EXIT SECTION
      END-IF

      MOVE WEAT-QNT(C4-I1) TO HEX8
      ENTER TAL "WT^UNHEX" USING HEX8, HEX16
      IF HEX16(1:6) NUMERIC
         MOVE HEX16(1:6)     TO UP-QNT(C4-I1)
         ADD  3              TO W-BUFFER-LEN
      ELSE
         MOVE C4-I1          TO D-NUM4
         MOVE 30             TO W-AC
         STRING "Numericfehler BMP63 - Artikelpos=", D-NUM4, " - Menge"
         DELIMITED BY SIZE INTO DATEN-BUFFER1
         PERFORM Z002-PROGERR
         EXIT SECTION
      END-IF

      MOVE WEAT-AMNT(C4-I1) TO HEX8
      ENTER TAL "WT^UNHEX" USING HEX8, HEX16
      IF HEX16(1:10) NUMERIC
         MOVE HEX16(1:10)     TO UP-AMNT(C4-I1)
         ADD  5               TO W-BUFFER-LEN
      ELSE
         MOVE C4-I1          TO D-NUM4
         MOVE 30             TO W-AC
         STRING "Numericfehler BMP63 - Artikelpos=", D-NUM4, " - Betrag"
         DELIMITED BY SIZE INTO DATEN-BUFFER1
         PERFORM Z002-PROGERR
         EXIT SECTION
      END-IF

      EVALUATE WEAT-VZ(C4-I1)

         WHEN " "    MOVE WEAT-VZ(C4-I1)   TO UP-VZ(C4-I1)
                     ADD  1                TO W-BUFFER-LEN
         WHEN "-"    MOVE WEAT-VZ(C4-I1)   TO UP-VZ(C4-I1)
                     ADD  1            TO W-BUFFER-LEN
         WHEN OTHER  MOVE C4-I1        TO D-NUM4
                     STRING "Vorzeichen ungueltig: " WEAT-VZ(C4-I1)
                            " - Position: " D-NUM4
                     DELIMITED BY SIZE INTO DATEN-BUFFER1
                     PERFORM Z002-PROGERR
                     MOVE 30           TO w-AC
                     EXIT SECTION

      END-EVALUATE
     END-PERFORM

     .
 C100-99.
      EXIT.

******************************************************************
* Kontrolle Sperrlisten über Pahtsend zum Server WTBLL06S
* Der Server WTBLL06 wird in R7 mit eingebunden, da Änderungen
* wegen der Version R7 zur Zeit nicht absehbar sind
******************************************************************
 C110-SPERRLISTEN-PRUEFUNG SECTION.
 C110-00.

     PERFORM P110-PSD-SPERRLISTEN-PRUEFUNG

     IF NOT ENDE
        MOVE IMSG-CC TO W-AC
     END-IF
     .
 C110-99.
     EXIT.

******************************************************************
* Kontrolle Sperrlisten
******************************************************************
 C120-LIMIT-PRUEFUNG SECTION.
 C120-00.

**AC=0: Limitpruefung
     PERFORM S600-GET-STIPLIMS
*Fehlermeldung bereits in S600-00
     IF ENDE
        EXIT SECTION
     END-IF
*Ermittlung Betrag für Limitprüfung und FCLOG/UMSATZ
     IF W18-BETRAG <= H-STIP-CALL
        CONTINUE
     ELSE IF W18-BETRAG <= H-STIP-REJECT
*Bei Indoor kann man anrufen
             IF C-POS-INDOOR
                MOVE 02 TO W-AC
*Bei Outdoor natürlich nicht --> Also Ablehnen
             ELSE
                MOVE 12 TO W-AC
             END-IF
             EXIT SECTION
          ELSE
             MOVE 12 TO W-AC
             EXIT SECTION
          END-IF
     END-IF
     .
 C120-99.
      EXIT.

******************************************************************
* Restriktionsprüfung für alle Artikel in Schleifenverarbeitung
******************************************************************

 C130-RESTRIKTIONEN-PRUEFUNG SECTION.
 C130-00.
** Indoor: Restriktionen nur prüfen, wenn der Serverparameter
** WGRSTPRF nicht den Wert "N" beinhaltet
** Der Parameter muss am Sever gesetzt sein, sonst Programmabbruch
**
** W-RST-PRUEFEN - Nur pruefen, wenn die Restriktionspruefung für
** Indoor eingeschaltet ist - W-RST-PRUEFEN true, weil W-RST-FLAG = "J"
**
** Outdoor: Bei Automaten werden die Warengrupen immer geprüft,
**          um die Saeule fuer die erlaubten Produkte freizuschalten

     IF  C-POS-INDOOR
         IF W-RST-PRUEFEN
            PERFORM S700-RESTRIKTIONEN-INDOOR
         END-IF
     ELSE
         PERFORM S702-RESTRIKTIONEN-OUTDOOR
     END-IF

     .
 C130-99.
      EXIT.
******************************************************************
* AS-Anfrageteile, die für alle gleich sind
******************************************************************
 C200-AS-GENERELL SECTION.
 C200-00.
**  ---> im Folgenden noch die Felder BMP 33, 39, 42 hinzufuegen
     MOVE IMSG-COBDATEN TO W207-COBDATEN

**  ---> zunächst die BitMap auf die Pflichtfelder setzen
     MOVE LOW-VALUE       TO W207-TBMP-O
     MOVE K-BYTEMAP-A1220 TO W207-TBMP-O (1:64)

**  ---> Werte für Tabelle =FCPARAM
     MOVE 1220 TO S-ISONTYP
     MOVE "AS" TO S-KZ-MSG

     SET W207-IFSF TO TRUE
**  ---> Nachrichtentyp für Nachricht setzen

*G.01.05 Anfang - Unterscheidung Buchung und Storno entfällt
     MOVE 1220 TO W207-NTYPE
*G.01.05 Ende

**  ---> schon mal einen Dummy-MAC vorbereiten
     MOVE 1220 TO S-ISONTYP
     MOVE "AS" TO S-KZ-MSG
     MOVE 64   TO S-BMP
     MOVE 1    TO S-LFDNR
     PERFORM U300-SEARCH-TAB
     SET MAC-NO TO TRUE
     IF  PRM-FOUND
         IF  T-KZ-ABWEICHUNG (T-AKT-IND) = "1"
             SET MAC-YES TO TRUE
             MOVE 64 TO W207-XBMP
             MOVE 08 TO W207-XCOBLEN
             MOVE ALL LOW-VALUES TO W207-XCOBVAL
             PERFORM L100-ADD-BMP
             IF  ENDE
                 EXIT SECTION
             END-IF
         ELSE
             MOVE ZERO TO W207-TBMP(64)
         END-IF
     END-IF

**  ---> BMP  2 - PAN
     IF  ERF-MANUELL
         MOVE 02         TO W207-XBMP
         MOVE W-KANR     TO W207-XCOBVAL
         MOVE W-KANR-LEN TO W207-XCOBLEN
         PERFORM L100-ADD-BMP
         IF  ENDE
             EXIT SECTION
         END-IF
     END-IF

** - Test
**  ---> BMP  3 - Zahlung/Gutschrift (nur Buffer manipulieren)
**   IF  ABWKZ-ZAHLUNG
**       MOVE "000000" TO W207-CF(W207-TPTR(3):W207-TLEN(3))
**   ELSE
**       MOVE "200000" TO W207-CF(W207-TPTR(3):W207-TLEN(3))
**   END-IF
*** - Ende

     MOVE "000000" TO W207-CF(W207-TPTR(3):W207-TLEN(3))

**  ---> BMP  4 - Betrag - wird übernommen
**  ---> BMP  7 - Übertragungszeit
     MOVE 07 TO W207-XBMP
     MOVE 10 TO W207-XCOBLEN
     STRING  TAL-MM   of TAL-TIME-D
             TAL-TT   of TAL-TIME-D
             TAL-HHMI of TAL-TIME-D
             TAL-SS   of TAL-TIME-D
                 delimited by size
       INTO  W207-XCOBVAL
     END-STRING
**  ---> und den Wert für evtl. Storni sichern
     MOVE W207-XCOBVAL (1:W207-XCOBLEN) TO W-BMP07
     PERFORM L100-ADD-BMP
     IF  ENDE
         EXIT SECTION
     END-IF

**  ---> BMP 11 - Trace-Nr.
**  +++>          AS-Trace-Nr. holen (steht dann in W-AS-TRACENR)
     PERFORM D910-GET-ASTRACENR
     IF  W-AC NOT = ZERO
         EXIT SECTION
     END-IF
**  +++> hier wird nur der Buffer manipuliert
     MOVE W-AS-TRACENR TO W207-CF(W207-TPTR(11):W207-TLEN(11))

**  ---> BMP 12 - Lokalzeit (JJJJMMTThhmmss)
     MOVE 12 TO W207-XBMP
     MOVE 12 TO W207-XCOBLEN
*G.01.06 - Anhand ZP-VERKAUF (Kombination TS BMP 12/13) AS BMP 12 fuellen
     COMPUTE D-NUM12 = W-ZP-VERKAUF - 20000000000000
     MOVE D-NUM12 TO W207-XCOBVAL
*     STRING  TAL-JHJJMMTT of TAL-TIME-D (3:6)
*             TAL-HHMI     of TAL-TIME-D
*             TAL-SS       of TAL-TIME-D
*                 delimited by size
*       INTO W207-XCOBVAL
*     END-STRING
*G.01.06 - Ende
     PERFORM L100-ADD-BMP
     IF  ENDE
         EXIT SECTION
     END-IF

**  ---> BMP 14 - Verfalldatum
     IF  ERF-MANUELL
         MOVE 14 TO W207-XBMP
         MOVE 4  TO W207-XCOBLEN
         MOVE W-ABL TO W207-XCOBVAL
         PERFORM L100-ADD-BMP
         IF  ENDE
             EXIT SECTION
         END-IF
     END-IF

**  ---> BMP 22 - Eingabeart
     MOVE 22 TO W207-XBMP
     MOVE 12 TO W207-XCOBLEN
*G.02.01 - Start von BMP 22 in FCPARAM definiert
*     MOVE "B10101" TO W207-XCOBVAL
*  ---> BMP 22 - Servicecode
     MOVE 22 TO S-BMP
     MOVE 99 TO S-LFDNR
     PERFORM U300-SEARCH-TAB
     IF  PRM-NOT-FOUND
         PERFORM E900-PUT-ERRLOG
         SET ENDE TO TRUE
         EXIT SECTION
     END-IF
     PERFORM U400-INTERPRET-ABWEICHUNG
     MOVE W-BUFFER     TO W207-XCOBVAL
*G.02.01 - Ende 
     EVALUATE TRUE
         WHEN W-ERF-MANUELL      MOVE "6" TO W207-XCOBVAL (7:1)
         WHEN W-ERF-MAGNET       MOVE "2" TO W207-XCOBVAL (7:1)
         WHEN W-ERF-CHIP         MOVE "5" TO W207-XCOBVAL (7:1)
         WHEN W-ERF-KONTAKTLOS   MOVE "A" TO W207-XCOBVAL (7:1)
         WHEN OTHER              MOVE "3" TO W207-XCOBVAL (7:1)
     END-EVALUATE
     EVALUATE TRUE
         WHEN PAC-YES            MOVE "1" TO W207-XCOBVAL (8:1)
         WHEN PAC-NO             MOVE "1" TO W207-XCOBVAL (8:1)
         WHEN OTHER              MOVE "6" TO W207-XCOBVAL (8:1)
     END-EVALUATE
     MOVE "4144" TO W207-XCOBVAL (9:4)
     PERFORM L100-ADD-BMP
     IF  ENDE
         EXIT SECTION
     END-IF

**  ---> BMP 24 - Funktionscode
     MOVE 24     TO W207-XBMP
     MOVE 03     TO W207-XCOBLEN
     MOVE "200"  TO W207-XCOBVAL
     PERFORM L100-ADD-BMP
     IF  ENDE
         EXIT SECTION
     END-IF

**  ---> BMP 25 - Message Reason
     MOVE 25 TO S-BMP
     MOVE 1  TO S-LFDNR
     PERFORM U300-SEARCH-TAB
     IF  PRM-NOT-FOUND
         PERFORM E900-PUT-ERRLOG
         SET ENDE TO TRUE
         EXIT SECTION
     END-IF
     PERFORM U400-INTERPRET-ABWEICHUNG
     MOVE 25           TO W207-XBMP
     MOVE W-BUFFER-LEN TO W207-XCOBLEN
     MOVE W-BUFFER     TO W207-XCOBVAL
     PERFORM L100-ADD-BMP
     IF  ENDE
         EXIT SECTION
     END-IF

**  ---> BMP 26 - Branchencode
     MOVE 26     TO W207-XBMP
     MOVE 04     TO W207-XCOBLEN
     MOVE "5541" TO W207-XCOBVAL
     PERFORM L100-ADD-BMP
     IF  ENDE
         EXIT SECTION
     END-IF
     
*G.02.01 - AIID aus neuer Tabelle FCAIID (geladen bei Start)
**  ---> BMP 32 - Netzbetreiber Kennung AIID
*     MOVE 32 TO S-BMP
*     MOVE 1  TO S-LFDNR
*     PERFORM U300-SEARCH-TAB
*     IF  PRM-NOT-FOUND
*         PERFORM E900-PUT-ERRLOG
*         SET ENDE TO TRUE
*         EXIT SECTION
*     END-IF
*     PERFORM U400-INTERPRET-ABWEICHUNG
*     MOVE 32           TO W207-XBMP
*     MOVE W-BUFFER-LEN TO W207-XCOBLEN
*     MOVE W-BUFFER     TO W207-XCOBVAL
     
     MOVE 32     TO W207-XBMP
     MOVE W-AIID TO W207-XCOBVAL
     MOVE ZERO TO D-NUM4N
     INSPECT W-AIID TALLYING D-NUM4N
     FOR CHARACTERS BEFORE INITIAL " "
     MOVE D-NUM4N TO W207-XCOBLEN 
*G.02.01 - Ende
     PERFORM L100-ADD-BMP
     IF  ENDE
         EXIT SECTION
     END-IF

**  ---> BMP 35 - Spur 2
     IF  ERF-SPUR2
         MOVE 35            TO W207-XBMP
         MOVE W207-TLEN(35) TO W207-XCOBLEN
         INSPECT W207-CF(W207-TPTR(35):W207-TLEN(35)) CONVERTING "D"
                                                              TO "="
         MOVE W207-CF(W207-TPTR(35):W207-TLEN(35)) TO W207-XCOBVAL
         PERFORM L100-ADD-BMP
         IF  ENDE
             EXIT SECTION
         END-IF
     END-IF

**  ---> BMP 41 - TERMNR - wird übernommen
     MOVE 41   TO S-BMP
     MOVE 1    TO S-LFDNR
     PERFORM U300-SEARCH-TAB
     IF  PRM-FOUND
         IF  T-KZ-ABWEICHUNG (T-AKT-IND) = "0"
             MOVE ZERO TO W207-TBMP-O (41:1)
         END-IF
     END-IF

**  ---> BMP 42 - VUNR
     MOVE 42               TO W207-XBMP
     MOVE VUNR of TSKART40 TO W207-XCOBVAL
     MOVE 15               TO W207-XCOBLEN
     PERFORM L100-ADD-BMP
     IF  ENDE
         EXIT SECTION
     END-IF

**  ---> BMP 49 - WKZ - wird übernommen
     MOVE 49               TO W207-XBMP
     MOVE IMSG-CF(IMSG-TPTR(49) + 1:3) TO W207-XCOBVAL
     MOVE 3                TO W207-XCOBLEN
     PERFORM L100-ADD-BMP
     IF  ENDE
         EXIT SECTION
     END-IF

**  ---> BMP 52 - PAK (muss umgeschlüsselt werden) 
     IF  PAC-YES
         MOVE 52 TO W207-XBMP
         MOVE 08 TO W207-XCOBLEN
*         PERFORM D200-FIX-KEY
         IF  ENDE
             EXIT SECTION
         END-IF
         PERFORM F930-PAC-UMSCHLUESSELN
         IF  ENDE
             EXIT SECTION
         END-IF
**      ---> PAC ist original Terminal-Nachricht (IMSG-..) umgeschlüsselt
**      ---> also in W207-Buffer übertragen
         MOVE IMSG-CF(IMSG-TPTR(52):IMSG-TLEN(52)) TO W207-XCOBVAL
         PERFORM L100-ADD-BMP
         IF  ENDE
             EXIT SECTION
         END-IF
     END-IF

**  ---> BMP 53 - wenn AS-MAC/PAC 
     IF  PAC-YES or MAC-YES
         MOVE W-ISOGEN-VERS TO W207-XCOBVAL
         MOVE 2 TO W207-XCOBLEN
         MOVE LOW-VALUE TO W207-XCOBVAL (W207-XCOBLEN + 1:)
         ADD 16 TO W207-XCOBLEN
         IF  IMSG-TBMP(57) = 1
             MOVE IMSG-CF(IMSG-TPTR(57) + 2:16) TO W207-XCOBVAL (W207-XCOBLEN + 1:)
         END-IF
         ADD 16 TO W207-XCOBLEN
**      --->  nachsehen, ob noch Suffix gesendet werden soll
         MOVE 53 TO S-BMP
         MOVE 1  TO S-LFDNR
         PERFORM U300-SEARCH-TAB
         IF  PRM-FOUND
             PERFORM U400-INTERPRET-ABWEICHUNG
             MOVE W-BUFFER TO W207-XCOBVAL (W207-XCOBLEN + 1:)
             ADD W-BUFFER-LEN TO W207-XCOBLEN
         END-IF
**      --->  und nun in Nachricht
         MOVE 53 TO W207-XBMP
         PERFORM L100-ADD-BMP
         IF  ENDE
             EXIT SECTION
         END-IF
     END-IF

**  ---> BMP 59 - Transportdaten
     MOVE 59 TO S-BMP
     MOVE 99 TO S-LFDNR
     PERFORM U300-SEARCH-TAB
     IF  PRM-NOT-FOUND
         PERFORM E900-PUT-ERRLOG
         SET ENDE TO TRUE
         EXIT SECTION
     END-IF
     PERFORM U400-INTERPRET-ABWEICHUNG
     MOVE 59           TO W207-XBMP
     MOVE W-BUFFER-LEN TO W207-XCOBLEN
     MOVE W-BUFFER     TO W207-XCOBVAL
     PERFORM L100-ADD-BMP
     IF  ENDE
         EXIT SECTION
     END-IF

**G.02.00 - Anfang 

     IF W207-TBMP(63) = 1
*G.02.01 - schon frueher gemoved
*        MOVE W-ROUTKZ    TO S-ROUTKZ  OF S-SEARCH-KEY
*G.02.01 - Ende
        MOVE W-CARDID    TO S-CARDID  OF S-SEARCH-KEY
        MOVE 1220        TO S-ISONTYP OF S-SEARCH-KEY
        MOVE "AS"        TO S-KZ-MSG  OF S-SEARCH-KEY
        MOVE 63          TO S-BMP     OF S-SEARCH-KEY
        MOVE 1           TO S-LFDNR   OF S-SEARCH-KEY

        PERFORM U300-SEARCH-TAB

        IF  PRM-FOUND
          IF T-KZ-ABWEICHUNG (T-AKT-IND) = "X"
             MOVE ZERO TO W207-TBMP-O (63:1)
             CONTINUE
          ELSE
           PERFORM C210-AS-GENERELL-PRF-BMP63
          END-IF
        ELSE
           PERFORM C210-AS-GENERELL-PRF-BMP63
        END-IF
     END-IF

**G.02.00 - Ende

**---> nur fürs testen
     IF  TRACE-ON
         move "Test #1 - C200-AS-GENERELL - w207-tbmp:" to daten-buffer1
         move w207-tbmp-o (1:64) to daten-buffer2

         perform z002-progerr
     END-IF
     .
 C200-99.
     EXIT.

******************************************************************
* Prüfung BMP63 Artikeldaten und BMP 48
******************************************************************
 C210-AS-GENERELL-PRF-BMP63     SECTION.
 C210-00.

**BMP 63 - Artikel über Artikel-Mapper prüfen

     PERFORM E305-01-ARTIKELDATEN

     IF NOT AMP-OK
**Fehler vom Artikel-Mapper
        MOVE AMP-RC TO D-NUM4
        STRING  "RC aus Artikelmapping: "
                D-NUM4
        DELIMITED BY SIZE
          INTO  DATEN-BUFFER1
        END-STRING
        PERFORM Z002-PROGERR

        IF  AMP-RC = 100
**Ablehnung senden: Ungültige Transaktion
            MOVE 45 TO W-AC
        ELSE
**Ablehnung senden: Verarbeitung z.Zt. nicht möglich
            MOVE 96 TO W-AC
        END-IF

        EXIT SECTION
     END-IF

**Ab hier Artikel-Mapper OK
**der Artikelmapper bringt ggf. zunächst Fahrer-/Fahrzeugdaten
**(AMP-BMP48-FLAG = 1), die müssen für BMP48 aufbereitet werden
**(Kfz-Daten für BMP48 werden für Avia nicht gebraucht)

     MOVE ZERO TO W-48-LEN
     SET KFZ-NO TO TRUE
     IF  AMP-BMP48-FLAG = 1
         SET KFZ-YES TO TRUE
         MOVE AMP-HOST-VAL(1:3)            TO W-48-LEN
         MOVE AMP-HOST-VAL(1:W-48-LEN + 3) TO W-BMP48-VAL
         ADD  3                            TO W-48-LEN
     END-IF

**Artikel einstellen

     MOVE 63           TO W207-XBMP
     COMPUTE W-BUFFER-LEN
           = AMP-HOST-LEN
           - W-48-LEN
     END-COMPUTE

     MOVE W-BUFFER-LEN TO W207-XCOBLEN
     MOVE AMP-HOST-VAL(W-48-LEN + 1:W-BUFFER-LEN) TO W207-XCOBVAL
     PERFORM L100-ADD-BMP
     IF ENDE
        EXIT SECTION
     END-IF

     .
 C210-99.
     EXIT.

**G.02.00 - Ende

******************************************************************
* AS-Anfrageteile, die für alle gleich sind
******************************************************************
 C300-AS-SPEZIELL SECTION.
 C300-00.
**  ---> verzweigen je nach ROUTKZ
*G.02.01 - VERF-AS verwenden hier
*     EVALUATE W-ROUTKZ
     EVALUATE VERF-AS
*G.02.01 - Ende
         WHEN 05     PERFORM D305-AVIA
         WHEN 07     PERFORM D307-SHELL
         WHEN 10     PERFORM D310-TOTAL
         WHEN 12     PERFORM D312-DKV
         WHEN 14     PERFORM D314-BP
         WHEN 15     PERFORM D315-ENI
         WHEN 16     PERFORM D316-ORLEN
         WHEN 17     PERFORM D317-UTA
         WHEN 18     PERFORM D318-TND
         WHEN OTHER
                 SET ENDE TO TRUE
*G.02.01 - jetzt VERF-AS verwenden
*                 MOVE W-ROUTKZ TO D-NUM4
                 MOVE VERF-AS TO D-NUM4
*G.02.01 - Ende
                 STRING  "Keine speziellen Verarbeitungsregeln "
                         "für Rout-KZ = "
                         D-NUM4
                             delimited by size
                   INTO  DATEN-BUFFER1
                 END-STRING
                 PERFORM Z002-PROGERR
                 EXIT SECTION

     END-EVALUATE
     .
 C300-99.
     EXIT.

******************************************************************
* AS-Nachricht erstellen
******************************************************************
 C400-BUILD-AS-NACHRICHT SECTION.
 C400-00.
     SET W207-IFSF TO TRUE
     PERFORM L110-COB2ISO
     IF  ENDE
         EXIT SECTION
     END-IF

**  ---> und echten MAC bilden und einstellen
     IF  MAC-YES
         MOVE ALL LOW-VALUES    TO W66-AKEY-NAME
         MOVE W-MACKEYA         TO W66-AKEY-NAME (1:4)
         SET  W66-MAC-BILDEN-AS TO TRUE
         SET  W66-IFSF          TO TRUE
         MOVE W-TERMNR          TO IMSG-TERMNR
         PERFORM F920-MAC-BILDEN
     END-IF

**  ---> interne Schnittstelle für AS-Nachricht merken
     MOVE IMSG-DATLEN   TO AS-DATLEN
     MOVE IMSG-NDATEN   TO AS-NDATEN

     MOVE IMSG-SENDLEN  TO AS-SENDLEN
     MOVE IMSG-COBDATEN TO AS-COBDATEN

**  ---> nur fürs testen
     IF  TRACE-ON
         move "Test #2 - C400-BUILD-AS-NACHRICHT - w207-tbmp:" to daten-buffer1
         move w207-tbmp-o (1:64) to daten-buffer2

         perform z002-progerr
     END-IF

     .
 C400-99.
     EXIT.

******************************************************************
* Loggen der Transaktionsdaten in den MEMLOG
******************************************************************
 C500-LOGGING SECTION.
 C500-00.
**  ---> Führungstabelle =TXILOG70
     PERFORM G100-PUT-TXILOG70

**  ---> Anfragenachricht für Tabelle =TXNLOG70 TS-Nachrichten
     PERFORM G110-PUT-TXNLOG70-TS

**  ---> und schliesslich Eintrag in CRDUSEDN erzeugen
     PERFORM G150-PUT-CRDUSEDN

     IF W-AC = ZEROS
**  ---> nun UMSWEAT bedienen
        PERFORM G130-PUT-UMSWEAT
        IF  ENDE
            EXIT SECTION
        END-IF

**  ---> hier die AS-Nachricht dem Nachbucher zur Verfügung stellen
        PERFORM G140-PUT-ASYNC70
     END-IF

     .
 C500-99.
     EXIT.

******************************************************************
* Key bestimmen
******************************************************************
 D200-FIX-KEY SECTION.
 D200-00.
**  ---> wenn Schlüsseltabelle nur mit einem belegt ist, wieder zurück
     IF  TK-MAX = 1
         MOVE 1 TO C4-I1
         EXIT SECTION
     END-IF

**  ---> nun muss doch gesucht werden
     PERFORM VARYING C4-I1 FROM 1 BY 1
             UNTIL   C4-I1 > TK-MAX

*G.02.01 - auch ROUTKZ Vergleich, da nun alle Eintraege geladen
         IF  TK-CARDID (C4-I1) NOT = W-CARDID
         OR  TK-ROUTKZ (C4-I1) NOT = W-ROUTKZ
*G.02.01 - Ende
**          ---> nächsten suchen
             EXIT PERFORM CYCLE
         END-IF

**      ---> Schlüssel zur Verfügung stellen
         MOVE TK-HEXKEY (C4-I1) TO W-MACKEYA
         MOVE TK-HEXKEY (C4-I1) TO W-PACKEYA (1:4)
**      ---> Schlüsselgeneration und -version bereitstellen
         MOVE TK-HEXISO (C4-I1) TO W-ISOGEN-VERS
*G.02.01 - AIID speichern
         MOVE TK-AIID (C4-I1)   TO W-AIID
*G.02.01 - Ende
         EXIT SECTION

     END-PERFORM

**  ---> hier sind keine Schlüssel gefunden worden
     SET ENDE TO TRUE
     MOVE W-ROUTKZ TO D-NUM4
     MOVE W-CARDID TO D-NUM4OV
     MOVE "Keine AS-Schlüssel in =KEYNAMEN gefunden für:" TO DATEN-BUFFER1
     STRING  "ROUTKZ / CARDID = "
             D-NUM4
             " / "
             D-NUM4OV
                 delimited by size
       INTO  DATEN-BUFFER2
     END-STRING
     PERFORM Z002-PROGERR
     .
 D200-99.
     EXIT.

******************************************************************
* spezielle Behandlung für das Avia-AS
*    ggf. mit Hilfe der Parameter aus Tabelle =FCPARAM
******************************************************************
 D305-AVIA SECTION.
 D305-00.
* Sollte der Wiedereinbau erforderlich werden, siehe PFCOFF7E
     CONTINUE
     .
 D305-99.
     EXIT.

******************************************************************
* spezielle Behandlung für das Shell-AS
******************************************************************
 D307-SHELL SECTION.
 D307-00.
* Sollte der Wiedereinbau erforderlich werden, siehe PFCOFF7E
     CONTINUE
     .
 D307-99.
     EXIT.

******************************************************************
* spezielle Behandlung für das Total-AS
******************************************************************
 D310-TOTAL SECTION.
 D310-00.
* Sollte der Wiedereinbau erforderlich werden, siehe PFCOFF7E
     CONTINUE
     .
 D310-99.
     EXIT.

******************************************************************
* spezielle Behandlung für das DKV-AS
******************************************************************
 D312-DKV SECTION.
 D312-00.
**  ---> Anwendung für MAC-Bildung setzen
     SET W66-DKV TO TRUE

**  ---> und BMP43 aufbereiten (Name, Ort)
     MOVE 43 TO W207-XBMP
     MOVE 1  TO C4-PTR
     MOVE SPACES TO W207-XCOBVAL
     STRING  NAME of STATION
                 delimited by space
             ", "
                 delimited by size
             ORT of STATION
                 delimited by space
       INTO  W207-XCOBVAL
       WITH  POINTER C4-PTR
     END-STRING

     COMPUTE W207-XCOBLEN = C4-PTR - 1

     IF  W207-XCOBLEN = ZERO
         MOVE 4      TO W207-XCOBLEN
         MOVE "WEAT" TO W207-XCOBVAL
     END-IF
     PERFORM L100-ADD-BMP
     IF  ENDE
         EXIT SECTION
     END-IF

**  ---> und BMP48 aufbereiten
     PERFORM E310-BMP48-DEFAULT
     .
 D312-99.
     EXIT.

******************************************************************
* spezielle Behandlung für das BP-AS
******************************************************************
 D314-BP SECTION.
 D314-00.
* Sollte der Wiedereinbau erforderlich werden, siehe PFCOFF7E
     CONTINUE
     .
 D314-99.
     EXIT.

******************************************************************
* spezielle Behandlung für das ENI-AS
******************************************************************
 D315-ENI SECTION.
 D315-00.
* Sollte der Wiedereinbau erforderlich werden, siehe PFCOFF7E
     CONTINUE
     .
 D315-99.
     EXIT.

******************************************************************
* spezielle Behandlung für das ORLEN-AS
******************************************************************
 D316-ORLEN SECTION.
 D316-00.
* Sollte der Wiedereinbau erforderlich werden, siehe PFCOFF7E
     CONTINUE
     .
 D316-99.
     EXIT.

******************************************************************
* spezielle Behandlung für das UTA-AS
******************************************************************
 D317-UTA SECTION.
 D317-00.
* Sollte der Wiedereinbau erforderlich werden, siehe PFCOFF7E
     CONTINUE
     .
 D317-99.
     EXIT.

******************************************************************
* spezielle Behandlung für das TND-AS
******************************************************************
 D318-TND SECTION.
 D318-00.
* Sollte der Wiedereinbau erforderlich werden, siehe PFCOFF7E
     CONTINUE
     .
 D318-99.
     EXIT.

******************************************************************
* bestimmen CARDID
******************************************************************
 D900-GET-CARDID SECTION.
 D900-00.
**  ---> CARDID über Modul WSYS930 holen, dafür
**  ---> erste 9 Stellen der Karten-Nr. bereitstellen
     IF  IMSG-TBMP(2) = 1
         MOVE IMSG-CF(IMSG-TPTR(2):9)  TO ROUT-MERKMAL
     ELSE
         MOVE IMSG-CF(IMSG-TPTR(35):9) TO ROUT-MERKMAL
     END-IF
     SET ROUT-CMD-ID TO TRUE
     PERFORM M150-CALL-WSYS930
     IF  ENDE
         EXIT SECTION
     END-IF

     MOVE ROUT-CARDID TO W-CARDID
                         S-CARDID,
                         CARD-FLAG,
                         CARDID  OF  WGRSTPRF,
                         CARDID  OF  STIPLIMS,
                         CARDID  OF  ARTMAP

**  ---> VUNR / Routkz über TSKART40 holen
     MOVE IMSG-MDNR TO MDNR    of TSKART40
     MOVE IMSG-TSNR TO TSNR    of TSKART40
     IF  IMSG-TBMP(25) = 1
         MOVE 2     TO CARDSYS OF TSKART40
     ELSE
         MOVE 1     TO CARDSYS OF TSKART40
     END-IF
     MOVE W-CARDID  TO CARDID  of TSKART40

     PERFORM S150-SELECT-TSKART40
     IF  W-AC not = ZERO
         EXIT SECTION
     END-IF

**  ---> Routinginformationen über Modul WSYS930 holen
     MOVE W-ROUTKZ TO ROUT-KZ
     SET ROUT-CMD-AS TO TRUE
     PERFORM M150-CALL-WSYS930
     IF  ROUT-NOT-OK
         EXIT SECTION
     END-IF

**  ---> FREGAT-Daten setzen
     MOVE ROUT-FREGATTE TO AS-NEXTSERV
     MOVE ROUT-DTX      TO AS-DTXNR
     IF  ROUT-KZSYNC = "S"
         SET AS-SEND-SYNC  TO TRUE
     ELSE
         SET AS-SEND-ASYNC TO TRUE
     END-IF
     MOVE ROUT-KZSYNC TO W-LTGIND
     .
 D900-99.
     EXIT.

******************************************************************
* holen AS-Tracenummer
******************************************************************
 D910-GET-ASTRACENR SECTION.
 D910-00.
     MOVE IMSG-MDNR          TO MDNR   of MDNR2AS
     MOVE CARDID of TSKART40 TO CARDID of MDNR2AS
     MOVE W-ROUTKZ           TO ROUTKZ of MDNR2AS
     PERFORM S110-SELECT-MDNR2AS
     IF  ENDE
         EXIT SECTION
     END-IF
     MOVE AIID of MDNR2AS TO W-ACQUIRER-ID
     IF  W-AC NOT = ZERO
**      ---> Ablehnung senden: AS nicht verfügbar
         PERFORM E100-FEP-ANTWORT
         EXIT SECTION
     END-IF

**  ---> prüfen auf Überschlag
     IF  TRACENR OF MDNR2AS = 999999
         MOVE 1 TO TRACENR OF MDNR2AS
     ELSE
         ADD  1 TO TRACENR OF MDNR2AS
     END-IF

**  ---> sichern AS-Trace-Nr.
     MOVE TRACENR of MDNR2AS TO W-AS-TRACENR

**  ---> TraceNr. und Datum updaten
     PERFORM S120-UPDATE-MDNR2AS
     IF  W-AC NOT = ZERO
**      ---> Ablehnung senden: AS nicht verfügbar
         PERFORM E100-FEP-ANTWORT
         EXIT SECTION
     END-IF
     .
 D910-99.
     EXIT.

******************************************************************
* generieren FEP-Antwort
******************************************************************
 E100-FEP-ANTWORT SECTION.
 E100-00.

    SET FEP-ANTWORT TO TRUE
    MOVE TS-INTERN-MESSAGE TO INTERN-MESSAGE

**  ---> im Folgenden noch die Felder BMP 33, 39 hinzufuegen
    MOVE IMSG-COBDATEN TO W207-COBDATEN
    SET W207-EC        TO TRUE

*Wurde ein MAC mit der Anfrage geschickt, dann wird auch ein MAC mit
*der Antwort zurück geschickt

    SET MAC-NO TO TRUE
    IF  IMSG-TBMP(64) = 1
        SET MAC-YES TO TRUE
    END-IF

**  ---> zunaechst Felder ausschalten, die nicht gesendet werden sollen
     MOVE ZERO TO W207-TBMP (02)
     MOVE ZERO TO W207-TBMP (14)
     MOVE ZERO TO W207-TBMP (22)
     MOVE ZERO TO W207-TBMP (25)
     MOVE ZERO TO W207-TBMP (26)
     MOVE ZERO TO W207-TBMP (35)
     MOVE ZERO TO W207-TBMP (52)
     MOVE ZERO TO W207-TBMP (56)
*kl20160527 - G.00.01 - Bei positivem Ergebnis Genehmigungsnummer
*                       und Text bauen
*    MOVE ZERO TO W207-TBMP (59)
*    MOVE ZERO TO W207-TBMP (60)
     IF W-AC = ZERO
        PERFORM E350-GENNR
        MOVE 1 TO W207-TBMP (59)
        MOVE 59        TO W207-XBMP
        MOVE D-GENNRX  TO W207-XCOBVAL
                          GENNR OF TXILOG70
        MOVE 6         TO W207-XCOBLEN
        PERFORM L100-ADD-BMP
        IF  ENDE
            EXIT SECTION
        END-IF
*       Genehmigungs-Nr und Text einstellen
        MOVE "1" TO W207-TBMP (60)
        MOVE "Genehmigung: " TO W207-XCOBVAL
        MOVE  D-GENNRX       TO W207-XCOBVAL (13:6)
        MOVE 60              TO W207-XBMP
        MOVE 21              TO W207-XCOBLEN
        PERFORM L100-ADD-BMP
     ELSE
        MOVE ZERO TO W207-TBMP (59)
        MOVE ZERO TO W207-TBMP (60)
     END-IF
*kl20160527 - G.00.01 - Exit
     MOVE ZERO TO W207-TBMP (63)

**  ---> Nachrichtentype setzen
     IF  NTYPE-BUCHUNG
         MOVE 0210 TO W207-NTYPE
     END-IF

**  ---> BMP 33: WEAT als AS-ID
     MOVE 33       TO W207-XBMP
     MOVE "740000" TO W207-XCOBVAL
     MOVE 6        TO W207-XCOBLEN
     PERFORM L100-ADD-BMP
     IF  ENDE
         EXIT SECTION
     END-IF

**BMP39  ACODE
     MOVE 39    TO W207-XBMP
     MOVE W-AC  TO W207-XCOBVAL
     MOVE 2     TO W207-XCOBLEN
     PERFORM L100-ADD-BMP
     IF  ENDE
         EXIT SECTION
     END-IF

**  ---> BMP 42 - VUNR
     MOVE 42               TO W207-XBMP
     MOVE VUNR of TSKART40 TO W207-XCOBVAL
     MOVE 15               TO W207-XCOBLEN
     PERFORM L100-ADD-BMP
     IF  ENDE
         EXIT SECTION
     END-IF

**BMP63 Artikeldaten
     IF W-AC = 87
     OR C-OPT-OUTDOOR
                MOVE 1          TO W207-TBMP (63)
                MOVE 63         TO W207-XBMP
                MOVE UP-STRUCT2 TO W207-XCOBVAL
                COMPUTE W207-XCOBLEN
                          = UP-COUNTER2 * 6 + 2
                END-COMPUTE
                PERFORM L100-ADD-BMP
                IF  ENDE
                        EXIT SECTION
                END-IF
         END-IF

** BMP53  Sicherheitsverfahren: wird so zurück gegeben
** wie in der Anfrage geschickt

** BMP64 dummy-MAC vorbereiten: BMP setzen und Speicher einrichten

     IF  MAC-YES
         MOVE 64         TO W207-XBMP
         MOVE LOW-VALUES TO W207-XCOBVAL
         MOVE 8          TO W207-XCOBLEN
         PERFORM L100-ADD-BMP
         IF  ENDE
             EXIT SECTION
          END-IF
     END-IF

**  ---> Nachricht zusammenstellen
     PERFORM L110-COB2ISO
     IF  ENDE
         EXIT SECTION
     END-IF

** MAC über die Box bilden und an Speicherort der Nachricht setzen
     IF  MAC-YES
         MOVE ALL LOW-VALUES    TO W66-TKEY-NAME
         MOVE W-MACKEYT         TO W66-TKEY-NAME (1:4)
         SET  W66-MAC-BILDEN-TS TO TRUE
         SET  W66-EC            TO TRUE
         MOVE W-TERMNR          TO IMSG-TERMNR
         PERFORM F920-MAC-BILDEN
     END-IF

**  --> FREGAT-Parameter setzen
     SET  IMSG-WRITE-SL    TO TRUE
     MOVE W-FRE-MONNAME    TO IMSG-NEXTSERV
     MOVE MODUL OF MYPROG  TO IMSG-MONNAME
     .
 E100-99.
     EXIT.

******************************************************************
* Aufruf Artikelmapper
******************************************************************
 E305-01-ARTIKELDATEN SECTION.
 E305-01-00.
**  ---> Artikel mappen über Mappingserver WXAMP07S
     SET AMP-P2S TO TRUE
     MOVE IMSG-CF(IMSG-TPTR(63):IMSG-TLEN(63))
                             TO AMP-POS-VAL
     MOVE IMSG-TLEN(63)      TO AMP-POS-LEN
     MOVE IMSG-MDNR          TO AMP-MDNR
     MOVE W-TSNR             TO AMP-TSNR
     MOVE CARDID of TSKART40 TO AMP-CARDID

**  ---> sollte Mapping für AGIP verändert werden, wirkt sich das auch hier aus
*     MOVE MODUL OF MYPROG(2:2)        TO AMP-FORMAT
     MOVE AS-VERF    TO AMP-FORMAT
     MOVE ALL SPACES TO AMP-HOST-VAL
     MOVE ZERO       TO AMP-RC
                        AMP-HOST-LEN

**  ---> Pathsend zu WXAMP07S
     PERFORM P100-PSD-ARTIKELMAPPER
     .
 E305-01-99.
     EXIT.

******************************************************************
* Aufbereitung BMP48 default
******************************************************************
 E310-BMP48-DEFAULT SECTION.
 E310-00.
**  ---> BMP 48 - geht erst nach Artikel-Mapper (fummelt aus BMP63 ggf.
**  --->          noch Fahrerdaten, die einzustellen sind (48.8))
**  ---> zunächst die BitMap erstellen
     MOVE ALL ZEROES TO W-BYTEMAP-48
     MOVE SPACES     TO W207-XCOBVAL
     MOVE 8          TO W207-XCOBLEN

**  +++> jetzt das Subfeld 3 oder auch nicht
     MOVE 48 TO S-BMP
     MOVE 3  TO S-LFDNR
     PERFORM U300-SEARCH-TAB
     IF  PRM-FOUND
         PERFORM U400-INTERPRET-ABWEICHUNG
         MOVE W-BUFFER (1:W-BUFFER-LEN) TO W207-XCOBVAL (W207-XCOBLEN + 1:)
         ADD W-BUFFER-LEN TO W207-XCOBLEN
         MOVE "1"         TO W-BYTEMAP-48(3:1)
     END-IF

**  +++> und jetzt das Subfeld 4 (Fixwert)
     MOVE "1"          TO W-BYTEMAP-48(4:1)
     MOVE "0000000001" TO W207-XCOBVAL (W207-XCOBLEN + 1:)
     ADD 10 TO W207-XCOBLEN

**  +++> und jetzt das Subfeld 8 (Kfz-Daten)
     IF  KFZ-YES AND W-48-LEN > ZERO
         MOVE W-BMP48-VAL (1:W-48-LEN) TO W207-XCOBVAL (W207-XCOBLEN + 1:)
         ADD W-48-LEN TO W207-XCOBLEN
         MOVE "1"     TO W-BYTEMAP-48(8:1)
     END-IF

**  +++> und jetzt das Subfeld 14, ggf. aus =FCPARAM
     MOVE "1" TO W-BYTEMAP-48(14:1)
     MOVE 48 TO S-BMP
     MOVE 14 TO S-LFDNR
     PERFORM U300-SEARCH-TAB
     IF  PRM-FOUND
         PERFORM U400-INTERPRET-ABWEICHUNG
         MOVE W-BUFFER (1:W-BUFFER-LEN) TO W207-XCOBVAL (W207-XCOBLEN + 1:)
         ADD W-BUFFER-LEN TO W207-XCOBLEN
     ELSE
         MOVE "33" TO W-BUFFER (W-BUFFER-LEN + 1:)
         ADD 2 TO W207-XCOBLEN
     END-IF

**  +++> und jetzt das Subfeld 38, ggf. aus =FCPARAM
     MOVE 48 TO S-BMP
     MOVE 38 TO S-LFDNR
     PERFORM U300-SEARCH-TAB
     IF  PRM-FOUND
         PERFORM U400-INTERPRET-ABWEICHUNG
         MOVE W-BUFFER (1:W-BUFFER-LEN) TO W207-XCOBVAL (W207-XCOBLEN + 1:)
         ADD W-BUFFER-LEN TO W207-XCOBLEN
         MOVE "1"         TO W-BYTEMAP-48(38:1)
     END-IF

**  +++>  nun endlich die Bitmap einfügen
     MOVE  LOW-VALUE TO W-BITMAP
     ENTER TAL "WT^BY2BI" USING W-BITMAP W-BYTEMAP-48
     MOVE W-BITMAP TO W207-XCOBVAL (1:8)

**  +++> jetzt in die Nachricht einbauen
     MOVE 48 TO W207-XBMP
     PERFORM L100-ADD-BMP
     IF  ENDE
         EXIT SECTION
     END-IF
     .
 E310-99.
     EXIT.
*kl20160527 - G.00.01 - Genehmigungsnummer fuer FEP-Antwort
*                       generieren (Standardverfahren)
******************************************************************
* Berechnen Genehmigungsnummer
******************************************************************
 E350-GENNR SECTION.
 E350-00.
**  ---> Die Genehigungsnummer wird wie folgt zusammengesetzt:
**  --->     1. Letzte Ziffer der Kartennummer
**  --->     2. Letzte zwei Ziffern der Tracenummer
**  --->     3. Aktueller Tag (zweistellig)
**  --->     4. Pruefziffer ueber die ersten 5 Stellen (Luhncheck Verf.)
**  --->
     MOVE SPACES                          TO D-GENNRX
     MOVE W-KANR (W-KANR-LEN:1)           TO D-GENNRX (1:1)
     MOVE W207-CF (W207-TPTR (11) + 4:2)  TO D-GENNRX (2:2)
     MOVE TAL-TT OF TAL-TIME-D            TO D-GENNRX (4:2)

     SET W04T-COMPUTE TO TRUE
     MOVE D-GENNRX TO W04T-KANR
     CALL "WSYS04T" USING W04T-WSYS04TC
     IF  W04T-OK
         MOVE W04T-KANR (1:6) TO D-GENNRX
     ELSE
         MOVE 1201 TO ERROR-NR OF GEN-ERROR
         MOVE W04T-STATUS TO D-NUM4
         STRING "WSYS04T@"
                D-NUM4
                "@"
                "Prg. Abbruch@"
                     DELIMITED BY SIZE
           INTO DATEN-BUFFER1
         END-STRING
         PERFORM Z002-PROGERR
         SET ENDE TO TRUE
         MOVE SPACE TO D-GENNRX
         MOVE 99 TO W-AC
     END-IF

     .
 E350-99.
     EXIT.
*kl20160527 - G.00.01 - Ende
******************************************************************
* erzeugen Fehlermeldung
******************************************************************
 E900-PUT-ERRLOG SECTION.
 E900-00.
     MOVE 1105 TO ERROR-NR of GEN-ERROR
     MOVE "=FCPARAM für: @" TO DATEN-BUFFER1
*G.02.01 - VERF-AS jetzt
*     MOVE W-ROUTKZ  TO D-NUM4
     MOVE VERF-AS  TO D-NUM4
*G.02.01 - Ende
     MOVE W-CARDID  TO D-NUM4OV
     MOVE S-ISONTYP TO D-NUM4M
     MOVE S-BMP     TO D-NUM4N
     STRING  "ROUTKZ/CARDID/ISONTYP/KZ-MSG/BMP = "
             D-NUM4 "/" D-NUM4OV "/" D-NUM4M "/" S-KZ-MSG "/" D-NUM4N
                 delimited by size
       INTO  DATEN-BUFFER2
     END-STRING
     PERFORM Z002-PROGERR
     .
 E900-99.
     EXIT.

******************************************************************
* MAC prüfen (ggf. ist W-AC gesetzt)
******************************************************************
 F910-MAC-PRUEFEN SECTION.
 F910-00.
**  ---> MAC ist ggf. schon in der Drehscheibe (WXCRD7) geprüft
     EVALUATE IMSG-MDNR-HOST

         WHEN 10     EXIT SECTION
         WHEN 97     MOVE 97 TO W-AC
                     MOVE "MAC-Prüfung aus WXCRD7S meldet MAC falsch"
                         TO DATEN-BUFFER1
                     MOVE "AC 97 gesetzt" TO DATEN-BUFFER2
                     PERFORM Z002-PROGERR
                     EXIT SECTION

     END-EVALUATE

**  ---> keine Vorprüfung des MAC also hier prüfen
     SET W66-MAC-PRUEFEN-TS TO TRUE
     MOVE 88                TO W66-RCODE of W66-WSY7066C
     SET  W66-DEFAULT       TO TRUE
     MOVE ALL LOW-VALUES    TO W66-TKEY-NAME
     MOVE W-MACKEYT         TO W66-TKEY-NAME (1:4)

     PERFORM M140-CALL-WSY7066
     .
 F910-99.
     EXIT.

******************************************************************
* Erstellen Antwort-MAC
******************************************************************
 F920-MAC-BILDEN SECTION.
 F920-00.
**  ---> MAC-Bildung und Schlüsselwechsel über WSY7066
**  ---> Datenbereich Message-datei external übergibt Daten für WSY7066
     MOVE 88 TO W66-RCODE of W66-WSY7066C
     PERFORM M140-CALL-WSY7066
     IF  W66-ERR
         MOVE 30   TO W-AC
         MOVE 1201 TO ERROR-NR of GEN-ERROR
         MOVE W66-RCODE TO D-NUM4
         STRING  "WSY7066@"          delimited by size
                 D-NUM4              delimited by size
                 "@@"                delimited by size
           INTO  DATEN-BUFFER1
         END-STRING
         MOVE "Bei F920-MAC-BILDEN" TO DATEN-BUFFER2
         MOVE "Transaktions-Ende"   TO DATEN-BUFFER3
         PERFORM Z002-PROGERR
         SET ENDE TO TRUE
         EXIT SECTION
     END-IF

**  --> Datenteil wird in WSY7066 mit MAC versehen
     .
 F920-99.
      EXIT.

******************************************************************
* PAC umschluesseln
******************************************************************
 F930-PAC-UMSCHLUESSELN SECTION.
 F930-00.
     SET W66-PAC-UMSCHL  TO TRUE

*G.00.04 - Anfang
**  ---> Anwendungs-KZ setzen
*G.02.01 - VERF-AS jetzt
*      EVALUATE W-ROUTKZ
      EVALUATE VERF-AS
*G.02.01 - Ende
          WHEN 18
               SET W66-TND     TO TRUE
          WHEN OTHER
               SET W66-DEFAULT TO TRUE
      END-EVALUATE
*G.00.04 - Anfang

     MOVE 88             TO W66-RCODE of W66-WSY7066C
     MOVE ALL LOW-VALUES TO W66-TKEY-NAME
     MOVE ALL LOW-VALUES TO W66-AKEY-NAME
     MOVE W-PACKEYT      TO W66-TKEY-NAME (1:4)
     MOVE W-PACKEYA      TO W66-AKEY-NAME (1:4)
     PERFORM M140-CALL-WSY7066
     IF  W66-ERR
         MOVE 1201 TO ERROR-NR of GEN-ERROR
         MOVE W66-RCODE TO D-NUM4
         STRING  "WSY7066@"          delimited by size
                 D-NUM4              delimited by size
                 "@@"                delimited by size
           INTO  DATEN-BUFFER1
         END-STRING
         MOVE "Bei F930-PAC-UMSCHLUESSELN" TO DATEN-BUFFER2
         MOVE "Transaktions-Ende"          TO DATEN-BUFFER3
         PERFORM Z002-PROGERR
         SET ENDE TO TRUE
         EXIT SECTION
     END-IF
     .
 F930-99.
   EXIT.

******************************************************************
* Einstellen Daten in TXILOG70 Buffer
******************************************************************
 G100-PUT-TXILOG70 SECTION.
 G100-00.
     MOVE W-TERMNR (7:2) TO PNR            of TXILOG70
     MOVE W-TERMNR       TO TERMNR         of TXILOG70
     MOVE W-TRACENR      TO TRACENR        of TXILOG70
     MOVE W-NTYPE        TO ISONTYP        of TXILOG70
     MOVE W-MDNR         TO MDNR           of TXILOG70
     MOVE W-TSNR         TO TSNR           of TXILOG70
     MOVE W-AS-TRACENR   TO TRACENR-AS     of TXILOG70
     MOVE W-KANR         TO KANR           of TXILOG70
     MOVE "V2"           TO KZ-E2EE        of TXILOG70
     MOVE SPACES         TO KEYNAME        of TXILOG70
     MOVE W18-BETRAG     TO BETRAG         of TXILOG70

*G.00.02 - Anfang
     MOVE W18-BETRAG     TO BETRAG-AUTOR   of TXILOG70
*G.00.02 - Ende

     MOVE "F"            TO BETRAG-ART     of TXILOG70
     MOVE W-CARDID       TO CARDID         of TXILOG70
     MOVE W-ROUTKZ       TO ROUTKZ         of TXILOG70
     MOVE W-LTGIND       TO LTGIND         of TXILOG70
     MOVE 740000         TO ASID           of TXILOG70
     MOVE 9999           TO AC-AS          of TXILOG70
     MOVE W-AC           TO AC-TERM        of TXILOG70
     MOVE WKZ-WKZ-A      TO WKZ            of TXILOG70
     MOVE 70             TO LOGPROT        of TXILOG70
     IF  FEP-ANTWORT
         MOVE "L"        TO KZ-BEARB       of TXILOG70
     ELSE
         MOVE "A"        TO KZ-BEARB       of TXILOG70
     END-IF
*G.01.08 - FK offl. ist jetzt "m"
*     MOVE "k"            TO KZ-VERF        of TXILOG70
     MOVE "m"            TO KZ-VERF        of TXILOG70
*G.01.08 - Ende
     IF  ABWKZ-ZAHLUNG
         MOVE "Z"        TO KZ-UMSATZ      of TXILOG70
     ELSE
         MOVE "G"        TO KZ-UMSATZ      of TXILOG70
     END-IF
     MOVE W-ABL          TO ABL-JJMM       of TXILOG70
     MOVE W-ACQUIRER-ID  TO ACQUIRER-ID    of TXILOG70
     MOVE W-ERFASSUNGS-ART TO ERFASSUNGS-ART of TXILOG70
     MOVE 221            TO KARTEN-ART     of TXILOG70
     IF  W-ERFASSUNGS-ART = 01
         MOVE "MO"       TO TRANS-ART      of TXILOG70
     ELSE
         MOVE "2O"       TO TRANS-ART      of TXILOG70
     END-IF
     IF  PAC-YES
         MOVE "MP"       TO TRANS-TYP      of TXILOG70
     ELSE
         MOVE "OP"       TO TRANS-TYP      of TXILOG70
     END-IF
     MOVE 5541           TO BRANCHEN-KZ    of TXILOG70
     MOVE VUNR of TSKART40 TO VUNR         of TXILOG70
     MOVE W-ZP-VERKAUF   TO ZP-VERKAUF     of TXILOG70
     MOVE W-BMP07        TO AF-BMP07       of TXILOG70

     MOVE TS-TLEN(63)    TO LEN of ARTIKEL of TXILOG70
     MOVE TS-CF(TS-TPTR(63):TS-TLEN(63))
                         TO VAL of ARTIKEL of TXILOG70

**  ---> holen momentanen Zeitpunkt
     PERFORM U200-TIMESTAMP
     MOVE TAGESDATUM TO ZP-TOUT of TXILOG70
     MOVE TAGESDATUM TO ZP-AOUT of TXILOG70
     MOVE H-ZP-IN        TO ZP-TIN  of TXILOG70

**  ---> und nun Schreiben
     PERFORM S180-INSERT-TXILOG70
     .
 G100-99.
     EXIT.

******************************************************************
* Einstellen Daten in TXNLOG70-TS Buffer
******************************************************************
 G110-PUT-TXNLOG70-TS SECTION.
 G110-00.
     MOVE W-TERMNR (7:2) TO PNR            of TXNLOG70-TS
     MOVE W-TERMNR       TO TERMNR         of TXNLOG70-TS
     MOVE W-TRACENR      TO TRACENR        of TXNLOG70-TS
     MOVE 200            TO ISONTYP        of TXNLOG70-TS
     MOVE "T"            TO KZ-MSG         of TXNLOG70-TS
     MOVE 1              TO ISO-VERF       of TXNLOG70-TS
     MOVE W-MDNR         TO MDNR           of TXNLOG70-TS
     MOVE W-TSNR         TO TSNR           of TXNLOG70-TS
     MOVE K-MODUL        TO LOG-SRV        of TXNLOG70-TS
     MOVE TS-HEADER      TO FREHEADER      of TXNLOG70-TS
     MOVE TS-NDATEN      TO VAL of ANFRAGE of TXNLOG70-TS
     MOVE TS-DATLEN      TO LEN of ANFRAGE of TXNLOG70-TS
     MOVE IMSG-NDATEN    TO VAL of ANTWORT of TXNLOG70-TS
     MOVE IMSG-DATLEN    TO LEN of ANTWORT of TXNLOG70-TS

**  ---> und nun Schreiben
     PERFORM S190-INSERT-TXNLOG70-TS
     .
 G110-99.
     EXIT.

******************************************************************
* Einstellen Daten in UMSWEAT Buffer
******************************************************************
 G130-PUT-UMSWEAT SECTION.
 G130-00.
     MOVE PNR       of TXILOG70  TO PNR       of UMSWEAT
     MOVE TERMNR    of TXILOG70  TO TERMNR    of UMSWEAT
     MOVE TRACENR   of TXILOG70  TO TRACENR   of UMSWEAT
     MOVE MDNR      of TXILOG70  TO MDNR      of UMSWEAT
     MOVE TSNR      of TXILOG70  TO TSNR      of UMSWEAT
     MOVE CARDID    of TXILOG70  TO CARDID    of UMSWEAT
*G.00.02 - Anfang
*    MOVE BETRAG    of TXILOG70  TO BETRAG    of UMSWEAT
**

     MOVE BETRAG-AUTOR of TXILOG70 TO BETRAG  of UMSWEAT
*G.00.02 - Ende

     MOVE WKZ       of TXILOG70  TO WKZ       of UMSWEAT
     MOVE KZ-VERF   of TXILOG70  TO KZ-VERF   of UMSWEAT
     IF  NTYPE-BUCHUNG
         MOVE "R"                TO KZ-BEARB  of UMSWEAT
         MOVE ZERO               TO TRACENR-S OF UMSWEAT
     ELSE
         MOVE "S"                TO KZ-BEARB  of UMSWEAT
         MOVE TRACENR-S of TXILOG70 TO TRACENR-S of UMSWEAT
     END-IF
     MOVE W-BELEGNR              TO BELEGNR   of UMSWEAT
*kl20180313 - G.01.01A - Wie in Spec: Nur die ersten beiden
*                        Stellen (hier W-ABWKZ-ZAHL-GUT)
*     MOVE W-ABWKZ                TO ABWKZ     of UMSWEAT
     MOVE W-ABWKZ-ZAHL-GUT       TO ABWKZ     of UMSWEAT
*kl20180313 - G.01.01A - Ende


**  ---> Aufruf Modul IUMSw07 (Zugriff zum UMSIFSF-Server)
     MOVE UMSWEAT    TO WUMS-UMSATZ
     MOVE K-MODUL    TO WUMS-ABSENDER
     SET WUMS-TAB-UW TO TRUE
     IF  NTYPE-BUCHUNG
         SET WUMS-CMD-I  TO TRUE
     ELSE
         SET WUMS-CMD-DT TO TRUE
     END-IF
     PERFORM M180-CALL-IUMSW07
     .
 G130-99.
     EXIT.

******************************************************************
* AS-Nachricht für den Nachbucher bereitstellen
******************************************************************
 G140-PUT-ASYNC70 SECTION.
 G140-00.
     MOVE PNR        OF TXILOG70 TO PNR            OF ASYNC70
     MOVE TERMNR     OF TXILOG70 TO TERMNR         OF ASYNC70
     MOVE TRACENR    OF TXILOG70 TO TRACENR        OF ASYNC70
     MOVE 200                    TO ISONTYP        OF ASYNC70
     MOVE MDNR       OF TXILOG70 TO MDNR           OF ASYNC70
     MOVE TSNR       OF TXILOG70 TO TSNR           OF ASYNC70
     MOVE "FK"                   TO VKZ            OF ASYNC70
     MOVE TRACENR-AS OF TXILOG70 TO TRACENR-AS     OF ASYNC70
     MOVE CARDID     OF TXILOG70 TO CARDID         OF ASYNC70
     MOVE ROUTKZ     OF TXILOG70 TO ROUTKZ         OF ASYNC70
     MOVE "A"                    TO KZ-BEARB       OF ASYNC70
     MOVE 1                      TO ANZ-REP        OF ASYNC70
     MOVE AS-HEADER              TO FREHEADER      OF ASYNC70
     MOVE AS-DATLEN              TO LEN OF ANFRAGE OF ASYNC70
     MOVE AS-NDATEN              TO VAL OF ANFRAGE OF ASYNC70
     MOVE ZEROS                  TO LEN OF ANTWORT OF ASYNC70
     MOVE SPACES                 TO VAL OF ANTWORT OF ASYNC70
     MOVE W-MACKEYA              TO KEY-NAME       OF ASYNC70

     PERFORM S210-INSERT-ASYNC70
     .
 G140-99.
     EXIT.

******************************************************************
* Einstellen Daten in CRDUSEDN-Buffer
******************************************************************
 G150-PUT-CRDUSEDN SECTION.
 G150-00.
**  ---> zunächst Länge der Kartennummer feststellen
     MOVE ZERO TO C4-ANZ
     MOVE 1    TO C4-PTR
     MOVE SPACES     TO W-TEILSTRING-TABELLE
     MOVE SPACES     TO W-DELIM-TABELLE
     MOVE LOW-VALUES TO W-COUNT-TABELLE

     UNSTRING KANR of TXILOG70
                                delimited by ALL SPACE
         INTO W-TEILSTRING (1)  delimiter in W-DELIM (1)
                                count     in W-COUNT (1)
              W-TEILSTRING (2)  delimiter in W-DELIM (2)
                                count     in W-COUNT (2)
         WITH     POINTER C4-PTR
         TALLYING IN      C4-ANZ
     END-UNSTRING

**  ---> Länge ist jetzt in W-COUNT(1)

     MOVe KANR of TXILOG70 (W-COUNT (1):1) TO SDB-PNR
     MOVE KANR      of TXILOG70 TO SDB-KANR
     MOVE "T"                   TO SDB-AKZ
     MOVE TERMNR    of TXILOG70 TO SDB-TERMNR
     MOVE TRACENR   of TXILOG70 TO SDB-TRACENR
     MOVE W-AC                  TO SDB-AC
     MOVE BETRAG    of TXILOG70 TO SDB-BETRAG
     MOVE W-MDNR                TO SDB-MDNR
     MOVE W-TSNR                TO SDB-TSNR

**  ---> und nun schreiben
     PERFORM M100-CALL-SDBCDU5
     .
 G150-99.
     EXIT.

******************************************************************
* Felder zusaetzlich in Nachricht einbauen
******************************************************************
 L100-ADD-BMP SECTION.
 L100-00.
     SET W207-ADD-BMP TO TRUE
     PERFORM M130-CALL-WISO207

     IF  W207-RCODE NOT = ZERO
         MOVE W207-RCODE TO D-NUM4
         MOVE 1201 TO ERROR-NR of GEN-ERROR
         STRING "WISO207 (ADD-BMP)/@"
                "RC: " D-NUM4
                "@"
                 delimited by size
           INTO DATEN-BUFFER1
         END-STRING
         SET ENDE TO TRUE
         PERFORM Z002-PROGERR
     END-IF
     .
 L100-99.
     EXIT.

******************************************************************
* ISO-Nachricht bereitstellen
******************************************************************
 L110-COB2ISO SECTION.
 L110-00.
     SET W207-COB2ISO TO TRUE
     PERFORM M130-CALL-WISO207

     IF  W207-RCODE NOT = ZERO
         MOVE W207-RCODE TO D-NUM4
         MOVE 1201 TO ERROR-NR of GEN-ERROR
         STRING "WISO207 (COB2ISO)/@"
                "RC: " D-NUM4
                "@"
                 delimited by size
           INTO DATEN-BUFFER1
         END-STRING
         PERFORM Z002-PROGERR
     END-IF

**  ---> Nachricht übertragen
     MOVE W207-ISOSTRING TO IMSG-NDATEN
     MOVE W207-COBDATEN  TO IMSG-COBDATEN
     MOVE W207-ISOLEN    TO IMSG-SENDLEN
                            IMSG-DATLEN

**  ---> nur fürs testen
     IF  TRACE-ON
         move "Test #4 - L110-COB2ISO - w207-tbmp:" to daten-buffer1
         move w207-tbmp-o (1:64) to daten-buffer2
         move W207-ISOLEN to d-num4
         move "isolen: " to daten-buffer3
         move d-num4     to daten-buffer3 (9:)
         perform z002-progerr
     END-IF

     .
 L110-99.
     EXIT.




******************************************************************
* Aufruf Modul SDBCDU5
******************************************************************
 M100-CALL-SDBCDU5 SECTION.
 M100-00.
     MOVE 9999 TO SDB-RCODE
     SET SDB-CMD-INS-OTA TO TRUE
     CALL "SDBCDU5" USING SDB-SDBCDU0C
     IF  SDB-ERR
**      ---> Ausgabe Fehlermeldung
         MOVE 1201 TO ERROR-NR OF GEN-ERROR
         MOVE SDB-RCODE TO D-NUM4
         STRING  "SDBCDU5@"
                 "RCODE@"
                 D-NUM4
                 "@"
                     delimited by size
           INTO  DATEN-BUFFER1
         END-STRING
         PERFORM Z002-PROGERR
**      ---> Programm soll trotzdem weiterarbeiten
         SET ENDE TO TRUE
     END-IF
     .
 M100-99.
     EXIT.

******************************************************************
* Aufruf Modul WISO207
******************************************************************
 M130-CALL-WISO207 SECTION.
 M130-00.
     CALL "WISO207" USING W207-WISO207C
     .
 M130-99.
     EXIT.

******************************************************************
* Aufruf Modul WSY7066
******************************************************************
 M140-CALL-WSY7066 SECTION.
 M140-00.
     CALL "WSY7066" USING W66-WSY7066C
                          INTERN-MESSAGE
     IF  W66-RCODE NOT = ZERO
         MOVE 97 TO W-AC
     END-IF
     .
 M140-99.
     EXIT.

******************************************************************
* Aufruf Modul WSYS930
******************************************************************
 M150-CALL-WSYS930 SECTION.
 M150-00.
     CALL "WSYS930" USING ROUT-SATZ
     IF  ROUT-NOT-OK
         IF  ROUT-CMD-ID
             MOVE 02 TO W-AC
             MOVE 1201 TO ERROR-NR OF GEN-ERROR
             STRING "WSYS930@nin KANR2ID: "
                    ROUT-MERKMAL
                    "@"
                         delimited by size
               INTO DATEN-BUFFER1
             END-STRING
             PERFORM Z002-PROGERR
             EXIT SECTION
         END-IF
         IF  ROUT-CMD-AS
             EVALUATE TRUE
**ROUT-ASERR- AS nicht erreichbar: Weiter mit STIP-Verarbeitung
                 WHEN ROUT-ASERR MOVE ZEROS TO W-AC
                                 SET ROUT-OK TO TRUE
                 WHEN OTHER      MOVE 91 TO W-AC
                                 MOVE 1201 TO ERROR-NR OF GEN-ERROR
                                 MOVE ROUT-KZ TO D-NUM20
                                 MOVE ROUT-CARDID TO D-NUM21
                                 STRING "WSYS930@kein TUEAS: "
                                        D-NUM20 "/" D-NUM21 "@"
                                             delimited by size
                                   INTO DATEN-BUFFER1
                                 END-STRING
                                 PERFORM Z002-PROGERR
             END-EVALUATE
             EXIT SECTION
         END-IF
     END-IF
     .
 M150-99.
     EXIT.

******************************************************************
* Aufruf Modul SYSAWKZ
******************************************************************
 M170-CALL-SYSAWKZ SECTION.
 M170-00.
     CALL "SYSAWKZ" USING WKZ-SYSWKZ0C
     IF  WKZ-ERR
         MOVE 30   TO W-AC
         MOVE 1201 TO ERROR-NR of GEN-ERROR
         MOVE WKZ-RCODE TO D-NUM4
         STRING  "SYSWKZ0"
                 "@"
                 "RCODE:"
                 "@"
                 D-NUM4
                 "@"
                     DELIMITED BY SIZE
           INTO DATEN-BUFFER1
         END-STRING
         EVALUATE WKZ-RCODE
             WHEN 2      MOVE "WKZ nicht zugelassen (=WKZKURS)"   TO DATEN-BUFFER2
                         MOVE WKZ-WKZ TO D-NUM4
                         MOVE "Ausgangswährung WKZ ="             TO DATEN-BUFFER3
                         MOVE D-NUM4                              TO DATEN-BUFFER3 (22:)

             WHEN OTHER  continue
         END-EVALUATE
         PERFORM Z002-PROGERR
     END-IF
     .
 M170-99.
     EXIT.

******************************************************************
* Aufruf Modul IUMSW07
******************************************************************
 M180-CALL-IUMSW07 SECTION.
 M180-00.
     CALL "IUMSW07" USING WUMS-WUMSO07C
     IF  WUMS-ERR
         MOVE 1201           TO ERROR-NR OF GEN-ERROR
         MOVE WUMS-RCODE     TO D-NUM4
         STRING  "IUMSW07@"
                 D-NUM4
                 "@@"
                     delimited by size
           INTO  DATEN-BUFFER1
         END-STRING
         SET ENDE TO TRUE
         PERFORM Z002-PROGERR
         EXIT SECTION
     END-IF
     .
 M180-99.
     EXIT.

******************************************************************
* Aufruf Artikel-Mapper
******************************************************************
 P100-PSD-ARTIKELMAPPER SECTION.
 P100-00.
**  ---> Nachrichtenparameter (Server, Länge, Timeout)
     MOVE W-ARTMAP TO PS-SRVCLASS
     MOVE 2048     TO PS-REQLEN
     MOVE 1000     TO PS-TIMEOUT

**  ---> Übrige Parameter
     MOVE PS-REQLEN TO PS-MAXREPLEN
     MOVE ZERO      TO PS-AKTREPLEN
                       PS-ERROR

**  ---> ggf. Pathwaysystem
     IF  PS-PATHNAME = SPACE
         MOVE ANCNAME TO PS-PATHNAME
     END-IF

     ENTER "SERVERCLASS_SEND_" USING  PS-PATHNAME
                                      PS-PATHNAMELEN
                                      PS-SRVCLASS
                                      PS-SRVCLASSLEN
                                      AMP-SCHNITTSTELLE
                                      PS-REQLEN
                                      PS-MAXREPLEN
                                      PS-AKTREPLEN
                                      PS-TIMEOUT
                               GIVING PS-ERROR

     IF  PS-ERROR NOT = ZERO
         ENTER "SERVERCLASS_SEND_INFO_" USING SSI-PSERROR
                                              SSI-FSERROR
                                       GIVING SSI-ERROR
         IF  SSI-ERROR = ZERO
             SET ENDE  TO TRUE
             MOVE 1210 TO ERROR-NR of GEN-ERROR
             MOVE PS-ERROR    TO D-NUM4
             MOVE SSI-PSERROR TO D-NUM4N
             MOVE SSI-FSERROR TO D-NUM4M
             STRING  D-NUM4
                     "/"
                     D-NUM4N
                     "/"
                     D-NUM4M
                     "@"
                         delimited by size
               INTO DATEN-BUFFER1
             END-STRING
         ELSE
             SET ENDE TO TRUE
             MOVE 1210 TO ERROR-NR of GEN-ERROR
             MOVE SSI-ERROR TO D-NUM4
             STRING  "INFO-Fehler: "
                     D-NUM4
                     "@"
                         delimited by size
               INTO DATEN-BUFFER1
             END-STRING
         END-IF
         PERFORM Z002-PROGERR
     END-IF
     .
 P100-99.
     EXIT.

******************************************************************
* Aufruf WXMSG06S ueber Pathsend
******************************************************************
 P110-PSD-SPERRLISTEN-PRUEFUNG SECTION.
 P110-00.

     MOVE "WTBLL06S"           TO PS-SRVCLASS
     MOVE 4096                 TO PS-REQLEN
     MOVE 1000                 TO PS-TIMEOUT

     MOVE PS-REQLEN            TO PS-MAXREPLEN
     MOVE ZERO                 TO PS-AKTREPLEN,
                                  PS-ERROR
     IF  PS-PATHNAME = SPACES
         MOVE    ANCNAME       TO PS-PATHNAME
     END-IF

     ENTER "SERVERCLASS_SEND_" USING  PS-PATHNAME
                                      PS-PATHNAMELEN
                                      PS-SRVCLASS
                                      PS-SRVCLASSLEN
                                      INTERN-MESSAGE
                                      PS-REQLEN
                                      PS-MAXREPLEN
                                      PS-AKTREPLEN
                                      PS-TIMEOUT
                              GIVING  PS-ERROR

     IF PS-ERROR NOT = ZEROS
         ENTER "SERVERCLASS_SEND_INFO_" USING SSI-PSERROR
                                              SSI-FSERROR
                                       GIVING SSI-ERROR
         IF  SSI-ERROR = ZERO
             SET ENDE TO TRUE
             MOVE 1210 TO ERROR-NR OF GEN-ERROR
             MOVE PS-ERROR  TO D-NUM4
             MOVE SSI-PSERROR TO D-NUM4N
             MOVE SSI-FSERROR TO D-NUM4M
             STRING  D-NUM4
                     "/"
                     D-NUM4N
                     "/"
                     D-NUM4M
                     "@"
                         DELIMITED BY SIZE
               INTO DATEN-BUFFER1
             END-STRING
         ELSE
             SET ENDE TO TRUE
             MOVE 1210 TO ERROR-NR OF GEN-ERROR
             MOVE SSI-ERROR TO D-NUM4
             STRING  "INFO-Fehler: "
                     D-NUM4
                     "@"
                         DELIMITED BY SIZE
               INTO DATEN-BUFFER1
             END-STRING
         END-IF
         PERFORM Z002-PROGERR
     END-IF
     .
 P110-99.
     EXIT.


******************************************************************
* hexen eines Strings (max. 16 Bytes)
*        Eingabe:    HEX16
*        Ausgabe:    HEX8
******************************************************************
 P900-WTHEX SECTION.
 P900-00.
     ENTER TAL "WT^HEX" USING HEX8 HEX16
     .
 P900-99.
     EXIT.

******************************************************************
* unhexen eines Strings (max. 16 Bytes)
*        Eingabe:    HEX8
*        Ausgabe:    HEX16
******************************************************************
 P910-WTUNHEX SECTION.
 P910-00.
     ENTER TAL "WT^UNHEX" USING HEX8 HEX16
     .
 P910-99.
     EXIT.

******************************************************************
* feststellen belegte Länge eines Strings
*        Eingabe:    W-BUFFER
*                    W-BUFFER-LEN
*        Ausgabe:    W-BUFFER-AKT
******************************************************************
 P920-STRING-LEN SECTION.
 P920-00.
     MOVE ZERO TO W-BUFFER-AKT
     ENTER TAL "String^Laenge" USING  W-BUFFER
                                      W-BUFFER-LEN
                               GIVING W-BUFFER-AKT
     .
 P920-99.
     EXIT.

******************************************************************
* Aufruf COBOL-Utility: GETPARAMTEXT
*
*              Eingabe: stup-portion (parametername)
*              Ausgabe: stup-result  (-1:NOK, >=0:OK länge von text)
*                       stup-text    (value von ..-portion)
*
******************************************************************
 P950-GETPARAMTEXT SECTION.
 P950-00.
     MOVE SPACE TO STUP-TEXT
     ENTER "GETPARAMTEXT"    USING   STUP-PORTION
                                     STUP-TEXT
                             GIVING  STUP-RESULT
     EVALUATE STUP-RESULT
         WHEN -9999 THRU ZERO
**                  ---> Fehler aus GetParamText
**                  ---> oder Parameter nicht vorhanden
                     MOVE 1101 TO ERROR-NR OF GEN-ERROR
                     MOVE STUP-RESULT TO D-NUM4
                     STRING  STUP-PORTION    delimited by space
                             "@"             delimited by size
                             D-NUM4          delimited by size
                             " oder fehlt@"  delimited by size
                       INTO  DATEN-BUFFER1
                     END-STRING
                     PERFORM Z002-PROGERR
                     SET PRG-ABBRUCH TO TRUE
                     EXIT SECTION

         WHEN OTHER
**                  ---> ParamText ist vorhanden in STUP-TEXT
**                  ---> Länge     ist vorhanden in STUP-RESULT

                     continue
     END-EVALUATE
     .
 P950-99.
     EXIT.

******************************************************************
* holen ancestor
******************************************************************
 P960-GET-PROC-ANCNAME SECTION.
 P960-00.
     ENTER TAL "WT^GETPROCNAME" USING FEHL
                                      PNAME
                                      PNAMELEN
     IF  PNAMELEN > 1
         continue
     ELSE
         MOVE "Prozessname nicht bestimmbar!" TO DATEN-BUFFER1
         MOVE "Programm-Abbruch (PFCSIP7S)"   TO DATEN-BUFFER2
         PERFORM Z002-PROGERR
         SET PRG-ABBRUCH TO TRUE
         EXIT SECTION
     END-IF

     ENTER TAL "WT^ANCNAME" USING FEHL
                                  ANCNAME
                                  PAIRINFO
     IF  FEHL not = ZERO
         MOVE "Ancestor Pathway nicht ermittelbar" TO DATEN-BUFFER1
         MOVE "Programm-Abbruch (PFCSIP7S)"        TO DATEN-BUFFER2
         STRING  "Prozessname: "
                 PNAME (1:PNAMELEN)
                     delimited by size
           INTO  DATEN-BUFFER3
         END-STRING
         PERFORM Z002-PROGERR
         SET PRG-ABBRUCH TO TRUE
         EXIT SECTION
     END-IF
     .
 P960-99.
     EXIT.


******************************************************************
* Lesen Message-Datei $RECEIVE
******************************************************************
 R000-LESEN-MESSAGE SECTION.
 R000-00.
     READ MESSAGE-DATEI
     IF  FILE-NOK
         SET MSG-EOF TO TRUE
     ELSE
         MOVE IMSG-DATLEN TO IMSG-SENDLEN
     END-IF
     .
 R000-99.
     EXIT.

******************************************************************
* Schreiben Reply-Datei $RECEIVE
******************************************************************
 S000-SCHREIBEN-REPLY SECTION.
 S000-00.
**  ---> wenn Verarbeitung nicht OK:
     IF  ENDE
         SET IMSG-READ-IL TO TRUE
         MOVE SPACE TO IMSG-NEXTSERV
         MOVE "*"   TO IMSG-TRACETERMID
     END-IF

     MOVE IMSG-DATLEN TO IMSG-SENDLEN
     COMPUTE REPLY-LAENGE = IMSG-SENDLEN + 88

     MOVE K-MODUL        TO IMSG-MONNAME
     MOVE INTERN-MESSAGE TO REPLY-SATZ

**  ---> Appl. Monitoring
     SET PCAP-WRITE-TRACE TO TRUE
     CALL "WCAPM92" USING PCAP-PCAPM01C

     WRITE REPLY-SATZ
     .
 S000-99.
     EXIT.

******************************************************************
* Select auf Tabelle KEYNAMEN
******************************************************************
 S100-SELECT-KEYNAMEN SECTION.
 S100-00.
     EXEC SQL
         SELECT    ROUTKZ, CARDID, ASNAME, KEYNAME, ISOGEN
                 , ISOVERS, OPTIONEN
           INTO   :ROUTKZ of KEYNAMEN
                 ,:CARDID of KEYNAMEN
                 ,:ASNAME of KEYNAMEN
                 ,:KEYNAME of KEYNAMEN
                 ,:ISOGEN of KEYNAMEN
                 ,:ISOVERS of KEYNAMEN
                 ,:OPTIONEN of KEYNAMEN
           FROM  =KEYNAMEN
          WHERE  ROUTKZ, CARDID
                 =    :ROUTKZ of KEYNAMEN
                     ,:CARDID of KEYNAMEN
         BROWSE  ACCESS
     END-EXEC
     EVALUATE SQLCODE OF SQLCA
         WHEN ZERO       SET KEYNAMEN-OK  TO TRUE
         WHEN OTHER      SET ENDE         TO TRUE
                         SET KEYNAMEN-NOK TO TRUE
                         MOVE 91   TO W-AC
                         MOVE 2001 TO ERROR-NR of GEN-ERROR
                         MOVE ROUTKZ of KEYNAMEN TO D-NUM20
                         MOVE CARDID of KEYNAMEN TO D-NUM21
                         STRING  "KEYNAMEN@"
                                 "ROUTID,CARDID="
                                 D-NUM20 "/" D-NUM21 "@"
                                     DELIMITED BY SIZE
                           INTO DATEN-BUFFER1
                         END-STRING
                         PERFORM Z002-PROGERR
                         EXIT SECTION
     END-EVALUATE
     .
 S100-99.
     EXIT.

******************************************************************
* Select auf Tabelle MDNR2AS
******************************************************************
 S110-SELECT-MDNR2AS SECTION.
 S110-00.
     EXEC SQL
         SELECT   ROUTKZ, CARDID, MDNR, AIID, TRACENR, DATUM
           INTO   :ROUTKZ of MDNR2AS
                 ,:CARDID of MDNR2AS
                 ,:MDNR of MDNR2AS
                 ,:AIID of MDNR2AS
                 ,:TRACENR of MDNR2AS
                 ,:DATUM of MDNR2AS
                     TYPE AS DATETIME YEAR TO DAY
           FROM  =MDNR2AS
          WHERE   ROUTKZ = :ROUTKZ of MDNR2AS
            AND  (CARDID = :CARDID of MDNR2AS or CARDID = 0)
            AND  (MDNR   = :MDNR   of MDNR2AS or MDNR = 0)
            FOR  REPEATABLE ACCESS
             IN  EXCLUSIVE MODE
     END-EXEC
     EVALUATE SQLCODE OF SQLCA
         WHEN ZERO   SET MDNR2AS-OK TO TRUE
         WHEN OTHER  MOVE 91   TO W-AC
                     MOVE 2001 TO ERROR-NR of GEN-ERROR
                     MOVE CARDID of MDNR2AS TO D-NUM20
                     MOVE ROUTKZ of MDNR2AS TO D-NUM21
                     STRING "MDNR2AS@"
                            MDNR   OF MDNR2AS "/"
                            D-NUM20 "/" D-NUM21 "@"
                                 delimited by size
                       INTO DATEN-BUFFER1
                     END-STRING
                     PERFORM Z002-PROGERR
     END-EVALUATE
     .
 S110-99.
     EXIT.

******************************************************************
* Update auf MDNR2AS
******************************************************************
 S120-UPDATE-MDNR2AS  SECTION.
 S120-00.
     EXEC SQL
         UPDATE  =MDNR2AS
            SET   TRACENR = :TRACENR of MDNR2AS
                 ,DATUM   = CURRENT YEAR TO DAY
          WHERE  MDNR, CARDID, ROUTKZ
                 =  :MDNR   of MDNR2AS
                   ,:CARDID of MDNR2AS
                   ,:ROUTKZ of MDNR2AS
     END-EXEC
     EVALUATE SQLCODE OF SQLCA
         WHEN ZERO   SET MDNR2AS-OK  TO TRUE
         WHEN OTHER  SET MDNR2AS-NOK TO TRUE
                     MOVE 91   TO W-AC
                     MOVE 2003 TO ERROR-NR of GEN-ERROR
                     MOVE CARDID of MDNR2AS TO D-NUM20
                     MOVE ROUTKZ of MDNR2AS TO D-NUM21
                     STRING "MDNR2AS@"
                            MDNR   of MDNR2AS "/"
                            D-NUM20 "/" D-NUM21 "@"
                                 delimited by size
                       INTO DATEN-BUFFER1
                     END-STRING
                     PERFORM Z002-PROGERR
     END-EVALUATE
     .
 S120-99.
     EXIT.

******************************************************************
* Select auf Tabelle TSKART40
******************************************************************
 S150-SELECT-TSKART40 SECTION.
 S150-00.
     EXEC SQL
         SELECT    VUNR, ROUTKZ
           INTO   :VUNR of TSKART40
                 ,:ROUTKZ of TSKART40
           FROM  =TSKART40
          WHERE  MDNR, TSNR, CARDSYS, CARDID
                 =  :MDNR    of TSKART40
                   ,:TSNR    of TSKART40
                   ,:CARDSYS of TSKART40
                   ,:CARDID  of TSKART40
         BROWSE  ACCESS
     END-EXEC
     EVALUATE SQLCODE OF SQLCA
         WHEN ZERO       SET TSKART40-OK  TO TRUE
         WHEN OTHER      SET TSKART40-NOK TO TRUE
                         MOVE 2001 TO ERROR-NR of GEN-ERROR
                         STRING "TSKART40@"
                                "CSYS/CID: "
                                CARDSYS of TSKART40 "/"
                                CARDID  of TSKART40 "@"
                                     delimited by size
                           INTO DATEN-BUFFER1
                         END-STRING
                         MOVE 04  TO W-AC
                         MOVE "*" TO IMSG-TRACETERMID
                         PERFORM Z002-PROGERR
     END-EVALUATE
     .
 S150-99.
     EXIT.

******************************************************************
* Select auf Tabelle STATION
******************************************************************
 S160-SELECT-STATION SECTION.
 S160-00.
     EXEC SQL
         SELECT   ORT, NAME
           INTO   :ORT of STATION
                 ,:NAME of STATION
           FROM  =STATION
          WHERE  MDNR, TSNR
                 =    :MDNR of STATION
                     ,:TSNR of STATION
         BROWSE  ACCESS
     END-EXEC
     EVALUATE SQLCODE OF SQLCA
         WHEN ZERO   SET STATION-OK  TO TRUE
         WHEN OTHER  SET ENDE        TO TRUE
                     SET STATION-NOK TO TRUE
                     MOVE 2001 TO ERROR-NR of GEN-ERROR
                     STRING  "STATION@"
                             "MDNR/TSNR: "
                             MDNR of STATION "/"
                             TSNR of STATION "@"
                                 delimited by size
                       INTO  DATEN-BUFFER1
                     END-STRING
                     STRING  "TERMNR/TRACENR: "
                             W-TERMNR
                             W-TRACENR
                                 delimited by size
                       INTO  DATEN-BUFFER2
                     END-STRING
                     MOVE "Transaktion wird nicht beantwortet" TO DATEN-BUFFER3
                     MOVE "*" TO IMSG-TRACETERMID
                     PERFORM Z002-PROGERR
     END-EVALUATE
     .
 S160-99.
     EXIT.

******************************************************************
* Select auf Tabelle STATIONA
******************************************************************
 S170-SELECT-STATIONA SECTION.
 S170-00.
     EXEC SQL
         SELECT    KZ_LAENGE, LAENGE
                 , KZ_BREITE, BREITE
           INTO   :KZ-LAENGE of STATIONA
                 ,:LAENGE of STATIONA
                 ,:KZ-BREITE of STATIONA
                 ,:BREITE of STATIONA
           FROM  =STATIONA
          WHERE  MDNR, TSNR
                 =    :MDNR of STATIONA
                     ,:TSNR of STATIONA
         BROWSE  ACCESS
     END-EXEC
     EVALUATE SQLCODE OF SQLCA
         WHEN ZERO       SET STATIONA-OK
                             GEODATA-YES  TO TRUE
         WHEN 100        SET STATIONA-NOK
                             GEODATA-NO   TO TRUE
                         EXIT SECTION
         WHEN OTHER      SET ENDE
                             GEODATA-NO   TO TRUE
                         EXIT SECTION
     END-EVALUATE

**  ---> Daten zur Verfügung stellen
     MOVE KZ-BREITE of STATIONA TO GEO-KZ-BREITE
     MOVE KZ-LAENGE of STATIONA TO GEO-KZ-LAENGE

     COMPUTE GEO-BREITE-8
           = BREITE of STATIONA
           * GEO-FAKTOR
     ON SIZE ERROR
        MOVE ZEROS TO GEO-BREITE-8
     END-COMPUTE

     COMPUTE GEO-LAENGE-9
           = LAENGE of STATIONA
           * GEO-FAKTOR
     ON SIZE ERROR
        MOVE ZEROS TO GEO-LAENGE-9
     END-COMPUTE
     .
 S170-99.
     EXIT.

******************************************************************
* Insert auf Tabelle TXILOG70
******************************************************************
 S180-INSERT-TXILOG70 SECTION.
 S180-00.
     EXEC SQL
         INSERT
           INTO  =TXILOG70
                 ( PNR, TERMNR, TRACENR, ISONTYP, MDNR
                 , TSNR, TRACENR_AS, TRACENR_S, BATCHNR, KANR
                 , KZ_E2EE, KEYNAME, BETRAG, BETRAG_AUTOR
                 , BETRAG_CASHBACK, BETRAG_ART, CARDID, ROUTKZ
                 , LTGIND, ASID, AC_AS, AC_TERM, GENNR, WKZ
                 , LOGPROT, KZ_BEARB, KZ_VERF, KZ_UMSATZ, ABL_JJMM
                 , ACQUIRER_ID, ERFASSUNGS_ART, KARTEN_ART
                 , KARTENFOLGE, POS_DATEN, TRANS_ART, TRANS_TYP
                 , CVM_RESULT, BRANCHEN_KZ, HAENDLERNAME, PROJEKT_ABH_DATEN
                 , VUNR, ZP_VERKAUF, ZP_TIN, ZP_AOUT
                 , ZP_TOUT, AA_BMP38, AF_BMP07, ARTIKEL, EMV_DATEN
                 )
         VALUES  (
                  :PNR of TXILOG70
                 ,:TERMNR of TXILOG70
                 ,:TRACENR of TXILOG70
                 ,:ISONTYP of TXILOG70
                 ,:MDNR of TXILOG70
                 ,:TSNR of TXILOG70
                 ,:TRACENR-AS of TXILOG70
                 ,:TRACENR-S of TXILOG70
                 ,:BATCHNR of TXILOG70
                 ,:KANR of TXILOG70
                 ,:KZ-E2EE of TXILOG70
                 ,:KEYNAME of TXILOG70
                 ,:BETRAG of TXILOG70
                 ,:BETRAG-AUTOR of TXILOG70
                 ,:BETRAG-CASHBACK of TXILOG70
                 ,:BETRAG-ART of TXILOG70
                 ,:CARDID of TXILOG70
                 ,:ROUTKZ of TXILOG70
                 ,:LTGIND of TXILOG70
                 ,:ASID of TXILOG70
                 ,:AC-AS of TXILOG70
                 ,:AC-TERM of TXILOG70
                 ,:GENNR of TXILOG70
                 ,:WKZ of TXILOG70
                 ,:LOGPROT of TXILOG70
                 ,:KZ-BEARB of TXILOG70
                 ,:KZ-VERF of TXILOG70
                 ,:KZ-UMSATZ of TXILOG70
                 ,:ABL-JJMM of TXILOG70
                 ,:ACQUIRER-ID of TXILOG70
                 ,:ERFASSUNGS-ART of TXILOG70
                 ,:KARTEN-ART of TXILOG70
                 ,:KARTENFOLGE of TXILOG70
                 ,:POS-DATEN of TXILOG70
                 ,:TRANS-ART of TXILOG70
                 ,:TRANS-TYP of TXILOG70
                 ,:CVM-RESULT of TXILOG70
                 ,:BRANCHEN-KZ of TXILOG70
                 ,:HAENDLERNAME of TXILOG70
                 ,:PROJEKT-ABH-DATEN of TXILOG70
                 ,:VUNR of TXILOG70
                 ,:ZP-VERKAUF of TXILOG70
*                 ,:H-ZP-IN
                 ,:ZP-TIN of TXILOG70
                     TYPE AS DATETIME YEAR TO FRACTION(2)
                 ,:ZP-AOUT of TXILOG70
                     TYPE AS DATETIME YEAR TO FRACTION(2)
*                 ,:H-ZP-OUT
                 ,:ZP-TOUT of TXILOG70
                     TYPE AS DATETIME YEAR TO FRACTION(2)
                 ,:AA-BMP38 of TXILOG70
                 ,:AF-BMP07 of TXILOG70
                 ,:ARTIKEL of TXILOG70
                 ,:EMV-DATEN of TXILOG70
                 )
     END-EXEC
     EVALUATE SQLCODE OF SQLCA
         WHEN ZERO   SET TXILOG70-OK  TO TRUE
         WHEN OTHER  SET TXILOG70-NOK TO TRUE
                     SET ENDE TO TRUE
                     MOVE 2005 TO ERROR-NR of GEN-ERROR
                     STRING  "TXILOG70@"
                             PNR     of TXILOG70 "/"
                             TERMNR  of TXILOG70 "/"
                             TRACENR of TXILOG70 "/"
                             ISONTYP of TXILOG70
                                 delimited by size
                       INTO DATEN-BUFFER1
                     END-STRING
                     PERFORM Z002-PROGERR
     END-EVALUATE
     .
 S180-99.
     EXIT.

******************************************************************
* Insert auf Tabelle TXNLOG70
******************************************************************
 S190-INSERT-TXNLOG70-TS SECTION.
 S190-00.
     EXEC SQL
         INSERT
           INTO  =TXNLOG70
                 ( PNR, TERMNR, TRACENR, ISONTYP, KZ_MSG, ISO_VERF
                 , MDNR, TSNR, LOG_SRV, FREHEADER, ANFRAGE, ANTWORT
                 )
         VALUES  (
                  :PNR of TXNLOG70-TS
                 ,:TERMNR of TXNLOG70-TS
                 ,:TRACENR of TXNLOG70-TS
                 ,:ISONTYP of TXNLOG70-TS
                 ,:KZ-MSG of TXNLOG70-TS
                 ,:ISO-VERF of TXNLOG70-TS
                 ,:MDNR of TXNLOG70-TS
                 ,:TSNR of TXNLOG70-TS
                 ,:LOG-SRV of TXNLOG70-TS
                 ,:FREHEADER of TXNLOG70-TS
                 ,:ANFRAGE of TXNLOG70-TS
                 ,:ANTWORT of TXNLOG70-TS
                 )
     END-EXEC
     EVALUATE SQLCODE OF SQLCA
         WHEN ZERO   SET TXNLOG70-OK  TO TRUE
         WHEN OTHER  SET TXNLOG70-NOK TO TRUE
                     SET ENDE TO TRUE
                     MOVE 2005 TO ERROR-NR of GEN-ERROR
                     STRING  "TXNLOG70@"
                             PNR     of TXNLOG70-TS "/"
                             TERMNR  of TXNLOG70-TS "/"
                             TRACENR of TXNLOG70-TS "/"
                             ISONTYP of TXNLOG70-TS "/"
                             KZ-MSG  of TXNLOG70-TS
                                 delimited by size
                       INTO DATEN-BUFFER1
                     END-STRING
                     PERFORM Z002-PROGERR
     END-EVALUATE
     .
 S190-99.
     EXIT.

******************************************************************
* Insert auf Tabelle ASYNC70
******************************************************************
 S210-INSERT-ASYNC70 SECTION.
 S210-00.
     EXEC SQL
         INSERT
           INTO  =ASYNC70
                 ( PNR,       TERMNR,  TRACENR,  ISONTYP, MDNR,
                   TSNR,      VKZ,     CARDID,   ROUTKZ,  TRACENR_AS,
                   KZ_BEARB,  ANZ_REP, KEY_NAME, ZPINS,   ZPUPD,
                   FREHEADER, ANFRAGE, ANTWORT
                 )
         VALUES  (
                  :PNR        OF ASYNC70
                 ,:TERMNR     OF ASYNC70
                 ,:TRACENR    OF ASYNC70
                 ,:ISONTYP    OF ASYNC70
                 ,:MDNR       OF ASYNC70
                 ,:TSNR       OF ASYNC70
                 ,:VKZ        OF ASYNC70
                 ,:CARDID     OF ASYNC70
                 ,:ROUTKZ     OF ASYNC70
                 ,:TRACENR-AS OF ASYNC70
                 ,:KZ-BEARB   OF ASYNC70
                 ,:ANZ-REP    OF ASYNC70
                 ,:KEY-NAME   OF ASYNC70
                 ,current  YEAR TO FRACTION(2)
                 ,current  YEAR TO FRACTION(2)
                 ,:FREHEADER      OF ASYNC70
                 ,:ANFRAGE        OF ASYNC70
                 ,:ANTWORT        OF ASYNC70
                 )
     END-EXEC
     EVALUATE SQLCODE OF SQLCA
         WHEN ZERO   SET ASYNC70-OK  TO TRUE
         WHEN OTHER  SET ASYNC70-NOK TO TRUE
                     SET ENDE        TO TRUE
     END-EVALUATE
     .
 S210-99.
     EXIT.

******************************************************************
* Laden STIPLIMS
******************************************************************
 S600-GET-STIPLIMS SECTION.
 S600-00.

* Kartenwechsel in W-CARDID-OLD
    IF CARDID OF STIPLIMS = W-CARDID-OLD
       EXIT SECTION
    END-IF

    EXEC SQL
         SELECT  STIP_CALL    * 100,
                 STIP_REJECT  * 100
         INTO   :H-STIP-CALL,
                :H-STIP-REJECT
         FROM =STIPLIMS
         WHERE   CARDID,
                 ROUTKZ
              = :CARDID OF STIPLIMS,
                 0
         FOR BROWSE ACCESS
    END-EXEC

    IF SQLCODE OF SQLCA = ZERO
*Neue Cardid merken
       MOVE CARDID OF STIPLIMS TO W-CARDID-OLD
    ELSE
       INITIALIZE GEN-ERROR
       MOVE SQLCODE OF SQLCA TO D-NUM4
       STRING "Fehler bei Lesen =STIPLIMS ", D-NUM4
       DELIMITED BY SIZE
           INTO   DATEN-BUFFER1
       STRING "CARDID: ", CARDID OF STIPLIMS
       DELIMITED BY SIZE
           INTO   DATEN-BUFFER2
       PERFORM Z002-PROGERR

       IF SQLCODE OF SQLCA = -8300
*         Ausgabe SQLERROR nur, wenn "File System Error", sonst PROGERR
          PERFORM Z001-SQLERROR
       END-IF
       SET ENDE TO TRUE
     END-IF
     .
 S600-99.
     EXIT.

******************************************************************
* Indoor: Nur pruefen, wenn die Restriktionspruefung für Indoor
*         eingeschaltet ist - W-RST-PRUEFEN true, weil W-RST-FLAG = "J"
*********************************************************************
 S700-RESTRIKTIONEN-INDOOR SECTION.
 S700-00.

     MOVE ZEROS  TO WG        OF WGRSTPRF
     MOVE SPACES TO OUTDOOR   OF WGRSTPRF

     MOVE ZEROS  TO WG-TS     OF ARTMAP
     MOVE ZEROS  TO WG-HOST   OF ARTMAP
     MOVE SPACES TO M-UNIT    OF ARTMAP
     MOVE ZEROS  TO TAX-CODE  OF ARTMAP

     MOVE ZEROS TO UP-COUNTER2

     PERFORM
     VARYING C4-I1
        FROM 1 BY 1
       UNTIL C4-I1 > UP-COUNTER
          OR C4-I1 > 20
          OR ENDE
          OR W-AC NOT = ZEROS
             MOVE UP-PROD-CODE(C4-I1)
               TO WG-TS OF ARTMAP
             PERFORM S701-RESTRIKTIONEN-INDOOR
     END-PERFORM

     .
 S700-99.
     EXIT.

******************************************************************
* Indoor: Restriktionspruefung der TXn (Cardid, WG, RST-CODE)
* RST-CODE: Priorisierung der erlaubten Artikel in Menge
* ( Was und wieviel )
******************************************************************

 S701-RESTRIKTIONEN-INDOOR SECTION.
 S701-00.

     EXEC SQL
          SELECT  B.WG_TS
          INTO    :WG-TS      OF ARTMAP
          FROM   =WGRSTPRF A, =ARTMAP B
          WHERE  A.WG     = B.WG_HOST
            AND  A.CARDID = B.CARDID
            AND   B.MDNR,
                  B.TSNR,
                  B.CARDID,
                  B.WG_TS
              =  :MDNR      OF ARTMAP
                ,:TSNR      OF ARTMAP
                ,:CARDID    OF ARTMAP
                ,:WG-TS     OF ARTMAP
            AND   A.RST_CODE
              <= :RST-CODE OF WGRSTPRF
          FOR BROWSE ACCESS
     END-EXEC

     IF SQLCODE OF SQLCA = ZERO
*Produkt als erlaubtes Produkt merken in Rueckgabestruktur BMP63
        ADD    1                      TO UP-COUNTER2
        MOVE   WG-TS OF ARTMAP        TO UP-PROD-CODE2(UP-COUNTER2)
        EXIT SECTION
     ELSE
        IF SQLCODE OF SQLCA = 100
           CONTINUE
        ELSE
*Sonstiger SQL-Fehler = Nicht erlaubt + Meldung
           MOVE 87                     TO W-AC
           INITIALIZE GEN-ERROR
           MOVE SQLCODE OF SQLCA       TO D-NUM4
           STRING "Fehler bei Lesen =WGRSTPRF ", D-NUM4
           DELIMITED BY SIZE INTO DATEN-BUFFER1
           STRING "CARDID: ", CARDID   OF WGRSTPRF,
                  " / WG: ",  WG       OF WGRSTPRF,
                  " / RST: ", RST-CODE OF WGRSTPRF
           DELIMITED BY SIZE INTO DATEN-BUFFER2
           PERFORM Z002-PROGERR
           IF SQLCODE OF SQLCA = -8300
*Ausgabe SQLERROR nur, wenn "File System Error", sonst PROGERR
              PERFORM Z001-SQLERROR
           END-IF
           EXIT SECTION
        END-IF
     END-IF

**Zweiter Versuch: TSNR = 0 (bei Artikelmapping auf Mandantenebene)

     MOVE ZERO TO TSNR OF ARTMAP
     EXEC SQL
          SELECT B.WG_TS
          INTO   :WG-TS OF ARTMAP
          FROM   =WGRSTPRF A, =ARTMAP B
          WHERE  A.WG     = B.WG_HOST
            AND  A.CARDID = B.CARDID
          AND    B.MDNR,
                 B.TSNR,
                 B.CARDID,
                 B.WG_TS
              = :MDNR      OF ARTMAP
               ,:TSNR      OF ARTMAP
               ,:CARDID    OF ARTMAP
               ,:WG-TS     OF ARTMAP
          AND    A.RST_CODE
             <= :RST-CODE OF WGRSTPRF
          FOR BROWSE ACCESS
     END-EXEC

     IF SQLCODE OF SQLCA = ZERO
*Produkt als erlaubtes Produkt merken in Rueckgabestruktur BMP63
        ADD    1                      TO UP-COUNTER2
        MOVE   WG-TS OF ARTMAP        TO UP-PROD-CODE2(UP-COUNTER2)
     ELSE
        IF SQLCODE OF SQLCA = 100
*Nicht gefunden = Nicht erlaubt
           MOVE 87                     TO W-AC
        ELSE
*          Sonstiger SQL-Fehler = Nicht erlaubt + Meldung
           MOVE 87                     TO W-AC
           INITIALIZE GEN-ERROR
           MOVE SQLCODE OF SQLCA       TO D-NUM4
           STRING "Fehler bei Lesen =WGRSTPRF ", D-NUM4
           DELIMITED BY SIZE
                   INTO DATEN-BUFFER1
           STRING "CARDID: ", CARDID   OF WGRSTPRF,
                  " / WG: ",  WG       OF WGRSTPRF,
                  " / RST: ", RST-CODE OF WGRSTPRF
           DELIMITED BY SIZE
                   INTO DATEN-BUFFER2
           PERFORM Z002-PROGERR
           IF SQLCODE OF SQLCA = -8300
*Ausgabe SQLERROR nur, wenn "File System Error", sonst PROGERR
              PERFORM Z001-SQLERROR
           END-IF
        END-IF
     END-IF
     .
 S701-99.
     EXIT.

******************************************************************
* Restriktionspruefung fuer Outdoor-Transaktionen, die durch
* BMP 25 gesetzt im Bereich Anfrage-Check identifiziert wurden
******************************************************************
 S702-RESTRIKTIONEN-OUTDOOR SECTION.
 S702-00.

     MOVE ZEROS  TO WG        OF WGRSTPRF

     MOVE ZEROS  TO WG-TS     OF ARTMAP
     MOVE ZEROS  TO WG-HOST   OF ARTMAP
     MOVE ZEROS  TO M-UNIT    OF ARTMAP
     MOVE ZEROS  TO TAX-CODE  OF ARTMAP

*G.00.06 - Anfang
     MOVE ZEROS TO UP-COUNTER2
*G.00.06 - Ende

     EXEC SQL
        OPEN OUTDOOR_CURS
     END-EXEC
     IF SQLCODE OF SQLCA = ZEROS
        CONTINUE
     ELSE
        INITIALIZE GEN-ERROR
        MOVE SQLCODE OF SQLCA TO D-NUM4
        STRING "Fehler bei OPEN OUTDOOR_CURS: ", D-NUM4
        DELIMITED BY SIZE
                INTO DATEN-BUFFER1
        STRING "CARDID: ", CARDID   OF WGRSTPRF,
               " / RST: ", RST-CODE OF WGRSTPRF
        DELIMITED BY SIZE
                INTO DATEN-BUFFER2
        PERFORM Z002-PROGERR

        IF SQLCODE OF SQLCA = -8300
*Ausgabe SQLERROR nur, wenn "File System Error", sonst PROGERR
           PERFORM Z001-SQLERROR
        END-IF
     END-IF

     EXEC SQL
        FETCH OUTDOOR_CURS
        INTO  :H-WG-OUTDOOR
     END-EXEC

     PERFORM UNTIL SQLCODE OF SQLCA NOT = ZERO

*G.00.06 - Anfang
*               OR UP-COUNTER2 > 20
**
                OR UP-COUNTER2 > 19
*G.00.06 - Ende

        IF SQLCODE OF SQLCA = ZERO
*Produkt als erlaubtes Produkt merken in Rueckgabestruktur BMP63
           ADD    1                      TO UP-COUNTER2
           MOVE   H-WG-OUTDOOR           TO UP-PROD-CODE2(UP-COUNTER2)
        ELSE
           IF SQLCODE OF SQLCA = 100
              CONTINUE
           ELSE
*Sonstiger SQL-Fehler = Nicht erlaubt + Meldung
              INITIALIZE GEN-ERROR
              MOVE SQLCODE OF SQLCA TO D-NUM4
              STRING "Fehler bei Lesen =WGRSTPRF ", D-NUM4
              DELIMITED BY SIZE
                          INTO DATEN-BUFFER1
              STRING "CARDID: ", CARDID   OF WGRSTPRF,
                     " / RST: ", RST-CODE OF WGRSTPRF
              DELIMITED BY SIZE
                          INTO DATEN-BUFFER2
              PERFORM Z002-PROGERR

                          IF SQLCODE OF SQLCA = -8300
*Ausgabe SQLERROR nur, wenn "File System Error", sonst PROGERR
                 PERFORM Z001-SQLERROR
              END-IF
           END-IF
        END-IF

*NACHLESEN CURSOR
        EXEC SQL
           FETCH OUTDOOR_CURS
           INTO  :H-WG-OUTDOOR
        END-EXEC

     END-PERFORM

     EXEC SQL
        CLOSE OUTDOOR_CURS
     END-EXEC
     .
 S702-99.
     EXIT.

******************************************************************
* OPEN Cursor
******************************************************************
 S900-OPEN-FCPARAM-CURSOR SECTION.
 S900-00.
     MOVE ZERO TO C9-COUNT
     EXEC SQL
         OPEN FCPARAM_CURS
     END-EXEC
     .
 S900-99.
     EXIT.

******************************************************************
* Fetch aus Tabelle FCPARAM
******************************************************************
 S910-FETCH-FCPARAM-CURSOR SECTION.
 S910-00.
     EXEC SQL
         FETCH FCPARAM_CURS
          INTO  :ROUTKZ of FCPARAM
               ,:CARDID of FCPARAM
               ,:ISONTYP of FCPARAM
               ,:KZ-MSG of FCPARAM
               ,:BMP of FCPARAM
               ,:LFDNR of FCPARAM
               ,:KZ-ABWEICHUNG of FCPARAM
     END-EXEC
     EVALUATE SQLCODE OF SQLCA
         WHEN ZERO   SET FCPARAM-OK  TO TRUE
                     ADD 1 TO C9-COUNT
         WHEN 100    SET FCPARAM-EOD TO TRUE
         WHEN OTHER  SET FCPARAM-NOK TO TRUE
     END-EVALUATE
     .
 S910-99.
     EXIT.

******************************************************************
* CLOSE Cursor
******************************************************************
 S920-CLOSE-FCPARAM-CURSOR SECTION.
 S920-00.
     EXEC SQL
         CLOSE FCPARAM_CURS
     END-EXEC
     .
 S920-99.
     EXIT.

******************************************************************
* OPEN Cursor
******************************************************************
 S930-OPEN-KEYNAMEN-CURSOR SECTION.
 S930-00.
     MOVE ZERO TO C9-COUNT
     EXEC SQL
         OPEN KEYNAMEN_CURS
     END-EXEC
     .
 S930-99.
     EXIT.

******************************************************************
* Fetch aus Tabelle KEYNAMEN
******************************************************************
 S940-FETCH-KEYNAMEN-CURSOR SECTION.
 S940-00.
     EXEC SQL
         FETCH KEYNAMEN_CURS
          INTO  :ROUTKZ of KEYNAMEN
               ,:CARDID of KEYNAMEN
               ,:KEYNAME of KEYNAMEN
               ,:ISOGEN of KEYNAMEN
               ,:ISOVERS of KEYNAMEN
     END-EXEC
     EVALUATE SQLCODE OF SQLCA
         WHEN 0      SET KEYNAMEN-OK  TO TRUE
                     ADD 1 TO C9-COUNT
         WHEN OTHER  SET KEYNAMEN-NOK TO TRUE
     END-EVALUATE
     .
 S940-99.
     EXIT.

******************************************************************
* CLOSE Cursor
******************************************************************
 S950-CLOSE-KEYNAMEN-CURSOR SECTION.
 S950-00.
     EXEC SQL
         CLOSE KEYNAMEN_CURS
     END-EXEC
     .
 S950-99.
     EXIT.
     
******************************************************************
* Select auf neue AIID-Tabelle
*G.02.01 - neu
******************************************************************
 S960-SELECT-AIID SECTION.
 S960-00.

     EXEC SQL
         SELECT AIID
         INTO  :AIID     OF FCAIID
         FROM  =FCAIID
         WHERE ROUTKZ, CARDID =
              :ROUTKZ    OF KEYNAMEN
             ,:CARDID    OF KEYNAMEN
         BROWSE ACCESS
     END-EXEC
     
     
     IF SQLCODE OF SQLCA = 100
         EXEC SQL
             SELECT AIID
             INTO  :AIID     OF FCAIID
             FROM  =FCAIID
             WHERE ROUTKZ, CARDID =
                  :ROUTKZ    OF KEYNAMEN
                 ,0
             BROWSE ACCESS
         END-EXEC
     END-IF
     
     EVALUATE SQLCODE OF SQLCA
        WHEN 0      CONTINUE
** ---> Dummy AIID, fuer RoutKZ, die nicht gepflegt sind
        WHEN 100    MOVE "000000" TO AIID OF FCAIID
        WHEN OTHER  MOVE  SQLCODE OF SQLCA    TO D-NUM4
                    STRING "Fehler bei LESEN FCAIID: ", D-NUM4
                    DELIMITED BY SIZE INTO DATEN-BUFFER1
                    MOVE ROUTKZ OF KEYNAMEN TO D-NUM4M
                    MOVE CARDID OF KEYNAMEN TO D-NUM4N
                    STRING "ROUTKZ= ",
                            D-NUM4M,
                            " / CARDID= ",
                            D-NUM4N,
                            " oder 0"
                    DELIMITED BY SIZE INTO DATEN-BUFFER2
                    PERFORM Z002-PROGERR
     END-EVALUATE

     .
 S960-99.
     EXIT.

******************************************************************
* Transaktionsbegrenzungen
******************************************************************
 U100-BEGIN SECTION.
 U100-00.
     EXEC SQL
         BEGIN WORK
     END-EXEC
     .
 U100-99.
     EXIT.

 U101-BEGINTRANSACTION SECTION.
 U101-00.
     ENTER TAL BEGINTRANSACTION
     .
 U101-99.
     EXIT.

 U110-COMMIT SECTION.
 U110-00.
     EXEC SQL
         COMMIT WORK
     END-EXEC
     .
 U110-99.
     EXIT.

 U111-ENDTRANSACTION SECTION.
 U111-00.
     ENTER TAL ENDTRANSACTION
        .
 U111-99.
     EXIT.

 U120-ROLLBACK SECTION.
 U120-00.
     EXEC SQL
         ROLLBACK WORK
     END-EXEC
     .
 U120-99.
     EXIT.

 U121-ABORTTRANSACTION SECTION.
 U121-00.
     ENTER TAL ABORTTRANSACTION
     .
 U121-99.
     EXIT.

******************************************************************
* TIMESTAMP erstellen
******************************************************************
 U200-TIMESTAMP SECTION.
 U200-00.
     ENTER TAL "TIME" USING TAL-TIME
     MOVE CORR TAL-TIME TO TAL-TIME-D

     MOVE TAL-JHJJ OF TAL-TIME-D TO TD-JHJJ
     MOVE TAL-MM   OF TAL-TIME-D TO TD-MM
     MOVE TAL-TT   OF TAL-TIME-D TO TD-TT
     MOVE TAL-HH   OF TAL-TIME-D TO TD-HH
     MOVE TAL-MI   OF TAL-TIME-D TO TD-MI
     MOVE TAL-SS   OF TAL-TIME-D TO TD-SS
     MOVE TAL-HS   OF TAL-TIME-D TO TD-HS
     .
 U200-99.
     EXIT.

******************************************************************
* suchen Eintrag aus Parametertabelle
*        Eingabe:    S-ISONTYP - Nachrichtentyp nach dem gesucht wird
*                    S-KZ-MSG  - Kennzeichen nach dem gesucht wird
*                    S-BMP     - BMP nach dem gesucht wird
*                    S-LFDNR   - LFDNR nach der gesucht wird (Start=0)
*        Ausgabe:    T-AKT-IND - Index der Fundstelle
*                    PRM-FOUND     - true or false
*                    PRM-NOT-FOUND - false of true
******************************************************************
 U300-SEARCH-TAB SECTION.
 U300-00.

*kl20180405 - G.01.04 - wg. Cardid ZERO zurueck zur klassischen
*                       Verarbeitung
*     MOVE ZERO TO T-AKT-IND
*
*     SEARCH ALL T-FCPARAM-TAB
*
*         AT END  SET PRM-NOT-FOUND TO TRUE
*                 EXIT SECTION
*
*         WHEN    T-KEY (IND-TAB) = S-SEARCH-KEY
*
*                 SET PRM-FOUND TO TRUE
*                 SET T-AKT-IND TO IND-TAB
*                 continue
*
*     END-SEARCH

*-->  SEARCH-KEY 2 besetzen
*     übernehmen kompletten 1. Key
     MOVE    S-SEARCH-KEY       TO S2-SEARCH-KEY
*     CARDID ersezten (ggf. auch für andere Werte möglich)
     MOVE    ZERO               TO S2-CARDID

*--> Suchschleife

*    Default: Nicht gefunden
     SET PRM-NOT-FOUND TO TRUE

     PERFORM VARYING T-AKT-IND from 1 BY 1  UNTIL T-AKT-IND > T-MAX
                                               OR PRM-FOUND

        IF T-KEY(T-AKT-IND) = S-SEARCH-KEY
        OR T-KEY(T-AKT-IND) = S2-SEARCH-KEY
           SET PRM-FOUND    TO TRUE
           EXIT PERFORM
        END-IF

     END-PERFORM
*kl20180405 - G.01.04 - Ende
     .
 U300-99.
     EXIT.

******************************************************************
* Interpretieren KZ-ABWEICHUNG
******************************************************************
 U400-INTERPRET-ABWEICHUNG SECTION.
 U400-00.
**  ---> Vorschrift separieren
     MOVE ZERO TO C4-ANZ
     MOVE 1    TO C4-PTR
     MOVE SPACES     TO W-TEILSTRING-TABELLE
     MOVE SPACES     TO W-DELIM-TABELLE
     MOVE LOW-VALUES TO W-COUNT-TABELLE

     UNSTRING T-KZ-ABWEICHUNG (T-AKT-IND)
                                delimited by
                                 ALL SPACE or "@"
         INTO W-TEILSTRING (1)  delimiter in W-DELIM (1)
                                count     in W-COUNT (1)
              W-TEILSTRING (2)  delimiter in W-DELIM (2)
                                count     in W-COUNT (2)
              W-TEILSTRING (3)  delimiter in W-DELIM (3)
                                count     in W-COUNT (3)
              W-TEILSTRING (4)  delimiter in W-DELIM (4)
                                count     in W-COUNT (4)
              W-TEILSTRING (5)  delimiter in W-DELIM (5)
                                count     in W-COUNT (5)
              W-TEILSTRING (6)  delimiter in W-DELIM (6)
                                count     in W-COUNT (6)
              W-TEILSTRING (7)  delimiter in W-DELIM (7)
                                count     in W-COUNT (7)
              W-TEILSTRING (8)  delimiter in W-DELIM (8)
                                count     in W-COUNT (8)
              W-TEILSTRING (9)  delimiter in W-DELIM (9)
                                count     in W-COUNT (9)
              W-TEILSTRING (10) delimiter in W-DELIM (10)
                                count     in W-COUNT (10)
         WITH     POINTER C4-PTR
         TALLYING IN      C4-ANZ
     END-UNSTRING

**  ---> Trennzeichen in W-DELIM      von Index 1
**  ---> zugeh. Wert  in W-TEILSTRING von Index 2

* Beispiel:
*    Source-String   : T2F=T14  (Terminal-BMP2 / fix "=" / Terminal-BMP14
*
*     ---> Teil-String  1 :                  Laenge: 0000 Trenner: T
*     ---> Teil-String  2 : 2                Laenge: 0001 Trenner: F
*     ---> Teil-String  3 : =                Laenge: 0001 Trenner: T
*     ---> Teil-String  4 : 14               Laenge: 0002 Trenner:
*     ---> Teil-String  5 :                  Laenge: 0000 Trenner:
*
*    C4-ANZ: Anzahl belegter Teilstrings

     IF  C4-ANZ = ZERO
**      ---> kein Inhalt
         EXIT SECTION
     END-IF

     MOVE SPACES TO W-BUFFER
     MOVE 1 TO C4-I2
     PERFORM VARYING C4-I1 FROM 1 BY 1
             UNTIL   C4-I1 > C4-ANZ

      COMPUTE C4-LEN = W-COUNT(C4-I1) - 1

      EVALUATE W-TEILSTRING (C4-I1) (1:1)

          WHEN "F" MOVE W-TEILSTRING(C4-I1) (2:C4-LEN)
                   TO W-BUFFER (C4-I2:C4-LEN)
                   ADD C4-LEN TO C4-I2

          WHEN "H" MOVE W-TEILSTRING(C4-I1) (2:C4-LEN)
                   TO W-BUFFER (C4-I2:C4-LEN)
                   MOVE W-BUFFER (C4-I2:C4-LEN) TO WTHEXS-SRC
                   MOVE C4-LEN                  TO WTHEXS-SRC-LEN
                   PERFORM V400-WT-HEX-STRING
                   MOVE WTHEXS-DST (1:WTHEXS-DST-LEN)
                   TO W-BUFFER (C4-I2: )
                   ADD WTHEXS-DST-LEN TO C4-I2

          WHEN "T" MOVE W-TEILSTRING(C4-I1) (2:C4-LEN) TO D-NUM4
                   MOVE D-NUM4 TO C4-I3
** ---> hier muss ggf. ein anderer Buffer gewählt werden
                   MOVE IMSG-CF(IMSG-TPTR(C4-I3):IMSG-TLEN(C4-I3))
                     TO W-BUFFER (C4-I2:IMSG-TLEN(C4-I3))
                   COMPUTE C4-I2 = C4-I2 + IMSG-TLEN(C4-I3)

          WHEN space  continue

          WHEN OTHER  SET ENDE TO TRUE
*G.02.01 - VERF-AS jetzt
*                         MOVE W-ROUTKZ TO D-NUM4
                      MOVE VERF-AS TO D-NUM4
*G.02.01 - Ende
                      STRING  "Unbekannte Verarbeitungsregeln "
                              "für Rout-KZ = "
                              D-NUM4
                      delimited by size
                        INTO  DATEN-BUFFER1
                      END-STRING
                      PERFORM Z002-PROGERR
                      EXIT SECTION

         END-EVALUATE

     END-PERFORM

     COMPUTE W-BUFFER-LEN = C4-I2 - 1

     continue
     .
 U400-99.
     EXIT.

******************************************************************
* hexen String
*                        Eingabe:    WTHEXS-SRC
*                                    WTHEXS-SRC-LEN
*                        Ausgabe:    WTHEXS-DST
*                                    WTHEXS-DST-LEN
******************************************************************
 V400-WT-HEX-STRING SECTION.
 V400-00.
     MOVE SPACES TO WTHEXS-DST
     ENTER TAL "WT^HEX^STRING" USING WTHEXS-SRC WTHEXS-SRC-LEN
                                     WTHEXS-DST WTHEXS-DST-LEN
     .
 V400-99.
     EXIT.




******************************************************************
* SQL-Fehlerbehandlung
******************************************************************
 Z001-SQLERROR SECTION.
 Z001-00.

**  ---> holen Daten für Fehlertabelle
     MOVE 1 TO ERR-STAT OF GEN-ERROR

     IF  IMSG-MDNR NUMERIC
         MOVE IMSG-MDNR TO MDNR OF GEN-ERROR
     ELSE
         MOVE ZERO      TO MDNR OF GEN-ERROR
     END-IF
     IF  IMSG-TSNR NUMERIC
         MOVE IMSG-TSNR TO TSNR OF GEN-ERROR
     ELSE
         MOVE ZERO      TO TSNR OF GEN-ERROR
     END-IF

     MOVE K-MODUL TO MODUL-NAME OF GEN-ERROR
     MOVE "SE"    TO ERROR-KZ   OF GEN-ERROR

**  ---> Einstellen in Fehlertabelle
     PERFORM Z999-ERRLOG
     SET ENDE TO TRUE
     .
 Z001-99.
     EXIT.

******************************************************************
* Programm-Fehlerbehandlung
******************************************************************
 Z002-PROGERR SECTION.
 Z002-00.
**  ---> Angaben für Terminal- und Trace-Nummer in BUFFER5 einstellen
*G.02.01 - Fehlermeldung erweitert
     MOVE W-CARDID TO D-NUM2
     MOVE W-ROUTKZ TO D-NUM4
     STRING  "TermNr/TraceNr/RoutKZ/Verf/Cardid: "
             W-TERMNR "/" W-TRACENR "/" D-NUM4 "/"
             VERF-AS  "/" D-NUM2
                 delimited by size
       INTO  DATEN-BUFFER5
     END-STRING
*G.02.01 - Ende

**  ---> holen Daten für Fehlertabelle
     MOVE 1 TO ERR-STAT OF GEN-ERROR

     IF  IMSG-MDNR NUMERIC
         MOVE IMSG-MDNR TO MDNR OF GEN-ERROR
     ELSE
         MOVE ZERO      TO MDNR OF GEN-ERROR
     END-IF
     IF  IMSG-TSNR NUMERIC
         MOVE IMSG-TSNR TO TSNR OF GEN-ERROR
     ELSE
         MOVE ZERO      TO TSNR OF GEN-ERROR
     END-IF

     MOVE K-MODUL TO MODUL-NAME OF GEN-ERROR
     MOVE "PE"    TO ERROR-KZ   OF GEN-ERROR

**  ---> Einstellen in Fehlertabelle
     PERFORM Z999-ERRLOG
     .
 Z002-99.
     EXIT.

******************************************************************
* Fehler in Tabelle ERRLOG schreiben
******************************************************************
 Z999-ERRLOG SECTION.
 Z999-00.
**  ---> Einstellen in Fehlertabelle
     CALL "WSYS022" USING GEN-ERROR
                          SQLCA
     INITIALIZE GEN-ERROR
     .
 Z999-99.
      EXIT.

******************************************************************
* ENDE Source-Programm
******************************************************************
